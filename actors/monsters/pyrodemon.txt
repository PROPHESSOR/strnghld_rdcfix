ACTOR PyroDemon 30105
{ 
//$Category Stronghold_Monsters
PainChance "Stunner", 128
spawnid 246
Health 2500 // originally 2000
Radius 24
Height 64
Mass 1000
Speed 8
PainChance 20
MONSTER
MeleeDamage 20
OBITUARY "$STR_OB_PYRODEMON"
HITOBITUARY "$STR_OB_PYRODEMONHIT"
Tag "$STR_MONSTERTAG_PYRODEMON"
Bloodcolor "75 25 00"
+FloorClip
+MissileMore
+NoRadiusDMG
+Boss
+DontHurtSpecies
//+DONTRIP
+DONTMORPH
+NOICEDEATH
SEESOUND "pyro/see"
PAINSOUND "pyro/pain"
DEATHSOUND "pyro/death"
ACTIVESOUND "pyro/idle"
states 
   { 
   Spawn:
       PYRO AB 10 A_Look
       loop
   See:
       PYRO AA 3 A_Chase
       TNT1 A 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       PYRO BB 3 A_Chase
       TNT1 B 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       PYRO CC 3 A_Chase
       TNT1 C 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       PYRO DD 3 A_Chase
       TNT1 D 0 A_SpawnItemEx("WalkFire",0,0,0,0,0,0,0,128)
       loop
   Missile:
       TNT1 A 0 A_Jump(32,"Teleport")
       TNT1 A 0 A_Jump(32,"Missile3")
       PYRO EF 8 bright A_FaceTarget
       TNT1 A 0 A_CustomMissile("FireShot",28,0,0,0)
       TNT1 A 0 A_CustomMissile("FireShot",28,0,-4,0)
       TNT1 A 0 A_CustomMissile("FireShot",28,0,4,0)
       PYRO GH 3 bright
       TNT1 A 0 A_Jump(32,"Missile2")
       PYRO IJ 8 bright A_FaceTarget
       TNT1 A 0 A_CustomMissile("FireShot",28,0,0,0)
       TNT1 A 0 A_CustomMissile("FireShot",28,0,-4,0)
       TNT1 A 0 A_CustomMissile("FireShot",28,0,4,0)
       PYRO KL 3 bright
       TNT1 A 0 A_Jump(32,"Missile2")
       goto See
   Missile2:
       PYRO MN 8 bright A_FaceTarget
       TNT1 A 0 A_CustomMissile("PyroBlast",28,0,0,0)
       PYRO OP 4 bright
       goto See
   Missile3:
       PYRO MN 8 bright A_FaceTarget
       PYRO O 4 bright A_CustomMissile("RainCometNull",28,0,0,0)
       PYRO O 4 bright A_CustomMissile("RainCometNull",28,0,90,0)
       PYRO O 4 bright A_CustomMissile("RainCometNull",28,0,180,0)
       PYRO O 4 bright A_CustomMissile("RainCometNull",28,0,270,0)
       PYRO P 4 bright
       goto See
   Teleport:
	TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_CustomMissile("EscapeExplosion",28,10,0,0)
        TNT1 A 0 A_GiveInventory("StrAMushroomBypassOn")
        TNT1 A 0 A_Mushroom("PyroBall",16)
        TNT1 A 0 A_GiveInventory("StrAMushroomBypassOff")
       PYRO OP 3 bright
    TNT1 A 0 A_UnSetShootable
    PYRO A 1 A_PlaySound("pyro/tele")
    PYRO A 1 A_SetTranslucent(0.90)
    PYRO A 1 A_SetTranslucent(0.80)
    PYRO A 1 A_SetTranslucent(0.70)
    PYRO A 1 A_SetTranslucent(0.60)
    PYRO A 1 A_SetTranslucent(0.50)
    PYRO A 1 A_SetTranslucent(0.40)
    PYRO A 1 A_SetTranslucent(0.30)
    PYRO A 1 A_SetTranslucent(0.20)
    PYRO A 1 A_SetTranslucent(0.10)
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1)
    PYRO A 1 A_PlaySound("pyro/tele")
    PYRO A 1 A_SetTranslucent(0.10)
    PYRO A 1 A_SetTranslucent(0.20)
    PYRO A 1 A_SetTranslucent(0.30)
    PYRO A 1 A_SetTranslucent(0.40)
    PYRO A 1 A_SetTranslucent(0.50)
    PYRO A 1 A_SetTranslucent(0.60)
    PYRO A 1 A_SetTranslucent(0.70)
    PYRO A 1 A_SetTranslucent(0.80)
    PYRO A 1 A_SetTranslucent(0.90)
    PYRO A 1 A_SetTranslucent(1.0)
    TNT1 A 0 A_CustomMissile("EscapeExplosion",28,10,0,0)
    TNT1 A 0 A_GiveInventory("StrAMushroomBypassOn")
    TNT1 A 0 A_Mushroom("PyroBall",16)
    TNT1 A 0 A_GiveInventory("StrAMushroomBypassOff")
    TNT1 A 0 A_SetShootable
       goto See
   Pain: 
       PYRO Q 2 
       PYRO Q 2 A_Pain 
       Goto See
   Pain.Stunner:
       PYRO Q 2 A_ChangeFlag("NoPain", 1)
       PYRO Q 300 A_Pain
       PYRO Q 0 A_ChangeFlag("NoPain", 0)
       goto See
   Death:
       TNT1 A 0 A_GiveToTarget("AwardItem", 36)
       TNT1 A 0 A_Scream
       PYRO RST 20 bright
       PYRO U 20 bright A_NoBlocking
       PYRO VWX 6 bright
       PYRO Y 3 bright
       PYRO Z 3000
       PYRO Z 2 A_FadeOut(0.1)
       wait
     } 
}

ACTOR FireShot
{
   Radius 3
   Height 3
   Speed 15
   Damage 3
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.67
   DamageType "Fire"
   SeeSound "pyro/flame"
   DeathSound "pyro/flamepoof"
   Decal RevenantScorch
   States
   {
   Spawn:
      FSHT AAAAAA 1 Bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
      Loop
   Death:
      TNT1 A 0 A_Explode
      FSHT BCDE 3 Bright
      Stop
   }
}

actor FireTail
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.4
    States
    {
    Spawn:
        FRTB ABCDEFGHI 1
        Stop
    }
}

ACTOR EscapeExplosion
{
    SeeSound "pyro/explosion"
    Radius 8
    Height 56
    Speed 0
    DamageType "Fire"
    +NOGRAVITY
    States
    {
    Spawn:
        NKXP B 0 bright
        NKXP B 3 bright A_Explode(128,128,0) // A_Mushroom explosion
        NKXP C 3 bright
        NKXP D 3 bright A_Explode(50,192,0)
        NKXP EFGHIJKLMNOPQRSTUV 3 bright
        stop
    }
}

ACTOR PyroBall
{
   Radius 10
   Height 8
   Speed 15
   BounceType "Doom"
   BOUNCESOUND "pyro/bomb"
   DEATHSOUND "pyro/bombhit"
   PROJECTILE  
   RENDERSTYLE Add
   Alpha 0.7
   DamageType "Fire"
   Decal RevenantScorch
   -NoGravity
   +LowGravity
   +SeekerMissile
   states
   {
   Spawn:
      RBAL A 0 bright
      RBAL A 0 bright A_AlertMonsters
      RBAL AAAABBBB 1 bright
      Goto Spawn+2
   Death:
      TNT1 A 0 A_NoGravity
      RBAL DEFGHI 4 bright
      Stop
   }
}

ACTOR FirebombExplosion1
{
   Radius 11
   Height 8
   Speed 18
   Damage 6
   +NOBLOCKMAP
   +DROPOFF
   +MISSILE
   +ACTIVATEIMPACT
   +ACTIVATEPCROSS
   DamageType "Fire"
   Obituary "$STR_OB_PYRODEMONFIREBOMB"
   States
   {
   Spawn:
      FRME A 1 Bright
      Loop
   Death:
      TNT1 A 0 A_NoGravity
      TNT1 A 0 A_PlaySound("pyro/blastexplosion")
      FRME A 3 bright A_Explode(128,144,0)
      TNT1 A 0 A_GiveInventory("StrAMushroomBypassOn")
      TNT1 A 0 A_Mushroom("PyroBall",8,MSF_DontHurt)
      TNT1 A 0 A_GiveInventory("StrAMushroomBypassOff")
      TNT1 A 0 A_SetDamageType("Fire")
      TNT1 A 0 A_Explode(128,128,0) // A_Mushroom explosion
      TNT1 A 0 A_CustomMissile("FirebombExplosion2",25,0,180,4)
      FRME BCDEFGHIJKLMNOPQRS 3 bright
      Stop
   }
}

ACTOR FirebombExplosion2 : FirebombExplosion1
{
   States
   {
   Death:
      TNT1 A 0 A_NoGravity
      TNT1 A 0 A_PlaySound("pyro/blastexplosion")
      FRME A 3 bright A_Explode(128,136,0)
      TNT1 A 0 A_CustomMissile("FirebombExplosion3",20,0,180,4)
      FRME BCDEFGHIJKLMNOPQRS 3 bright
      Stop
   }
}

ACTOR FirebombExplosion3 : FirebombExplosion1
{
   Speed 14
   Damage 5
   States
   {
   Death:
      TNT1 A 0 A_NoGravity
      TNT1 A 0 A_PlaySound("pyro/blastexplosion")
      TNT1 A 0 A_Explode(128,128,0)
      FRME ABCDEFGHIJKLMNOPQRS 3 bright
      Stop
   }
}

ACTOR PyroBlast
{
   PROJECTILE
   +SeekerMissile
   +NoGravity
   RENDERSTYLE Add
   Alpha 0.4
   SEESOUND "pyro/blast"
   DEATHSOUND "pyro/flamepoof"
   Speed 17
   Height 10
   Radius 10
   DamageType "Fire"
   Decal Scorch
   states
   {
   Spawn:
    SBLL A 0 bright
    SBLL ABCDEFGHIJ 1 bright A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
    TNT1 A 0 A_SeekerMissile(0,2)
    Goto Spawn+1
   Death:
    TNT1 A 0 A_CustomMissile("FirebombExplosion1",1,0,45,4)
    TNT1 A 0 A_CustomMissile("FirebombExplosion1",1,0,135,4)
    TNT1 A 0 A_CustomMissile("FirebombExplosion1",1,0,225,4)
    TNT1 A 0 A_CustomMissile("FirebombExplosion1",1,0,315,4)
    SBLL KLMNOPQRSTUVW 1 bright
    Stop
   }
}

ACTOR WalkFire
{
     +NOCLIP
     +MISSILE
     +DROPOFF
     +RANDOMIZE
     Scale 1.5
     Speed 1
     RENDERSTYLE ADD
     ALPHA 0.67
     States
     {
     Spawn:
          TNT1 A 8
          FRTF ABC 3
          FRTF C 0 A_Jump(191,4)
          FRTF C 0 A_CustomMissile("WalkFire", 0, 24, 0)
          FRTF C 0 A_CustomMissile("WalkFire", 0, 0, 0)
          FRTF C 0 A_CustomMissile("WalkFire", 0, -24, 0)
          FRTF DEFGHIJKLMNO 3
          Stop
     }
}

ACTOR RainCometNull
{
  Radius 0
  Height 0
  Speed 8
  PROJECTILE
  +FloorClip
  +Ripper
  +DoomBounce
  +CeilingHugger
  +DontHurtSpecies
  states
  {
  Spawn:
    ZAP5 A 0 A_Jump(5,8)
    ZAP5 AAAAAA 1 A_Wander
    ZAP5 A 8 A_CustomMissile("FallingComet",-24,random(0,128),random(0,360),6,random(1,89))
    Loop
  Death:
    ZAP5 A 0
    Stop
  }
}

ACTOR FallingComet
{
    Radius 6
    Height 8
    Speed 5
    Damage 5
    DamageType "Fire"
    Scale .55
    PROJECTILE
    SEESOUND "pyro/comet"
    DEATHSOUND "pyro/cometdeath"
    Decal RevenantScorch
    -NoGravity
    -Float
    +FloorClip
    +DontHurtSpecies
    +SpawnCeiling
    states
    {
    Spawn:
        COMT AAAABBBBCCCC 1 BRIGHT A_SpawnItemEx("FireTail",0,0,0,0,0,0,0,128)
        Loop
    Death:
        TNT1 A 0 A_NoGravity
        COMT D 3 BRIGHT A_SpawnItemEx("CometDeathGlow", 0, 0, 0, 0, 0, 0, 0, 128)
        COMT E 3 BRIGHT A_Explode(20,128,0)
        COMT E 0 A_SpawnItemEx("CometDeath", 0, 0, 0, 0, 0, 0, 0, 128)
        Stop
    }
}

actor CometDeathGlow : FireTail
{
    Scale 2
    Alpha 0.6
    States
    {
    Spawn:
        FRTB ABCDEFGHI 3
        Stop
    }
}

actor CometDeath
{
    PROJECTILE
    RENDERSTYLE ADD
    +NOCLIP
    ALPHA 0.7
    States
    {
    Spawn:
        COMT FGHI 3
        Stop
    }
}
