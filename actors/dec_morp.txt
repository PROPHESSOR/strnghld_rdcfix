ACTOR MorphedPlayer : PlayerPawn
{
	Player.ForwardMove .4,.4
	Player.SideMove .4,.4
	Health 1500
	Radius 24
	Height 64
	Mass 2000
	PainChance 80
	Player.ColorRange 112, 127
	Player.DisplayName "Bruiser"
	player.damagescreencolor "FF 33 00"
	player.jumpz 0
	player.viewheight 64
	player.morphweapon "BruiserWeapon"
	DamageFactor "BruiserAttack",0
	DamageFactor "MarineAlly",0
	+NOSKIN
	-TELESTOMP
	States
	{
	Spawn:
		BRUS AB 10 Bright
		Loop
	See:
		BRUS ABCD 6 Bright
		Loop
	Missile:
		BRUS EFGHIJ 3
		Goto Spawn
	Melee:
		BRUS EFG 6 Bright
		Goto See
	Pain:
		BRUS N 5 Bright A_Pain
		Goto Spawn
	Death:
		BRUD A 6 Bright A_PlayerScream
		BRUD BCDEFG 4 Bright
		BRUD H 4 Bright A_NoBlocking
		BRUD IJKLMNOP 4 Bright
		BRUD W -1
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// BRUISER'S RANDOM ATTACK //////////////////////
//////////////////////////////////////////////////////////////////////////////////////

ACTOR BruiserWeapon : Weapon
{
	//$Category Stronghold_Weapons
	+CHEATNOTWEAPON
	+NOAUTOFIRE
	States
	{
	Ready:
		BHND A 1 Bright A_WeaponReady
		loop
	Deselect:
		"####" A 0 A_Lower
		"####" A 1 Bright A_Lower
		Loop
	Select:
		"####" A 0 A_Raise
		"####" A 1 Bright A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("BruiserAttackType",2,"Huggers")
		TNT1 A 0 A_JumpIfInventory("BruiserAttackType",1,"FireShower")
	Meteor:
		BHND IJK 3 Bright
		BHND L 3 Bright A_FireCustomMissile("BruiserBall")
		BHND Z 15 Bright
		TNT1 A 0 A_Refire
		Goto Ready
	FireShower:
		BHND BCD 4 Bright
		BHND E 1 Bright A_FireCustomMissile("BruiserBall2",20)
		BHND E 1 Bright A_FireCustomMissile("BruiserBall2",15)
		BHND E 1 Bright A_FireCustomMissile("BruiserBall2",10)
		BHND E 1 Bright A_FireCustomMissile("BruiserBall2",5)
		BHND F 1 Bright A_FireCustomMissile("BruiserBall2")
		BHND F 1 Bright A_FireCustomMissile("BruiserBall2",-5)
		BHND F 1 Bright A_FireCustomMissile("BruiserBall2",-10)
		BHND F 1 Bright
		BHND GHZ 4 Bright
		TNT1 A 0 A_Refire
		Goto Ready
	Huggers:
		BHND IJK 4 Bright
		TNT1 A 0 A_FireCustomMissile("BruiserFireSpawner",15)
		TNT1 A 0 A_FireCustomMissile("BruiserFireSpawner")
		BHND L 4 A_FireCustomMissile("BruiserFireSpawner",-15)
		BHND Z 15 Bright
		TNT1 A 0 A_Refire
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("BruiserAttackType",2,2)
		TNT1 A 0 A_GiveInventory("BruiserAttackType",1)
		Goto Ready
		TNT1 A 0 A_TakeInventory("BruiserAttackType")
		Goto Ready
	}
}

actor BruiserAttackType : Inventory
{
	Inventory.MaxAmount 2
}

ACTOR MorphedPlayer2 : MorphedPlayer
{
	Health 4000
	Mass 1500
	Player.DisplayName "Terminator"
	player.damagescreencolor "FF FF FF"
	player.morphweapon "TerminatorWeapon"
	DamageFactor "RedBlast",0
	DamageFactor "MarineAlly",0
	States
	{
	Spawn:
		TERM AB 10 Bright
		Loop
	See:
		TERM A 0 A_Playsound("Terminator/terstepA")
		TERM AABB 4
		TERM A 0 A_Playsound("Terminator/terstepA")
		TERM CCDD 4
		Loop
	Missile:
	Melee:
		TERM A 0 A_JumpIfInventory ("TerminatorAttackType", 2, "Attack3")
		TERM A 0 A_JumpIfInventory ("TerminatorAttackType", 1, "Attack2")
	Attack1:
		TERM E 5 BRIGHT
		TERM F 5 BRIGHT LIGHT("TerminatorAttack2")
		goto Spawn
	Attack2:
		TERM G 2 BRIGHT
		TERM H 2 BRIGHT LIGHT("TerminatorAttack1")
		goto Spawn
	Attack3:
		TERM J 6
		TERM K 6 BRIGHT LIGHT("TerminatorAttack3")
		Goto Spawn
	Pain:
		TERM L 3 Bright A_Pain
		Goto Spawn
	Death:
		TERM M 6 Bright Light("TerminatorDeath1") A_PlayerScream
		TERM N 4 Light("TerminatorDeath2")
		TERM O 4 Bright Light("TerminatorDeath3")
		TERM P 4 Bright Light("TerminatorDeath4")
		TERM Q 4 Bright Light("TerminatorDeath5")
		TERM R 0 A_SpawnItemEx("TerminatorHead", 15, 0, 90, 10, 0, 0, -170)
		TERM R 0 A_SpawnItemEx("TerminatorShoulder", 60, 0, 75, 8, 0, 0, -70)
		TERM R 4 Bright Light("TerminatorDeath6") A_SpawnItemEx("TerminatorArm", 60, 0, 0, 3, 0, 0, -90)
		TERM S 4 Bright Light("TerminatorDeath7")
		TERM T 4 Bright Light("TerminatorDeath8")
		TERM U 4 Bright Light("TerminatorDeath9")
		TERM V 4 Bright Light("TerminatorDeath10")
		TERM W 4 Light("TerminatorDeath11")
		TERM X 6 Light("TerminatorDeath12") A_Fall
		TERM Y 6 Light("TerminatorDeath13")
		TERM Z 1 A_Playsound("Terminator/terstepA")
		TERM [ -1
		Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// TERMINATORS'S RANDOM ATTACK //////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////

ACTOR TerminatorWeapon : BruiserWeapon
{
	//$Category Stronghold_Weapons
	+WEAPON.DONTBOB
	States
	{
	Ready:
		JUGS A 1 Bright A_WeaponReady
		loop
	Fire:
		JUGS A 0 A_JumpIfInventory("TerminatorAttackType",3,"RedMissile")
		JUGS A 0 A_JumpIfInventory("TerminatorAttackType",2,"RedStar")
		JUGS A 0 A_JumpIfInventory("TerminatorAttackType",1,"SuperChaingun")
	Blaster:
		JUGS A 4
		JUGS F 0 A_GunFlash
		JUGS F 2 Bright A_FireCustomMissile("TerminatorBlaster2",Random(250,-250)/100.0,0,15,6)
		JUGS B 2
		TNT1 A 0 A_Refire
		Goto Ready
	SuperChaingun:
		JUGS F 0 A_GunFlash
		JUGS F 0 A_PlaySound("Terminator/tershotB",CHAN_WEAPON)
		JUGS F 2 Bright A_FireBullets(20,10,10,5,"Terminatorpuff")
		JUGS A 2
		JUGS F 0 A_GunFlash
		JUGS F 0 A_PlaySound("Terminator/tershotB",CHAN_WEAPON)
		JUGS F 2 Bright A_FireBullets(20,10,10,5,"Terminatorpuff")
		JUGS B 2
		JUGS F 0 A_GunFlash
		JUGS F 0 A_PlaySound("Terminator/tershotB",CHAN_WEAPON)
		JUGS F 2 Bright A_FireBullets(20,10,10,5,"Terminatorpuff")
		JUGS A 2
		JUGS F 0 A_GunFlash
		JUGS F 0 A_PlaySound("Terminator/tershotB",CHAN_WEAPON)
		JUGS F 2 Bright A_FireBullets(20,10,10,5,"Terminatorpuff")
		JUGS B 2
		TNT1 A 0 A_Refire
		Goto Ready
	RedStar:
		JUGS AB 3
		JUGS F 0 A_GunFlash
		JUGS F 2 Bright A_FireCustomMissile("RedStar2",Random(15,-15)/10.0,0,-15,6)
		JUGS AB 2
		JUGS AB 3
		JUGS F 0 A_GunFlash
		JUGS F 2 Bright A_FireCustomMissile("RedStar2",Random(15,-15)/10.0,0,15,6)
		JUGS AB 2
		TNT1 A 0 A_Refire
		JUGS AB 2
		JUGS AB 3
		Goto Ready
	RedMissile:
		JUGS A 6
		JUGS F 0 A_GunFlash
		JUGS F 2 Bright A_FireCustomMissile("RedGrenade2",0,0,-15,6)
		JUGS AB 2
		JUGS ABAB 3
		TNT1 A 0 A_Refire
		Goto Ready
	AltFire:
		JUGS A 5
		JUGS E 12
		JUGS A 0 A_JumpIfInventory("TerminatorAttackType",3,2)
		JUGS A 0 A_GiveInventory("TerminatorAttackType",1)
		Goto Ready
		JUGS A 1 A_TakeInventory("TerminatorAttackType",4)
		Goto Ready
	Flash:
		TNT1 A 1 A_Light1
		TNT1 A 1 A_Light2
		Goto LightDone
	}
}

ACTOR TerminatorBlaster2 : TerminatorBlaster
{
	Damage 10
	States
	{
	Death:
		BLST B 6 Bright LIGHT("TerminatorBlaster2") A_Explode(64,128,0,0,96)
		Goto Super::Death+1
	}
}

ACTOR Redstar2 : Redstar
{
	Damage 8
	States
	{
	Spawn:
		STAX A 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX A 1 Bright LIGHT("RedStar") A_SpawnItemEx("grenadetrail")
		STAX A 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX A 1 Bright LIGHT("RedStar") A_SpawnItemEx("grenadetrail")
		STAX A 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX A 1 Bright LIGHT("RedStar") A_SpawnItemEx("grenadetrail")
		STAX A 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX A 1 Bright LIGHT("RedStar") A_SpawnItemEx("grenadetrail")
		STAX B 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX B 1 Bright LIGHT("RedStar2") A_SpawnItemEx("grenadetrail")
		STAX B 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX B 1 Bright LIGHT("RedStar2") A_SpawnItemEx("grenadetrail")
		STAX B 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX B 1 Bright LIGHT("RedStar2") A_SpawnItemEx("grenadetrail")
		STAX B 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX B 1 Bright LIGHT("RedStar2") A_SpawnItemEx("grenadetrail")
		STAX C 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX C 1 Bright LIGHT("RedStar3") A_SpawnItemEx("grenadetrail")
		STAX C 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX C 1 Bright LIGHT("RedStar3") A_SpawnItemEx("grenadetrail")
		STAX C 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX C 1 Bright LIGHT("RedStar3") A_SpawnItemEx("grenadetrail")
		STAX C 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX C 1 Bright LIGHT("RedStar3") A_SpawnItemEx("grenadetrail")
		STAX D 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX D 1 Bright LIGHT("RedStar4") A_SpawnItemEx("grenadetrail")
		STAX D 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX D 1 Bright LIGHT("RedStar4") A_SpawnItemEx("grenadetrail")
		STAX D 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX D 1 Bright LIGHT("RedStar4") A_SpawnItemEx("grenadetrail")
		STAX D 0 A_SeekerMissile(2,10,SMF_LOOK)
		STAX D 1 Bright LIGHT("RedStar4") A_SpawnItemEx("grenadetrail")
		Loop
	Death:
		STAX E 3 Bright LIGHT("RedStar5") A_Explode(80,160,0,0,120)
		Goto Super::Death+1
	}
}

ACTOR RedGrenade2 : Redgrenade
{
	Damage 8
	-ForceRadiusDMG
	States
	{
	Death:
		STAR E 0 A_custommissile("grenadeblast2",0,0,user_theta,2,0)
		STAR E 0 A_SetUserVar("user_theta",user_theta+20)
		STAR E 0 A_JumpIf(user_theta>=360,1)
		Loop
		STAR EFGHIJKLMNO 3 Bright
		Stop
	}
}

ACTOR grenadeblast2 : Grenadeblast
{
	Speed 8
	Damage 2
	DamageType Normal
	-FORCERADIUSDMG
	-NODAMAGETHRUST
}

actor TerminatorAttackType : Inventory
{
	Inventory.MaxAmount 4
}