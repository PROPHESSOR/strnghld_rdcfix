ACTOR RocketGuy 3202
{
//$Category Stronghold_Monsters
   PainChance "Stunner", 255
   spawnid 244
   Health 50
   Radius 20
   Height 56
   Mass 100
   Speed 8
   PainChance 200
   Monster
   +FLOORCLIP
   SeeSound "grunt/sight"
   PainSound "grunt/pain"
   Deathsound "grunt/death"
   ActiveSound "grunt/active"
   DropItem RocketAmmo
   Obituary "$STR_OB_ROCKETGUY"
   Tag "$STR_MONSTERTAG_ROCKETGUY"
   States
   {
   Spawn:
      MPOS AB 10 A_Look
      Loop
   See:
      MPOS AABBCCDD 3 A_Chase
      Loop
   Missile:
      MPOS EE 10 A_FaceTarget
      MPOS F 5
      MPOS G 13 A_CyberAttack
      Goto See
   Pain:
      MPOS H 2
      MPOS H 2 A_Pain
      Goto See
   Pain.Stunner:
       MPOS H 2 A_ChangeFlag("NoPain", 1)
       MPOS H 300 A_Pain
       MPOS H 0 A_ChangeFlag("NoPain", 0)
       goto See
   Death:
      TNT1 A 0 A_GiveToTarget("AwardItem", 3)
      MPOS I 5
      MPOS J 5 A_Scream
      MPOS K 5
      MPOS L 5 A_Fall
      MPOS M 3000 CanRaise
      MPOS M 2 A_FadeOut(0.1)
      wait
   XDeath:
      TNT1 A 0 A_GiveToTarget("AwardItem", 5)
      MPOS N 5
      MPOS O 5 A_XScream
      MPOS P 5
      MPOS Q 5 A_Fall
      MPOS RSTU 5
      MPOS V 3000 CanRaise
      MPOS V 2 A_FadeOut(0.1)
      wait
   Raise:
      MPOS MLKJI 5
      goto See
   }
}

ACTOR PlasmaZombie 3205
{
    Health 50
    Radius 20
    Height 56
    Speed 8
    PainChance 100
    PainChance "Stunner", 255
    MONSTER 
    +FLOORCLIP
    SeeSound "grunt/sight"
    AttackSound "grunt/attack"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    Obituary "$STR_OB_PLASMAZOMBIE"
    Tag "$STR_MONSTERTAG_PLASMAZOMBIE"
    DropItem Cell
    MissileType "PlasmaBall"
    States
    {
    Spawn:
        ZMAY AB 10 A_Look
        Loop
    See:
        ZMAY AABBCCDD 4 A_Chase
        Loop
    Missile:
        ZMAY E 10 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 5 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        Goto See
    Pain:
        ZMAY G 3
        ZMAY G 3 A_Pain
        goto See
    Pain.Stunner:
        ZMAY G 2 A_ChangeFlag("NoPain", 1)
        ZMAY G 300 A_Pain
        ZMAY G 0 A_ChangeFlag("NoPain", 0)
        goto See
    Death:
        ZMAY H 5 A_GiveToTarget("AwardItem", 3)
        ZMAY I 5 A_Scream
        ZMAY J 5 A_Fall
        ZMAY K 5
        ZMAY L 3000 CanRaise
        ZMAY L 2 A_FadeOut(0.1)
        wait
    XDeath:
        ZMAY O 5 A_GiveToTarget("AwardItem", 5)
        ZMAY P 5 A_XScream
        ZMAY Q 5 A_Fall
        ZMAY RSTU 5
        ZMAY V 3000 CanRaise
        ZMAY V 2 A_FadeOut(0.1)
        wait
    Raise:
        ZMAY KJIH 5
        goto See
    }
}
