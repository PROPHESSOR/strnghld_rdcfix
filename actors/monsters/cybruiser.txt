ACTOR Cybruiser : StrongholdBaronOfHell 30128
{
	//$Category Stronghold_Monsters
	painchance "Stunner",160
	Health 2000
	PainChance 20
	Obituary "%o was blown away by a Cybruiser."
	HitObituary "%o was smashed by a Cybruiser."
	+MISSILEMORE
	+DONTHARMCLASS
	SeeSound "monster/brusit"
	DeathSound "monster/brudth"
	BloodColor "red"
	States
	{
	Spawn:
		BRU2 AB 10 A_Look
		Loop
	See:
		BRU2 A 1 A_Playsound("monster/bruwlk")
		BRU2 A 0 A_Chase
		BRU2 AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BRU2 A 1 A_Chase
		BRU2 AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BRU2 B 1 A_Chase
		BRU2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BRU2 B 1 A_Chase
		BRU2 BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BRU2 C 1 A_Playsound("monster/bruwlk")
		BRU2 C 0 A_Chase
		BRU2 CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BRU2 C 1 A_Chase
		BRU2 CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BRU2 D 1 A_Chase
		BRU2 DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		BRU2 D 1 A_Chase
		BRU2 DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		BRU2 E 8 A_FaceTarget
		BRU2 F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
		BRU2 E 12 A_FaceTarget
		BRU2 F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
		BRU2 E 12 A_FaceTarget
		BRU2 F 12 Bright A_CustomMissile("BruiserMissile",38,15,0,0)
		Goto See
	Melee:
		BRU2 GH 8 A_FaceTarget
		BRU2 I 8 A_CustomMeleeAttack(random(1,8)*10,"baron/melee")
		Goto See
	Pain:
		BRU2 J 2
		BRU2 J 2 A_Pain
		Goto See
	Pain.Stunner:
		BRU2 J 2 A_ChangeFlag("NoPain",1)
		BRU2 J 300 A_Pain
		BRU2 J 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		BRU2 K 6 Bright LIGHT("CybruiserDeath1") A_Scream
		BRU2 L 6 Bright LIGHT("CybruiserDeath2")
		BRU2 M 6 Bright LIGHT("CybruiserDeath3")
		BRU2 N 6 Bright LIGHT("CybruiserDeath4")
		BRU2 O 6 Bright LIGHT("CybruiserDeath5") A_NoBlocking
		BRU2 P 6 Bright LIGHT("CybruiserDeath6")
		BRU2 Q 6 Bright LIGHT("CybruiserDeath7")
		BRU2 R 6 Bright LIGHT("CybruiserDeath8")
		BRU2 S 6 Bright
		BRU2 T 3000 A_BossDeath
		BRU2 T 1 A_FadeOut(0.05)
		Wait
	Raise:
		Stop
	}
}

ACTOR BruiserMissile
{
	Radius 8
	Height 12
	Speed 20
	Damage 10
	PROJECTILE
	SeeSound "monster/brufir"
	DeathSound "weapons/hellex"
	DamageType Fire
	States
	{
	Spawn:
		FBRS A 2 Bright LIGHT("BruiserMissile") A_SpawnItemEx("BruiserTrail")
		loop
	Death:
		BAL3 C 6 Bright LIGHT("BruiserMissile2") A_SetTranslucent(0.67,1)
		BAL3 D 6 Bright LIGHT("BruiserMissile3") A_Explode(64,64,0)
		BAL3 E 6 Bright LIGHT("BruiserMissile4")
		stop
	}
}

ACTOR BruiserTrail
{
	+CLIENTSIDEONLY
	Radius 3
	Height 3
	RENDERSTYLE Translucent
	Alpha 0.67
	PROJECTILE
	States
	{
	Spawn:
		TNT1 A 3
		PUFF ABCD 4 BRIGHT
		Stop
	}
}