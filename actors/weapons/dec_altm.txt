///////////////////////////////
// ALTERED ORIGINAL MONSTERS //
///////////////////////////////

actor DemonStun : Demon replaces Demon
{
    PainChance "Stunner", 255
    FastSpeed 14
    Tag "$FN_DEMON"
	states // redefining states to ensure the See state works as intended
	{
    Spawn:
        SARG AB 10 A_Look
        loop
    See:
        SARG AABBCCDD 2 A_Chase
        loop
    Melee:
        SARG EF 8 fast A_FaceTarget
        SARG G 8 fast A_SargAttack
        goto See
    Pain:
        SARG H 2
        SARG H 2 A_Pain
        goto See
	Pain.Stunner:
		SARG H 2 A_ChangeFlag("NoPain", 1)
		SARG H 300 A_Pain
		SARG H 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 6)
		SARG I 8
		SARG J 8 A_Scream
		SARG K 4
		SARG L 4 A_NoBlocking
		SARG M 4
		SARG N 3000 CanRaise
		SARG N 2 A_FadeOut(0.1)
		wait
    Raise:
        SARG N 5
        SARG MLKJI 5
        goto See
	}
}

actor StealthDemonStun : DemonStun replaces StealthDemon
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHDEMON"
    HitObituary "$OB_STEALTHDEMON"
    +STEALTH
}

actor ZombieManStun : ZombieMan replaces ZombieMan
{
    PainChance "Stunner", 255
    Tag "$FN_ZOMBIE"
    States
    {
    Missile:
        POSS E 10 A_FaceTarget
        POSS F 8 bright A_PosAttack
        POSS E 8
        goto See
	Pain.Stunner:
		POSS G 2 A_ChangeFlag("NoPain", 1)
		POSS G 300 A_Pain
		POSS G 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 1)
		POSS H 5
		POSS I 5 A_Scream
		POSS J 5 A_NoBlocking
		POSS K 5
		POSS L 3000 CanRaise
		POSS L 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 3)
		POSS M 5
		POSS N 5 A_XScream
		POSS O 5 A_NoBlocking
		POSS PQRST 5
		POSS U 3000 CanRaise
		POSS U 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthZombieManStun : ZombieManStun replaces StealthZombieMan
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHZOMBIE"
    +STEALTH
}

actor ArachnotronStun : Arachnotron replaces Arachnotron
{
    PainChance "Stunner", 128
	Radius 48 // originally 64
	Height 56 // originally 64
    Tag "$FN_ARACH"
	states
	{
	Pain.Stunner:
		BSPI I 2 A_ChangeFlag("NoPain", 1)
		BSPI I 300 A_Pain
		BSPI I 0 A_ChangeFlag("NoPain", 0)
		goto See+1
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 12)
		BSPI J 20 A_Scream
		BSPI K 7 A_NoBlocking
		BSPI LMNO 7
		BSPI P 3000 CanRaise A_BossDeath
		BSPI P 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthArachnotronStun : ArachnotronStun replaces StealthArachnotron
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHBABY"
    +STEALTH
}

actor ArchvileStun : Archvile replaces Archvile
{
    PainChance "Stunner", 128
    Tag "$FN_ARCH"
	states
	{
    Missile:
        TNT1 A 0 A_VileStart
        VILE G 10 bright A_FaceTarget
        VILE H 8 bright A_VileTarget
        VILE IJKLMN 8 bright A_FaceTarget
        VILE O 8 bright A_VileAttack("vile/stop",20,70,70,1.0,"Fire",1)
        goto Super::Missile+10
	Pain.Stunner:
		VILE Q 2 A_ChangeFlag("NoPain", 1)
		VILE Q 300 A_Pain
		VILE Q 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 18)
		VILE Q 7
		VILE R 7 A_Scream
		VILE S 7 A_NoBlocking
		VILE TUVWXY 7
		VILE Z 3000
		VILE Z 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthArchvileStun : ArchvileStun replaces StealthArchvile
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHVILE"
    +STEALTH
}

actor BaronOfHellStun : BaronOfHell replaces BaronOfHell
{
    PainChance "Stunner", 96
    BloodColor "66 FF 00"
    Tag "$FN_BARON"
	states
	{
	Pain.Stunner:
		BOSS H 2 A_ChangeFlag("NoPain", 1)
		BOSS H 300 A_Pain
		BOSS H 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 15)
		BOSS I 8
		BOSS J 8 A_Scream
		BOSS K 8
		BOSS L 8 A_NoBlocking
		BOSS MN 8
		BOSS O 3000 CanRaise A_BossDeath
		BOSS O 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthBaronOfHellStun : BaronOfHellStun replaces StealthBaron
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHBARON"
    HitObituary "$OB_STEALTHBARON"
    +STEALTH
}

actor HellKnightStun : HellKnight replaces HellKnight
{
    PainChance "Stunner", 96
    BloodColor "66 FF 00"
    Tag "$FN_HELL"
	states
	{
	Pain.Stunner:
		BOS2 H 2 A_ChangeFlag("NoPain", 1)
		BOS2 H 300 A_Pain
		BOS2 H 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 11)
		BOS2 I 8
		BOS2 J 8 A_Scream
		BOS2 K 8
		BOS2 L 8 A_NoBlocking
		BOS2 MN 8
		BOS2 O 3000 CanRaise
		BOS2 O 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthHellKnightStun : HellKnightStun replaces StealthHellKnight
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHKNIGHT"
    HitObituary "$OB_STEALTHKNIGHT"
    +STEALTH
}

actor ShotgunGuyStun : ShotgunGuy replaces ShotgunGuy
{
    PainChance "Stunner", 255
    Tag "$FN_SHOTGUN"
	states
	{
	Pain.Stunner:
		SPOS G 2 A_ChangeFlag("NoPain", 1)
		SPOS G 300 A_Pain
		SPOS G 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 2)
		SPOS H 5
		SPOS I 5 A_Scream
		SPOS J 5 A_NoBlocking
		SPOS K 5
		SPOS L 3000 CanRaise
		SPOS L 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 4)
		SPOS M 5
		SPOS N 5 A_XScream
		SPOS O 5 A_NoBlocking
		SPOS PQRST 5
		SPOS U 3000 CanRaise
		SPOS U 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthShotgunGuyStun : ShotgunGuyStun replaces StealthShotgunGuy
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHSHOTGUNGUY"
    +STEALTH
}

actor CacodemonStun : Cacodemon replaces Cacodemon
{
    PainChance "Stunner", 200
    BloodColor "00 44 FF"
    Tag "$FN_CACO"
	states
	{
	Pain.Stunner:
		HEAD E 2 A_ChangeFlag("NoPain", 1)
		HEAD E 300 A_Pain
		HEAD E 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 9)
		HEAD G 8
		HEAD H 8 A_Scream
		HEAD IJ 8
		HEAD K 8 A_NoBlocking
		HEAD L 3000 CanRaise A_SetFloorClip
		HEAD L 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthCacodemonStun : CacodemonStun replaces StealthCacodemon
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHCACO"
    HitObituary "$OB_STEALTHCACO"
    +STEALTH
}

actor CacolanternBall : CacodemonBall
{
  Speed 20
  Decal CacoScorch
  States
  {
  Spawn:
    BAL8 AB 4 Bright
    loop
  Death:
    BAL8 CDE 6 Bright
    Stop
  }
}

actor NightmareCaco : CacodemonStun
{
  Health 700
  RenderStyle Translucent
  Alpha 0.75
  MeleeDamage 9
  MissileType "CacolanternBall"
  BloodColor Red
  Translation "168:191=192:207", "192:207=168:191", "16:31=192:199", "32:63=200:207"
  Obituary "$STR_OB_NIGHTMARECACO"
  HitObituary "$STR_OB_NIGHTMARECACOHIT"
  Tag "$STR_MONSTERTAG_NIGHTMARECACO"
  States
  {
  Spawn:
    HEAD A 10 A_Look
    loop
  See:
    HEAD A 2 A_Chase
    loop
  Missile:
    HEAD B 0 A_Jump(128,1)
    Goto See
    HEAD BC 3 A_FaceTarget
    HEAD D 4 bright A_ComboAttack
    HEAD BC 3 A_FaceTarget
    HEAD D 4 bright A_ComboAttack
    HEAD BC 3 A_FaceTarget
    HEAD D 4 bright A_ComboAttack
    Goto See
  Pain:
    HEAD E 3
    HEAD E 3 A_Pain
    HEAD F 6
    Goto See
  Pain.Stunner:
    HEAD E 2 A_ChangeFlag("NoPain", 1)
    HEAD E 300 A_Pain
    HEAD E 0 A_ChangeFlag("NoPain", 0)
    goto See
  Death:
    TNT1 A 0 A_GiveToTarget("AwardItem", 5)
    goto Super::Death
  Raise:
    HEAD L 8 A_UnSetFloorClip
    HEAD KJIHG 8
    goto See
  }
}

actor CyberdemonStun : Cyberdemon replaces Cyberdemon
{
    PainChance "Stunner", 96
    Tag "$FN_CYBER"
    //+DONTRIP
	states
	{
	Pain.Stunner:
		CYBR G 2 A_ChangeFlag("NoPain", 1)
		CYBR G 300 A_Pain
		CYBR G 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 40)
		CYBR H 10
		CYBR I 10 A_Scream
		CYBR JKL 10
		CYBR M 10 A_NoBlocking
		CYBR NO 10
		CYBR P 30
		CYBR P 3000 A_BossDeath
		CYBR P 2 A_FadeOut(0.1)
		wait
	}
}

actor StrRocket : Rocket replaces Rocket
{
    Decal Scorch
    DamageType "Rocket" // get that fire stuff outta here.
    -DEHEXPLOSION
    States
    {
      Death:
        TNT1 A 0 A_SetTranslucent(0.67,1)
        goto Super::Death
    }
}

actor SpectreStun : DemonStun replaces Spectre
{
    PainChance "Stunner", 255
    SeeSound "spectre/sight"
    AttackSound "spectre/melee"
    PainSound "spectre/pain"
    DeathSound "spectre/death"
    ActiveSound "spectre/active"
	hitobituary "$OB_SPECTREHIT"
    Tag "$FN_SPECTRE"
	renderstyle OptFuzzy
	alpha 0.5
    +SHADOW
}

actor ChaingunGuyStun : ChaingunGuy replaces ChaingunGuy
{
	Health 60 // originally 70
    PainChance "Stunner", 255
    Tag "$FN_HEAVY"
	states
	{
	Pain.Stunner:
		CPOS G 2 A_ChangeFlag("NoPain", 1)
		CPOS G 300 A_Pain
		CPOS G 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 3)
		CPOS H 5
		CPOS I 5 A_Scream
		CPOS J 5 A_NoBlocking
		CPOS KLM 5
		CPOS N 3000 CanRaise
		CPOS N 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 6)
		CPOS O 5
		CPOS P 5 A_XScream
		CPOS Q 5 A_NoBlocking
		CPOS RS 5
		CPOS T 3000 CanRaise
		CPOS T 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthChaingunGuyStun : ChaingunGuyStun replaces StealthChaingunGuy
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHCHAINGUY"
    +STEALTH
}

actor DoomImpStun : DoomImp replaces DoomImp
{
    PainChance "Stunner", 255
    Tag "$FN_IMP"
	states
	{
	Pain.Stunner:
		TROO H 2 A_ChangeFlag("NoPain", 1)
		TROO H 300 A_Pain
		TROO H 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 3)
		TROO I 8
		TROO J 8 A_Scream
		TROO K 6
		TROO L 6 A_NoBlocking
		TROO M 3000 CanRaise
		TROO M 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 6)
		TROO N 5
		TROO O 5 A_XScream
		TROO P 5
		TROO Q 5 A_NoBlocking
		TROO RST 5
		TROO U 3000 CanRaise
		TROO U 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthDoomImpStun : DoomImpStun replaces StealthDoomImp
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHIMP"
    HitObituary "$OB_STEALTHIMP"
    +STEALTH
}

actor StrDoomImpBall : DoomImpBall replaces DoomImpBall { Decal DoomImpScorch DamageType "Fire" }

actor FatsoStun : Fatso replaces Fatso
{
    PainChance "Stunner", 128
    Tag "$FN_MANCU"
	states
	{
	Pain.Stunner:
		FATT J 2 A_ChangeFlag("NoPain", 1)
		FATT J 300 A_Pain
		FATT J 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 12)
		FATT K 6
		FATT L 6 A_Scream
		FATT M 6 A_NoBlocking
		FATT NOPQRS 6
		FATT T 3000 CanRaise A_BossDeath
		FATT T 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthFatsoStun : FatsoStun replaces StealthFatso
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHFATSO"
    +STEALTH
}

actor StrFatShot : FatShot replaces FatShot { Decal Scorch DamageType "Fire" }

actor LostSoulStun : LostSoul replaces LostSoul
{
    PainChance "Stunner", 200
	+NOBLOOD
    Tag "$FN_LOST"
	states
	{
	Pain.Stunner:
		SKUL E 2 bright A_ChangeFlag("NoPain", 1)
		SKUL E 300 bright A_Pain
		SKUL E 0 bright A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 4)
		goto Super::Death
	}
}

actor PainElementalStun : PainElemental replaces PainElemental
{
    PainChance "Stunner", 160
    Tag "$FN_PAIN"
	states
	{
	Pain.Stunner:
		PAIN G 2 A_ChangeFlag("NoPain", 1)
		PAIN G 300 A_Pain
		PAIN G 0 A_ChangeFlag("NoPain", 0)	// A_SpawnItemEx
		goto See				// (huh? What's the above comment supposed to mean?)
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 10)
		goto Super::Death
	}
}

actor RevenantStun : Revenant replaces Revenant
{
    PainChance "Stunner", 160
    Tag "$FN_REVEN"
	states
	{
	Missile:				//Needed to prevent the Revenant seeker from playing ricochet sounds.
   		TNT1 A 0 A_FaceTarget
   		SKEL J 10 bright A_FaceTarget
		TNT1 A 0 A_Jump(128, "Missile2")	//It's a bit messy, but it works :P
    		SKEL K 10 A_CustomMissile("NewRevenantMissile",48,0,0,1)         
    		SKEL K 10 A_FaceTarget
    		goto See
    Missile2:
    		SKEL K 10 A_CustomMissile("NewRevenantSeeker",48,0,0,1)         
    		SKEL K 10 A_FaceTarget
		goto See
    Melee:
        TNT1 A 0 A_FaceTarget
        SKEL G 6 A_SkelWhoosh
        SKEL H 6 A_FaceTarget
        SKEL I 6 A_SkelFist
        goto See
	Pain.Stunner:
		SKEL L 2 A_ChangeFlag("NoPain", 1)
		SKEL L 300 A_Pain
		SKEL L 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 12)
		SKEL LM 7
		SKEL N 7 A_Scream
		SKEL O 7 A_NoBlocking
		SKEL P 7
		SKEL Q 3000 CanRaise
		SKEL Q 2 A_FadeOut(0.1)
		wait
	}
}

actor StealthRevenantStun : RevenantStun replaces StealthRevenant
{
    RenderStyle Translucent
    Alpha 0
    Obituary "$OB_STEALTHUNDEAD"
    HitObituary "$OB_STEALTHUNDEAD"
    +STEALTH
}

actor NewRevenantMissile : RevenantTracer
{
    Decal RevenantScorch
    DamageType "Rocket"
    States
    {
      Spawn:
        FATB AB 2 Bright
        loop
    }
}

actor NewRevenantSeeker : NewRevenantMissile
{
    States
    {
      Spawn:
        FATB A 0 bright
        FATB AB 2 bright A_SeekerMissile(4,8)
        TNT1 A 0 A_SpawnItemEx("NewSeekerSmoke",0,0,0,0,0,0,180,128)
        TNT1 A 0 A_SpawnItemEx("RevenantTracerSmoke",0,0,0,0,0,0,180,128)
        goto Spawn+1
    }
}

actor NewSeekerSmoke : BulletPuff
{
    States
    {
      Spawn:
        PUFF AB 4 bright
        goto Super::Melee
    }
}

actor SpiderMastermindStun : SpiderMastermind replaces SpiderMastermind
{
    Radius 80 // originally 128
    Height 80 // originally 100
    PainChance "Stunner", 96
    Tag "$FN_SPIDER"
    +MISSILEEVENMORE
    //+DONTRIP
	States
	{
	Pain.Stunner:
		SPID I 2 A_ChangeFlag("NoPain", 1)
		SPID I 300 A_Pain
		SPID I 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 45)
		SPID J 20 A_Scream
		SPID K 10 A_NoBlocking
		SPID LMNOPQR 10
		SPID S 30
		SPID S 3000 A_BossDeath
		SPID S 2 A_FadeOut(0.1)
		wait
	}
}

actor WolfensteinSSStun : WolfensteinSS replaces WolfensteinSS
{
    PainChance "Stunner", 255
    Tag "$FN_WOLFSS"
	states
	{
	Pain.Stunner:
		SSWV H 2 A_ChangeFlag("NoPain", 1)
		SSWV H 300 A_Pain
		SSWV H 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 2) // huhm...
        SSWV I 5
        SSWV J 5 A_Scream
        SSWV K 5 A_NoBlocking
        SSWV L 5
        SSWV M 3000 CanRaise
        SSWV M 2 A_FadeOut(0.1)
        wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 4)
        SSWV N 5
        SSWV O 5 A_XScream
        SSWV P 5 A_NoBlocking
        SSWV QRSTU 5
        SSWV V 3000 CanRaise
        SSWV V 2 A_FadeOut(0.1)
        wait
	}
}

//////////////////////////////////////////////
////////// PSEUDO-SKULLTAG MONSTERS //////////
//////////////////////////////////////////////
actor Abaddon : CacodemonStun 5015
{
    Game Doom
    Health 1200
    SpawnID 220
    Speed 12
    PainChance 40
    BloodColor Red
    Translation "170:191=[81,81,81]:[0,0,0]", "16:47=[120,120,120]:[0,0,0]", "164:167=108:108", "217:223=107:109", "112:123=160:167", "124:125=77:78", "126:127=1:2", "192:207=168:187", "240:247=188:191", "48:79=96:111", "144:151=176:191"
    Obituary "$STR_OB_ABADDON"
    HitObituary "$STR_OB_ABADDONHIT"
    Tag "$STR_MONSTERTAG_ABADDON"
    //+DONTRIP
    States
    {
    Missile:
      HEAD BC 3 A_FaceTarget
      HEAD D 4 Bright A_BasicAttack(10, "", "HadesBall", 32)
      HEAD BC 3 A_FaceTarget
      HEAD D 4 Bright A_BasicAttack(10, "", "HadesBall", 32)
      Goto See
  Death:
    TNT1 A 0 A_GiveToTarget("AwardItem", 8)
    goto Super::Death
    }
}

actor Belphegor : BaronOfHellStun 5008
{
    Game Doom
    SpawnID 215
    Health 1500
    PainChance 25
    Obituary "$STR_OB_BELPHEGOR"
    HitObituary "$STR_OB_BELPHEGORHIT"
    Translation "16:47=176:191", "48:79=[80,80,80]:[0,0,0]"
    Tag "$STR_MONSTERTAG_BELPHEGOR"
    //+DONTRIP
    States
    {
    Melee:
    Missile:
      BOSS EF 6 A_FaceTarget
      BOSS G 6 A_BasicAttack(10, "baron/melee", "BaronBallFast", 32)
      BOSS EF 6 A_FaceTarget
      BOSS G 6 A_BasicAttack(10, "baron/melee", "BaronBallFast", 32)
      BOSS EF 6 A_FaceTarget
      BOSS G 6 A_BasicAttack(10, "baron/melee", "BaronBallFast", 32)
      Goto See
  Death:
    TNT1 A 0 A_GiveToTarget("AwardItem", 4)
    goto Super::Death
    }
}

actor BaronBallFast : BaronBall { Speed 20 }

actor Cacolantern : CacodemonStun 5006
{
    Game Doom
    SpawnID 159
    Health 800
    Obituary "$STR_OB_CACOLANTERN"
    HitObituary "$STR_OB_CACOLANTERNHIT"
    Translation "171:175=209:214", "176:187=212:223", "188:191=232:235", "16:39=212:223", "40:47=232:235", "164:167=215:218"
    Tag "$STR_MONSTERTAG_CACOLANTERN"
    States
    {
  Missile:
    HEAD BC 5 A_FaceTarget
    HEAD D 5 Bright A_BasicAttack(8, "", "CacolanternBall", 32)
    Goto See

  Death:
    TNT1 A 0 A_GiveToTarget("AwardItem", 4)
    goto Super::Death
    }
}

actor DarkImp : DoomImpStun 5003
{
    Game Doom
    SpawnID 155
    Health 120
    Obituary "$STR_OB_DARKIMP"
    HitObituary "$STR_OB_DARKIMPHIT"
    Translation "1:1=8:8", "48:79=[185,185,185]:[0,0,0]", "151:151=6:6"
    Tag "$STR_MONSTERTAG_DARKIMP"
    States
    {
    Melee:
    Missile:
        TROO EF 6 A_FaceTarget
        TROO G 5 A_CustomComboAttack("DarkImpBallFast",32,3*random(1,8),"imp/melee")
        Goto See
  Death:
    TNT1 A 0 A_GiveToTarget("AwardItem", 2)
    goto Super::Death
  XDeath:
    TNT1 A 0 A_GiveToTarget("AwardItem", 2)
    goto Super::XDeath
    }
}

ACTOR DarkImpBallFast
{
    Radius 6
    Height 8
    Speed 20
    Damage 3
    PROJECTILE
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    RenderStyle Add
    Alpha 0.75
    DamageType "Fire"
    Decal DarkImpBallScorch
    States
    {
    Spawn:
        DIMB AB 4 Bright
        Loop
    Death:
        DIMB CDE 6 Bright
        Stop
    }
}
