ACTOR ZombieTank 21012
{
	//$Category Stronghold_Monsters
	painchance "Stunner",32
	Health 600
	Painchance 50
	Speed 6
	Radius 27
	+NOBLOOD
	Height 72
	Mass 1000
	SeeSound "ZTank/see"
	AttackSound "ZTank/attack"
	PainSound "ZTank/pain"
	DeathSound "ZTank/death"
	DropItem Zombieman
	Obituary "%o was shot by a zombie in a tank."
	MONSTER
	+FLOORCLIP
	Decal Bulletchip
	States
	{
	Spawn:
		ZTNK A 8 A_Look
		Loop
	See:
		ZTNK A 0 A_PlaySound("ZTank/Run")
		ZTNK AABBCC 1 A_Chase
		Loop
	Missile:
		ZTNK A 1 A_FaceTarget
		ZTNK D 3 BRIGHT LIGHT("ZombieTankAttack") A_CustomBulletAttack(22.5,11.25,2,4,"BulletPuff")
		ZTNK A 1 A_FaceTarget
		ZTNK E 3 BRIGHT LIGHT("ZombieTankAttack") A_CustomBulletAttack(22.5,11.25,2,4,"BulletPuff")
		ZTNK A 0 A_Jump(128,"See")
		ZTNK A 0 A_CPosRefire
		goto Missile
	Pain:
		ZTNK F 8 A_Pain
		Goto See
	Pain.Stunner:
		ZTNK F 0 A_ChangeFlag("NoPain",1)
		ZTNK F 300 A_Pain
		ZTNK F 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		ZTNK G 4 ACS_NamedExecuteAlways("AwardPoints",0)
		ZTNK G 4 A_Scream
		ZTNK H 8 A_SpawnItemEx("ZombieTankGun",-40,0,30,12,0,0,64)
		ZTNK I 8 A_SpawnItemEx("ZombieTankGun",40,0,30,12,0,0,192)
		ZTNK J 8 A_CustomMissile("ZombieTankExplosion",48,0,0)
		ZTNK K 8 A_Fall
		ZTNK L 8
		ZTNK M 8 A_SpawnItemEx("ZTankSmoke",0,0,48)
		ZTNK N 8
		ZTNK O 3000
		ZTNK O 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR ZombieTankGun
{
	+CLIENTSIDEONLY
	Radius 4
	Height 4
	Mass 5
	Speed 12
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+NOTELEPORT
	BounceType Doom
	+NoTarget
	States
	{
	Spawn:
		ZTNG ABCDEFG 3
		Loop
	Death:
		ZTNG G 3000
		ZTNG G 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR ZombieTankExplosion
{
	RENDERSTYLE ADD
	ALPHA 0.7
	Radius 10
	Height 32
	SeeSound "ZTank/explode"
	PROJECTILE
	DamageType Fire
	+NOCLIP
	States
	{
	Spawn:
		ZTXP A 2 BRIGHT LIGHT("ZombieTankExplosion1")
		ZTXP B 2 BRIGHT LIGHT("ZombieTankExplosion2")
		ZTXP C 2 BRIGHT LIGHT("ZombieTankExplosion3")
		ZTXP D 2 BRIGHT LIGHT("ZombieTankExplosion4")
		ZTXP E 2 BRIGHT LIGHT("ZombieTankExplosion5")  A_Explode(128,128,0)
		ZTXP F 2 BRIGHT LIGHT("ZombieTankExplosion6")
		ZTXP G 2 BRIGHT LIGHT("ZombieTankExplosion7")
		ZTXP H 2 BRIGHT LIGHT("ZombieTankExplosion8")
		ZTXP I 2 BRIGHT LIGHT("ZombieTankExplosion9")
		ZTXP J 2 BRIGHT LIGHT("ZombieTankExplosion10")
		ZTXP K 2 BRIGHT LIGHT("ZombieTankExplosion11")
		ZTXP L 2 BRIGHT LIGHT("ZombieTankExplosion12")
		ZTXP M 2 BRIGHT LIGHT("ZombieTankExplosion13")
		ZTXP N 2 BRIGHT LIGHT("ZombieTankExplosion14")
		ZTXP O 2 BRIGHT LIGHT("ZombieTankExplosion15") A_FadeOut(.05)
		ZTXP P 2 BRIGHT LIGHT("ZombieTankExplosion16") A_FadeOut(.05)
		ZTXP Q 2 BRIGHT LIGHT("ZombieTankExplosion17") A_FadeOut(.05)
		ZTXP R 2 BRIGHT LIGHT("ZombieTankExplosion18") A_FadeOut(.05)
		ZTXP S 2 BRIGHT LIGHT("ZombieTankExplosion19") A_FadeOut
		ZTXP T 2 BRIGHT LIGHT("ZombieTankExplosion20") A_FadeOut
		Stop
	}
}

Actor ZTankSmoke //Temporary smoke puff
{
	+CLIENTSIDEONLY
	Height 4
	Radius 4
	+NoGravity
	Mass 5
	States
	{
	Spawn:
		TNT1 A 1 A_CustomMissile("ZTSmokeA",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeB",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeC",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeD",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeA",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeB",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeC",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeD",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeA",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeB",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeC",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeD",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeA",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeB",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeC",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeD",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeA",0,10,random(0,360),2,random(80,100))
		TNT1 A 1 A_CustomMissile("ZTSmokeB",0,10,random(0,360),2,random(80,100))
		Stop
	}
}

Actor ZTSmokeA
{
	+CLIENTSIDEONLY
	Projectile
	RenderStyle Translucent
	Mass 5
	speed 1
	Alpha .5
	Scale .7
	+noclip
	+DONTSPLASH
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	States
	{
	Spawn:
		TSM1 IHGF 4
		"####" EEDDCC 2 A_FadeOut(.05)
		"####" BBBBAAAA 1 A_FadeOut(.02)
		Stop
	}
}

Actor ZTSmokeB : ZTSmokeA
{
	speed 2
	States
	{
	Spawn:
		TSM2 IHGF 4
		Goto Super::Spawn+4
		Stop
	}
}

Actor ZTSmokeC : ZTSmokeA
{
	speed 3
	States
	{
	Spawn:
		TSM3 IHGF 4
		Goto Super::Spawn+4
		Stop
	}
}

Actor ZTSmokeD : ZTSmokeA
{
	States
	{
	Spawn:
		TSM4 IHGF 4
		Goto Super::Spawn+4
		Stop
	}
}