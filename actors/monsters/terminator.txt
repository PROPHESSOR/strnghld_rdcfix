///////////////////////////////////////////////////////////////////
//Terminator
//created by: Vader
///////////////////////////////////////////////////////////////////

ACTOR Terminator 30666
{
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	Health 7000
	Radius 24
	Height 64
	Mass 1500
	Speed 3
	PainChance 8
	scale 1.2
	MONSTER
	Obituary "%o was liquidated by a Terminator."
	+FLOORCLIP
	+MISSILEMORE
	+MISSILEEVENMORE
	+BOSS
	+DONTHARMCLASS
	+NORADIUSDMG
	MeleeRange 192
	Damagefactor "Redblast",0.0
	SeeSound "monster/termsit"
	PainSound "monster/termpin"
	DeathSound "monster/termdth"
	ActiveSound "Terminator/termact"
	States
	{
	Spawn:
		TERM AB 10 A_Look
		Loop
	See:
		TERM A 0 A_Playsound("Terminator/terstepA")
		TERM A 1 A_Chase
		TERM AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		TERM A 1 A_Chase
		TERM AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		TERM B 1 A_Chase
		TERM BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		TERM B 1 A_Chase
		TERM BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		TERM C 0 A_Playsound("Terminator/terstepA")
		TERM C 1 A_Chase
		TERM CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		TERM C 1 A_Chase
		TERM CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		TERM D 1 A_Chase
		TERM DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		TERM D 1 A_Chase
		TERM DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Pain:
		TERM L 3
		TERM L 3 A_Pain
		Goto See
	Pain.Stunner:
		TERM L 3 A_ChangeFlag("NoPain",1)
		TERM L 300 A_Pain
		TERM L 0 A_ChangeFlag("NoPain",0)
		goto See
	Missile:
		TERM E 0 A_JumpIfCloser(512,"Missile2")
		TERM E 0 A_Jump(170,"Missile2")
		TERM E 5 A_FaceTarget
		TERM F 5 Bright LIGHT("TerminatorAttack2") A_CustomMissile("TerminatorBlaster",38,15,Random(Random(2,Random(3,4)),Random(-2,(Random(-3,-4)))))
		TERM F 0 A_Spidrefire
		Goto Missile+2
	Missile2:
		TERM G 0 A_Jump(128,"Missile3")
	Melee:
		TERM G 2 A_Facetarget
		TERM H 1 Bright LIGHT("TerminatorAttack1") A_CustomBulletAttack(24,12,6,3,"Terminatorpuff")
		TERM H 1 Bright LIGHT("TerminatorAttack1") A_Playsound("Terminator/tershotB")
		TERM G 2 A_Facetarget
		TERM I 1 Bright LIGHT("TerminatorAttack1") A_CustomBulletAttack(24,12,6,3,"Terminatorpuff")
		TERM I 1 Bright LIGHT("TerminatorAttack1") A_Playsound("Terminator/tershotB")
		TERM G 1 A_Spidrefire
		Goto Missile2+1
	Missile3:
		TERM J 0 A_Jump(128,"Missile5")
		TERM K 0 A_JumpIfCloser(192,"Missile4")
		TERM J 8 A_Facetarget
		TERM K 8 Bright LIGHT("TerminatorAttack3") A_CustomMissile("Redgrenade",38,-15)
		Goto See
	Missile4:
		TERM J 6 A_Facetarget
		TERM K 6 Bright LIGHT("TerminatorAttack3") A_CustomMissile("Redstar",38,-15,0,Random(2,-2))
		TERM J 6 A_Facetarget
		TERM K 6 Bright LIGHT("TerminatorAttack3") A_CustomMissile("Redstar",38,-15,0,Random(8,-8))
	Missile5:
		TERM J 6 A_Facetarget
		TERM K 6 Bright LIGHT("TerminatorAttack3") A_CustomMissile("Redstar",38,-15)
		Goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		TERM M 6 Light("TerminatorDeath1") A_Scream
		TERM N 4 Light("TerminatorDeath2")
		TERM O 4 Bright Light("TerminatorDeath3")
		TERM P 4 Bright Light("TerminatorDeath4")
		TERM Q 4 Bright Light("TerminatorDeath5")
		TERM R 0 A_SpawnItemEx("TerminatorHead",15,0,90,10,0,0,-170)
		TERM R 0 A_SpawnItemEx("TerminatorShoulder",60,0,75,8,0,0,-70)
		TERM R 4 Bright Light("TerminatorDeath6") A_SpawnItemEx("TerminatorArm",60,0,0,3,0,0,-90)
		TERM S 4 Bright Light("TerminatorDeath7")
		TERM T 4 Bright Light("TerminatorDeath8")
		TERM U 4 Bright Light("TerminatorDeath9")
		TERM V 4 Bright Light("TerminatorDeath10")
		TERM W 4 Light("TerminatorDeath11")
		TERM X 6 Light("TerminatorDeath12") A_Fall
		TERM Y 6 Light("TerminatorDeath13")
		TERM Z 1 A_Playsound("Terminator/terstepA")
		TERM Z 5
		TERM [ 3000
		TERM [ 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR TerminatorBlaster
{
	Radius 8
	Height 12
	Speed 35
	FastSpeed 45
	Damage 8
	PROJECTILE
	RENDERSTYLE ADD
	alpha 0.85
	SeeSound "monster/brufir"
	DeathSound "weapons/hellex"
	+FORCERADIUSDMG
	States
	{
	Spawn:
		BLST A -1 Bright LIGHT("TerminatorBlaster")
		loop
	Death:
		BLST B 6 Bright LIGHT("TerminatorBlaster2") A_Explode(16,128)
		BLST C 3 Bright LIGHT("TerminatorBlaster3")
		BLST D 3 Bright LIGHT("TerminatorBlaster4")
		BLST E 3 Bright LIGHT("TerminatorBlaster5")
		BLST F 3 Bright LIGHT("TerminatorBlaster6")
		BLST G 3 Bright LIGHT("TerminatorBlaster7")
		BLST H 3 Bright LIGHT("TerminatorBlaster8")
		BLST I 3 Bright LIGHT("TerminatorBlaster9")
		BLST J 3 Bright LIGHT("TerminatorBlaster10")
		BLST K 3 Bright LIGHT("TerminatorBlaster11")
		BLST L 3 Bright LIGHT("TerminatorBlaster12")
		stop
	}
}

Actor Redstar
{
	Radius 10
	Height 20
	Speed 30
	FastSpeed 40
	Damage 4
	Scale 0.6
	+SEEKERMISSILE
	+FORCERADIUSDMG
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.8
	SeeSound "Terminator/tershotA"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		STAX A 0 A_SeekerMissile(15,45)
		STAX AAAA 1 Bright LIGHT("RedStar") A_SpawnItemEx("grenadetrail")
		STAX B 0 A_SeekerMissile(15,45)
		STAX BBBB 1 Bright LIGHT("RedStar2") A_SpawnItemEx("grenadetrail")
		STAX C 0 A_SeekerMissile(15,45)
		STAX CCCC 1 Bright LIGHT("RedStar3") A_SpawnItemEx("grenadetrail")
		STAX D 0 A_SeekerMissile(15,45)
		STAX DDDD 1 Bright LIGHT("RedStar4") A_SpawnItemEx("grenadetrail")
		Loop
	Death:
		STAX E 3 Bright LIGHT("RedStar5") A_Explode(20,192,1)
		STAX F 3 BRIGHT LIGHT("RedStar6")
		STAX G 3 BRIGHT LIGHT("RedStar7")
		STAX H 3 BRIGHT LIGHT("RedStar8")
		STAX I 3 BRIGHT LIGHT("RedStar9")
		STAX J 3 BRIGHT LIGHT("RedStar10")
		STAX K 3 BRIGHT LIGHT("RedStar11")
		STAX L 3 BRIGHT LIGHT("RedStar12")
		STAX MNO 3 Bright
		Stop
	}
}

actor Redgrenade
{
	Radius 6
	Height 12
	Speed 30
	FastSpeed 45
	Damage 4
	PROJECTILE
	+ForceRadiusDMG
	RENDERSTYLE ADD
	ALPHA 0.8
	SeeSound "Terminator/tershotA"
	DeathSound "imp/shotx"
	var int user_theta;
	States
	{
	Spawn:
		TGND AAAABBBBCCCC 1 Bright LIGHT("Redgrenade") A_SpawnItemEx("grenadetrail")
		Loop
	Death:
		STAR E 0 A_custommissile("grenadeblast",0,0,user_theta,2,0)
		STAR E 0 A_SetUserVar("user_theta",user_theta+20)
		STAR E 0 A_JumpIf(user_theta>=360,1)
		Loop
		STAR EFGHIJKLMNO 3 Bright
		Stop
	}
}

ACTOR Grenadetrail
{
	+CLIENTSIDEONLY
	PROJECTILE
	+NOCLIP
	Renderstyle ADD
	Alpha 0.6
	DamageType Redblast
	States
	{
	Spawn:
		TGTR ABCDEFGHI 4 Bright
		Stop
	}
}

actor Grenadeblast
{
	Radius 6
	Height 12
	Speed 5
	Speed 6
	Damage 3
	DamageType Redblast
	PROJECTILE
	RENDERSTYLE ADD
	+Ripper
	+ForceRadiusDMG
	+NoDamageThrust
	ALPHA 0.8
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		TGBL A 2 BRIGHT LIGHT("Grenadeblast1")
		TGBL B 2 BRIGHT LIGHT("Grenadeblast2")
		TGBL C 2 BRIGHT LIGHT("Grenadeblast3")
		TGBL D 2 BRIGHT LIGHT("Grenadeblast4")
		TGBL E 2 BRIGHT LIGHT("Grenadeblast5")
		TGBL F 2 BRIGHT LIGHT("Grenadeblast6")
	Death:
		TGBL G 2 BRIGHT LIGHT("Grenadeblast7")
		TGBL H 2 BRIGHT LIGHT("Grenadeblast8")
		TGBL I 2 BRIGHT LIGHT("Grenadeblast9")
		TGBL J 2 BRIGHT LIGHT("Grenadeblast10")
		TGBL K 2 BRIGHT LIGHT("Grenadeblast11")
		stop
	}
}

ACTOR Terminatorpuff : Bulletpuff
{
	Renderstyle ADD
	Alpha 0.75
	scale 0.75
	States
	{
	Spawn:
	Melee:
		TPUF A 2 Bright LIGHT("Terminatorpuff1")
		TPUF A 0 A_Jump(128,10)
		TPUF B 2 BRIGHT LIGHT("Terminatorpuff2")
		TPUF C 2 BRIGHT LIGHT("Terminatorpuff3")
		TPUF D 2 BRIGHT LIGHT("Terminatorpuff4")
		TPUF E 2 BRIGHT LIGHT("Terminatorpuff5")
		TPUF F 2 BRIGHT LIGHT("Terminatorpuff6")
		TPUF G 2 BRIGHT LIGHT("Terminatorpuff7")
		TPUF H 2 BRIGHT LIGHT("Terminatorpuff8")
		Stop
		TPUF I 2 BRIGHT LIGHT("Terminatorpuff9")
		TPUF J 2 BRIGHT LIGHT("Terminatorpuff10")
		TPUF K 2 BRIGHT LIGHT("Terminatorpuff11")
		TPUF L 2 BRIGHT LIGHT("Terminatorpuff12")
		TPUF M 2 BRIGHT LIGHT("Terminatorpuff13")
		TPUF N 2 BRIGHT LIGHT("Terminatorpuff14")
		TPUF O 2 BRIGHT LIGHT("Terminatorpuff15")
		Stop
	}
}

ACTOR TerminatorArm
{
	+CLIENTSIDEONLY
	Radius 12
	Height 15
	Speed 3
	Mass 1000
	scale 1.2
	BounceType Doom
	States
	{
	Spawn:
		TARM ABCDE 5
		TARM F 3000
		TARM F 1 A_FadeOut(0.05)
		Wait
	}
}

actor TerminatorHead : TerminatorArm
{
	Radius 6
	Height 8
	Speed 10
	+DROPOFF
	+Missile
	scale 1.2
	States
	{
	Spawn:
		THAD DEFGHABC 2
		loop
	Death:
		"####" I 3000
		"####" I 1 A_FadeOut(0.05)
		Wait
	}
}

actor TerminatorShoulder : TerminatorHead
{
	Radius 8
	Speed 8
	States
	{
	Spawn:
		TSHO ABCDEFGH 2
		loop
	}
}