ACTOR HellWarrior 3119
{
spawnid 214
PainChance "Stunner", 64
//$Category Stronghold_Monsters
   Health 400
   Speed 8
   Radius 24
   Height 64
   PainChance 50
   Mass 1000
   Meleedamage 8
   SeeSound "knight/sight"
   PainSound "knight/pain"
   DeathSound "knight/death"
   ActiveSound "knight/active"
   MeleeSound "baron/melee"
   MONSTER
   +FLOORCLIP
   Missiletype HellWarriorBall
   Obituary "$STR_OB_HELLWARRIOR"
   HitObituary "$STR_OB_HELLWARRIORHIT"
   Tag "$STR_MONSTERTAG_HELLWARRIOR"
   States
   {
   Spawn:
      HWAR A 0
      HWAR A 0 A_UnSetInvulnerable
      HWAR AB 10 A_Look
      Loop
   See:
      HWAR A 0
      HWAR A 0 A_UnSetInvulnerable
      HWAR AABBCCDD 3 A_Chase
      Loop
   Missile:
      HWAR E 0 A_Jump(128, "Missile2")
      HWAR EF 8 A_FaceTarget
      HWAR G 8 A_ComboAttack
      Goto See
   Missile2:
      HWAR H 0 A_Jump(128, "Missile3")
      HWAR H 0 A_SetInvulnerable
      HWAR H 6 A_FaceTarget
      HWAR I 4 BRIGHT A_CustomMissile("HellWarriorBall2", 25, 0, 0)
      HWAR H 6 A_FaceTarget
      HWAR H 0 A_UnSetInvulnerable
      Goto See
   Missile3:
      HWAR H 0 A_SetInvulnerable
      HWAR H 6 A_FaceTarget
      HWAR I 4 BRIGHT A_CustomMissile("HellWarriorBall2", 25, 0, 0)
      HWAR H 16 A_SpidReFire
      HWAR I 4 BRIGHT A_CustomMissile("HellWarriorBall2", 25, 0, 0)
      HWAR H 6 A_FaceTarget
      HWAR H 0 A_UnSetInvulnerable
      Goto See
   Melee:
      HWAR EF 8 A_FaceTarget
      HWAR G 8 A_ComboAttack
      Goto See
   Pain:
      TNT1 A 0 A_UnSetInvulnerable
      HWAR J 6 A_Pain
      HWAR H 0 A_SetInvulnerable
      HWAR HHHHHHH 6 A_FaceTarget
      HWAR H 0 A_UnSetInvulnerable
      Goto See
   Pain.Stunner:
       TNT1 A 0 A_UnSetInvulnerable
       HWAR J 2 A_ChangeFlag("NoPain", 1)
       HWAR J 300 A_Pain
       HWAR J 0 A_ChangeFlag("NoPain", 0)
       goto See
   Death:
      TNT1 A 0 A_GiveToTarget("AwardItem", 14)
      TNT1 A 0 A_FaceTarget
      HWAR K 5 A_SpawnItemEx("HellWarriorShield", 0, 0, 25, 6, 0, 0, 60, 128)
      HWAR L 5 A_Scream
      HWAR M 5
      HWAR N 5 A_NoBlocking
      HWAR OPQRS 5
      HWAR T 3000 CanRaise
      HWAR T 2 A_FadeOut(0.1)
      wait
   Raise:
      HWAR QPONMLK 5
      goto See
   }
}

actor HellWarriorBall
{
    Radius 6
    Height 8
    Speed 14
    Damage 8
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.7
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal BaronScorch
    States
    {
    Spawn:
        HWFB AB 5 Bright
        Loop
    Death:
        HWFB CDE 5 Bright
        Stop
    }
}

actor HellWarriorBall2
{
    Radius 6
    Height 8
    Speed 8
    Damage 2
    PROJECTILE
    RENDERSTYLE ADD
    +RANDOMIZE
    +RIPPER
    ALPHA 0.8
    SCALE 0.4
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
        GRBA ABCDEFGH 2 Bright
        Loop
    Death:
        GRBA AIJKLMN 3 Bright
        Stop
    }
}

ACTOR HellWarriorShield
{
	Radius 8
	Height 8
	Speed 6
	+DOOMBOUNCE
	+DROPOFF
	+Missile
      States
	{
	Spawn:
         HWSH ABCDEFGH 3
         Loop
	Death:
	 HWSH I 3000
	 HWSH I 2 A_FadeOut(0.1)
	 wait
	}
}
