actor Helemental 30130
{
//$Category Stronghold_Monsters
  spawnid 226
  PainChance "Stunner", 128
  Obituary "$STR_OB_HELEMENTAL"
  HitObituary "$STR_OB_HELEMENTALHIT"
  Tag "$STR_MONSTERTAG_HELEMENTAL"
  health 1600
  radius 34
  height 64
  mass 800
  speed 10
  PainChance 32
  MeleeDamage 10
  seesound "monster/helsit"
  painsound "monster/helpai"
  deathsound "monster/heldth"
  activesound "monster/helact"
  meleesound "caco/melee"
  MONSTER
  +FLOAT
  +NOGRAVITY
  +BOSS
  +MISSILEMORE
  +DONTHURTSPECIES
  //+DONTRIP
  +NORADIUSDMG
  states
  {
  Spawn:
    HELE A 10 A_Look
    loop
  See:
    HELE A 0 A_SetShootable
    HELE AAAAAAAAAAAA 3 A_Chase
    HELE A 0 A_Jump(72,1)
    goto See+1
    HELE A 0 A_UnSetshootable
    HELE A 1 A_PlaySound("monster/heltel")
    HELE A 1 A_SetTranslucent(0.90)
    HELE A 1 A_SetTranslucent(0.80)
    HELE A 1 A_SetTranslucent(0.70)
    HELE A 1 A_SetTranslucent(0.60)
    HELE A 1 A_SetTranslucent(0.50)
    HELE A 1 A_SetTranslucent(0.40)
    HELE A 1 A_SetTranslucent(0.30)
    HELE A 1 A_SetTranslucent(0.20)
    HELE A 1 A_SetTranslucent(0.10)
    TNT1 A 0 A_Jump(128,25)
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
    TNT1 A 0 A_Jump(128,25)
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1)
    HELE A 1 A_PlaySound("monster/heltel")
    HELE A 1 A_SetTranslucent(0.10)
    HELE A 1 A_SetTranslucent(0.20)
    HELE A 1 A_SetTranslucent(0.30)
    HELE A 1 A_SetTranslucent(0.40)
    HELE A 1 A_SetTranslucent(0.50)
    HELE A 1 A_SetTranslucent(0.60)
    HELE A 1 A_SetTranslucent(0.70)
    HELE A 1 A_SetTranslucent(0.80)
    HELE A 1 A_SetTranslucent(0.90)
    HELE A 1 A_SetTranslucent(1.0)
    HELE A 0 A_SetShootable
    goto See
  Missile:
    HELE A 0 A_Jump(80,1)
    goto Missile1
    HELE A 0 A_Jump(128,"Missile3")
    goto Missile2
  Missile1:
    HELE EF 5 Bright A_Facetarget
    TNT1 A 0 A_CustomMissile("HadesBall",24,0,-10,0,0)
    TNT1 A 0 A_CustomMissile("HadesBall",24,0,-5,0,0)
    TNT1 A 0 A_CustomMissile("HadesBall",24,0,0,0,0)
    TNT1 A 0 A_CustomMissile("HadesBall",24,0,5,0,0)
    HELE G 5 Bright A_CustomMissile("HadesBall",24,0,10,0,0)
    goto See+1
  Missile2:
    HELE EF 5 Bright A_Facetarget
    TNT1 A 0 A_CustomMissile("HadesBolt",32,0,-16,0,0)
    TNT1 A 0 A_CustomMissile("HadesBolt",32,0,0,0,0)
    HELE G 5 Bright A_CustomMissile("HadesBolt",32,0,16,0,0)
    goto See+1
  Missile3:
    HELE BC 5 Bright A_Facetarget
    HELE D 5 Bright A_DualPainAttack("HS")
    goto See+1
  Melee:
    HELE ABD 5 Bright A_Facetarget
    HELE C 5 Bright A_MeleeAttack
    goto See+1
  Pain:
    HELE H 3
    HELE H 3 A_Pain
    HELE H 6
    goto See
  Pain.Stunner:
    HELE H 2 A_ChangeFlag("NoPain", 1)
    HELE H 300 A_Pain
    HELE H 0 A_ChangeFlag("NoPain", 0)
    goto See
  Death:
    TNT1 A 0 A_GiveToTarget("AwardItem", 30)
    HELE I 5 Bright A_Scream
    HELE JKL 5 Bright
    HELE M 5 Bright A_NoBlocking
    TNT1 A 0 A_PainDie("HS")
    HELE NOP 5 Bright 
    HELE QRSTU 5
    HELE V 3000 A_SetFloorClip
    HELE V 2 A_FadeOut(0.1)
    wait
  }
}

ACTOR HadesBall : CacodemonBall
{
Damage 8
Speed 15
Alpha 0.8
DamageType Lightning
+FORCEXYBILLBOARD
SeeSound "Monster/hadtel"
DeathSound "Monster/hadsit"
Decal CacoScorch
 states
  {
  Spawn:
    HEFX AB 4 bright
    loop
  Death:
    HEFX CDEEFGH 3 bright
    stop
  }
}

ACTOR HadesBolt : CacodemonBall
{
   Damage 5
   Speed 5
   Radius 8
   Height 8
   Damagetype Lightning
   BounceType "Hexen"
   SeeSound "weapons/none"
   DeathSound "weapons/gntidl"
   YScale 4
   XScale 2
   ReactionTime 35
   +FLOORHUGGER
   +RIPPER
   -NOGRAVITY
   +STRIFEDAMAGE
   States
   {
   Spawn:
      LFX2 F 1 Bright A_Explode(16,64,0)
      TNT1 A 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
      TNT1 A 0 ThrustThing(random(0,255),1,0,0)
      LFX2 G 1 Bright A_Explode(16,64,0)
      TNT1 A 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
      LFX2 H 1 Bright A_Explode(16,64,0)
      TNT1 A 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
      LFX2 I 1 Bright A_Explode(16,64,0)
      TNT1 A 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
      LFX2 J 1 Bright A_Explode(16,64,0)
      TNT1 A 0 A_CustomMissile("HadesBolt2",0,0,0,6,90)
      TNT1 A 0 A_CountDown
      loop
   Death:
      LFX2 FGHIJ 2 Bright A_Explode(16,64,0)
      stop
   }
}

ACTOR HadesBolt2 : CacodemonBall
{
Damage (0)
Speed 184
RenderStyle None
DamageType Lightning
+RIPPER
SeeSound "Monster/hadtel"
DeathSound "Monster/hadsit"
 states
  {
  Spawn:
    TNT1 A 1 A_Explode(16,64,0)
    loop
  Death:
    TNT1 A 1
    stop
  }
}
//End of Hades Elemental
//Monster-Hades Sphere
ACTOR HS 30116
{
//$Category Stronghold_Monsters
spawnid 224
Health 35
Radius 16 // originally 24
Height 32 // originally 48
Mass 3000
Speed 10
Obituary "$STR_OB_HS"
Tag "$STR_MONSTERTAG_HS"
PAINCHANCE 0
MONSTER
+LOOKALLAROUND
+DONTGIB
+NOGRAVITY
+FLOAT
+DONTFALL
//+DONTHURTSPECIES
+LONGMELEERANGE
+FORCEXYBILLBOARD
SeeSound "monster/hadsit"
DeathSound "monster/hadexp"
PainChance "Stunner", 255
States
   {
   Spawn:
       TNT1 A 1 A_Look
       loop

   See:
       HADE EFGH 4 bright
   SeeLoop: // SpecialChase handling caused desyncs when given as an item
       TNT1 A 0 A_PlaySound("monster/hadwlk")
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust")
       HADE A 2 bright A_Chase
   SeeLoop2:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust2")
       HADE A 2 bright A_Chase
   SeeLoop3:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust3")
       HADE B 2 bright A_Chase
   SeeLoop4:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust4")
       HADE B 2 bright A_Chase
   SeeLoop5:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust5")
       HADE C 2 bright A_Chase
   SeeLoop6:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust6")
       HADE C 2 bright A_Chase
   SeeLoop7:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust7")
       HADE D 2 bright A_Chase
   SeeLoop8:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoopThrust8")
       HADE D 2 bright A_Chase
   SeeLoopFinish:
       TNT1 A 0 A_CheckFlag("SKULLFLY","SeeLoop")
       TNT1 A 0 A_Jump(32,"TeleportOut")
       goto SeeLoop

   SeeLoopThrust:
       HADE A 2 bright A_Chase("","")
       goto SeeLoop2
   SeeLoopThrust2:
       HADE A 2 bright A_Chase("","")
       goto SeeLoop3
   SeeLoopThrust3:
       HADE B 2 bright A_Chase("","")
       goto SeeLoop4
   SeeLoopThrust4:
       HADE B 2 bright A_Chase("","")
       goto SeeLoop5
   SeeLoopThrust5:
       HADE C 2 bright A_Chase("","")
       goto SeeLoop6
   SeeLoopThrust6:
       HADE C 2 bright A_Chase("","")
       goto SeeLoop7
   SeeLoopThrust7:
       HADE D 2 bright A_Chase("","")
       goto SeeLoop8
   SeeLoopThrust8:
       HADE D 2 bright A_Chase("","")
       goto SeeLoopFinish

   TeleportOut:
       //TNT1 A 0 A_UnSetSolid
       //TNT1 A 0 A_UnSetShootable //Hey you can shoot them
       HADE H 5 bright A_PlaySound("monster/hadtel")
       HADE GFEIJKL 4 bright
   SeeTeleport:
       TNT1 AAAAAAAA 2 A_Chase("","")
       TNT1 A 0 A_Jump(96,"TeleportIn")
       loop
   TeleportIn:
       HADE L 3 bright A_PlaySound("monster/hadtel")
       HADE KJIEFG 4 bright
       //TNT1 A 0 A_SetShootable
       //TNT1 A 0 A_SetSolid
       goto SeeLoop
   Melee:
       //TNT1 A 0 A_SetInvulnerable
       HADE HGFE 4 bright
       //TNT1 A 0 A_UnSetInvulnerable
       //TNT1 A 0 A_SetShootable
       TNT1 A 0 A_Die
       goto Death
   Pain:
       HADE A 35
       goto SeeLoop
  Pain.Stunner:
    HADE A 302 A_ChangeFlag("NoPain", 1) // 2 + 300
    HADE A 0 A_ChangeFlag("NoPain", 0)
    goto SeeLoop
   Death:
       TNT1 A 0 A_GiveToTarget("AwardItem", 2)
       TNT1 A 0 A_NoBlocking
       TNT1 A 0 A_SetTranslucent(0.9,1)
       TNT1 A 0 A_Scream
       HADE M 4 bright A_Explode(128,112,4) // originally 112 damage
       HADE NOPQ 5 bright
       stop
   XDeath:
       TNT1 A 0 A_GiveToTarget("AwardItem", 2)
       TNT1 A 0 A_NoBlocking
       HADE H 5 bright A_PlaySound("monster/haddth",2)
       HADE GFERSTUVWX 4 bright
       stop
   Ice:
       "####" "#" 0 A_Gravity
       goto GenericFreezeDeath
   }
}

ACTOR HSBoom
{
   Alpha 0.9
   RENDERSTYLE ADD
   +FORCEXYBILLBOARD
   +NOGRAVITY
   States
   {
   Spawn:
      HADE M 0 bright
      HADE M 4 bright A_PlaySound("monster/hadexp",2)
      HADE NOPQ 5 bright
      Stop
   }
}
//End of Hades Sphere
