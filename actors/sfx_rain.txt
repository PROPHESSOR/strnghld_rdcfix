//Place the RainDropSpawner on your map, and use values set on the specials tab to modify it
//The first argument controls it's area. 128, for example makes it a 256x256 square or 256-radius circle (See fourth argument)
//The second argument controls it's frequency. The lower the number, the heavier the rainfall
//The third argument controls whether or not it plays it's ambient sound. 0 will play the sound, 1 will not
//The fourth argument controls the area type. 0 is a square, 1 is a circle

Actor RainSpawner 3798
{
	//$Category Special Effects
	//$Title Rain Spawner (arg1 rad, arg2 freq, arg3 sound, arg4 shape)
	//$Color 12

	//$Arg0 "Radius"
	//$Arg0Tooltip "Radius in map units"
	//$Arg1 "Frequency"
	//$Arg1Tooltip "Frequency, the lower the number, the heavier the rainfall"
	//$Arg2 "Sound"
	//$Arg2Type 11
	//$Arg2Enum { 0 = "Yes"; 1 = "No"; }
	//$Arg3 "Area type"
	//$Arg3Type 11
	//$Arg3Enum { 0 = "Square"; 1 = "Circle"; }


	Radius 1
	Height 1
	+NoClip
	+ClientSideOnly
	+SpawnCeiling
	+NoGravity
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("weatherfx")==0,"NoRain")
		TNT1 A 0 A_JumpIf(Args[2] > 0,2)
		TNT1 A 0 A_PlaySound("Ambient/Rain",7,1,1)
		TNT1 A 0 A_JumpIf(Args[3] > 0,2)
		TNT1 A 1 A_SpawnItemEx("RainDrop",Random(-Args[0],Args[0]),Random(-Args[0],Args[0]),-2,0,0,0,0,0,Args[1])
		Loop
		TNT1 A 1 A_SpawnItemEx("RainDrop",Random(-Args[0],Args[0]),0,-2,0,0,0,Random(0,360),0,Args[1])
		Loop
	NoRain:
		TNT1 A 1 A_StopSound(7)
		Goto Spawn
	}
}

Actor RainDrop
{
	+CLIENTSIDEONLY
	+Missile
	+NoBlockMap
	+DONTSPLASH
	Height 2
	Radius 2
	Renderstyle Translucent
	Alpha 0.8
	YScale 4.0
	States
	{
	Spawn:
		RNDR A 1 A_JumpIf(WaterLevel > 0,"Death")
		Loop
	Death:
		RNDR BCDEFGH 3 A_FadeOut(0.15)
		Stop
	}
}