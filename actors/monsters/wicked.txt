ACTOR Wicked 30133
{
//$Category Stronghold_Monsters
spawnid 227
PainChance "Stunner", 128
Health 275
Radius 25
Height 88
Mass 200
Speed 10
PainChance 112
MONSTER
+NOGRAVITY
+FLOAT
Obituary "$STR_OB_WICKED"
Tag "$STR_MONSTERTAG_WICKED"
SeeSound "monster/wiksit"
PainSound "monster/wikpai"
DeathSound "monster/wikdth"
ActiveSound "monster/wikact"
States
   {
   Spawn:
       WICK ABCD 8 A_Look
       Loop
   See:
       WICK AABBCCDD 4 A_Chase
       WICK A 0 A_Jump(64,"See2")
       loop
   See2:
       WICK EEFFGGHH 2 A_Chase
       WICK E 0 A_Jump(32,"See")
       loop
   Missile:
       WICK I 0 A_PlaySound("monster/wikatk")
       WICK I 5 A_FaceTarget
       WICK J 5 BRIGHT A_FaceTarget
       WICK K 8 BRIGHT A_CustomMissile("WickedShot",60,0,0)
       WICK L 5
       Goto See
   Pain:
       WICK M 3
       WICK M 6 A_Pain
       Goto See
   Pain.Stunner:
       WICK M 2 A_ChangeFlag("NoPain", 1)
       WICK M 300 A_Pain
       WICK M 0 A_ChangeFlag("NoPain", 0)
       goto See
   Death:
       TNT1 A 0 A_GiveToTarget("AwardItem", 8)
       WICK N 5 A_Scream
       WICK OP 5
       WICK P 0 A_SpawnItemEx("WickedTorso",0,0,48,0,0,0,0,16)
       WICT A 5 A_NoBlocking
       WICT BCDEF 5
       WICT G 3000 CanRaise A_SetFloorClip
       WICT G 2 A_FadeOut(0.1)
       wait
   Raise:
       TNT1 A 0 A_RemoveChildren(TRUE)
       TNT1 A 0 A_SpawnItemEx("WickedTorsoRes",0,0,0,0,0,1,0,0)
       WICT G 5 A_UnSetFloorClip
       WICT FEDCB 5
       WICK PON 5
       goto See
   }
}

ACTOR WickedShot
{
   Radius 8
   Height 8
   Speed 10
   Damage 5
   RENDERSTYLE ADD
   DamageType "Fire"
   ALPHA 0.67
   PROJECTILE
   +SEEKERMISSILE
   +FORCEXYBILLBOARD
   Seesound "weapons/firmfi"
   DeathSound "weapons/firex5"
   Decal DoomImpScorch
   States
   {
   Spawn:
      WIBL ABCDABCDABCDABCDABCDABCDABCD 1 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
      WIBL ABCD 0 Bright A_SeekerMissile(90,90)
      loop
   Death:
      WIBL JKLMN 3 Bright
      stop
   }
}

ACTOR WickedFX
{
   Radius 2
   Height 2
   +NOBLOCKMAP
   +NOGRAVITY
   +DONTSPLASH
   +FORCEXYBILLBOARD
   RENDERSTYLE ADD
   ALPHA 0.67
   States
   {
   Spawn:
      TNT1 A 3
      WIBL EFGHI 3 BRIGHT
      Stop
   }
}

ACTOR WickedTorso
{
   Mass 1000000
   Radius 1
   Height 1
   +ISMONSTER
   +CORPSE
   States
   {
   Spawn:
      WICK Q 0
      WICK Q 5 A_PlaySound("monster/tenpn1")
      WICK R 5
      wait
   Crash:
      WICK S 0 A_SetFloorClip
      WICK S 5 A_PlaySound("monster/tenpn2")
      WICK TUVW 5
      WICK W 3000
      WICK W 2 A_FadeOut(0.1)
      wait
   }
}

ACTOR WickedTorsoRes
{
   Radius 24
   Height 24
   +NOGRAVITY
   +NOBLOCKMAP
   +NOCLIP
   States
   {
   Spawn:
      WICK WVUTSRQ 5
      stop
   }
}
//End of Wicked
