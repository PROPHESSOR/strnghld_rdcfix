ACTOR Demolisher 30110
{
//$Category Stronghold_Monsters
PainChance "Stunner", 128
spawnid 249
Obituary "$STR_OB_DEMOLISHER"
Tag "$STR_MONSTERTAG_DEMOLISHER"
Health 6000
Radius 80 // originally 96
Height 80 // originally 100
Mass 1000
Speed 16
PainChance 20
MONSTER
+FLOORCLIP
+BOSS
+MISSILEEVENMORE
+NORADIUSDMG
+DONTMORPH
//+DONTRIP
SeeSound "monster/demsit"
AttackSound "spider/attack"
PainSound "spider/pain"
DeathSound "monster/demdth"
ActiveSound "spider/active"
States
   {
   Spawn:
       DEMO AB 10 A_Look
       Loop
   See:
       DEMO A 3 A_Metal
       DEMO ABB 3 A_Chase
       DEMO C 3 A_Metal
       DEMO CDD 3 A_Chase
       DEMO E 3 A_Metal
       DEMO EFF 3 A_Chase
       Loop
   Missile:
       TNT1 A 0 A_Jump(128,"Missile2")
       DEMO A 20 BRIGHT A_FaceTarget
       TNT1 A 0 A_PlayWeaponSound("weapons/bfgf")
       DEMO T 5 BRIGHT A_FaceTarget
       DEMO T 4 BRIGHT A_FaceTarget
       DEMO TTTT 5 BRIGHT A_FaceTarget
       DEMO U 10 BRIGHT A_CustomMissile("BFGBall",32,0,0)
       TNT1 A 0 A_Jump(128,"Missile2")
       TNT1 A 0 A_PlayWeaponSound("weapons/bfgf")
       DEMO T 5 BRIGHT A_FaceTarget
       DEMO T 4 BRIGHT A_FaceTarget
       DEMO TTTT 5 BRIGHT A_FaceTarget
       DEMO U 10 BRIGHT A_CustomMissile("BFGBall",32,0,0)
       TNT1 A 0 A_Jump(192,"Missile2")
       Goto See
   Missile2:
       DEMO A 20 BRIGHT A_FaceTarget
       TNT1 A 0 A_SPosAttackUseAtkSound
       DEMO G 4 BRIGHT A_SPosAttackUseAtkSound
       TNT1 A 0 A_SPosAttackUseAtkSound
       DEMO H 4 BRIGHT A_SPosAttackUseAtkSound
       DEMO H 1 BRIGHT A_SpidRefire
       Goto Missile2+1
   Pain:
       DEMO I 3
       DEMO I 3 A_Pain
       Goto See
   Pain.Stunner:
       DEMO I 2 A_ChangeFlag("NoPain", 1)
       DEMO I 300 A_Pain
       DEMO I 0 A_ChangeFlag("NoPain", 0)
       goto See
   Death:
       TNT1 A 0 A_GiveToTarget("AwardItem", 55)
       DEMO J 20 Bright A_Scream
       DEMO K 10 Bright A_NoBlocking
       DEMO LMNOPQR 10 Bright
       DEMO S 30
       DEMO S 3000 A_BossDeath
       DEMO S 2 A_FadeOut(0.1)
       wait
   }
}
