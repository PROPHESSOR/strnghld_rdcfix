actor StrongholdPlayer : DoomPlayer replaces DoomPlayer
{
	Health 200
	DamageFactor "MarineAlly", 0
	DamageFactor "MarineAllyRocket", 0
	DamageFactor "MarineAllyPlasma", 0
	DamageFactor "MarineAllyRailgun", 0
	DamageFactor "MarineAllyRepeater", 0
	DamageFactor "MarineAllyFire", 0
	DamageFactor "MarineAllyBFG", 0
	DamageFactor "MarineAllyBFGSplash", 0
	DamageFactor "MarineAllyIce", 0
	DamageFactor "BossFactor", 0
	//Player.StartItem "GreenArmor" // handled in ACS now
	Player.StartItem "LifeItem", 5    // starting lives
	//Player.StartItem "CoinItem", 800 // starting money which is now handled in ACS
	Player.StartItem "DEPistol"
	Player.StartItem "DEFist"
	Player.StartItem "Clip", 50
	Species "Player"
	+THRUSPECIES
    +NOTAUTOAIMED
    States
    {
      Melee:
        PLAY F 6 bright light("ZOMBIEATK") // support this light for skins
        goto Super::Missile
    }
}

/*ACTOR GreenArmorModified : GreenArmor
{
    Armor.SavePercent 40
    Armor.SaveAmount 150
}

ACTOR BlueArmorModified : BlueArmor
{
    Armor.SavePercent 60
    Armor.SaveAmount 250
}*/

////////////////////////////////////////////////////////
////////////////// CHAINSAW ////////////////////////////
////////////////////////////////////////////////////////
actor StrMeleePuff : BulletPuff
{
    Mass 1
    Alpha 1
    DamageType "PlayerAlly"
    Species "Player"
    +MTHRUSPECIES
	States
	{
	Spawn:
        PUFF AB 4 bright
        goto Super::Melee
	}
}

actor DEChainsaw : Weapon replaces Chainsaw
{
  spawnid 32
  obituary "$OB_MPCHAINSAW"
  radius 20
  height 16
  inventory.pickupmessage "$GOTCHAINSAW"
  weapon.selectionorder 2200
  weapon.upsound "weapons/sawup"
  weapon.readysound "weapons/sawidle"
  Tag "$TAG_CHAINSAW"
  Decal BulletChip
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    SAWG CD 4 A_WeaponReady
    loop

  Deselect:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
    TNT1 A 0 A_Lower
    SAWG C 1 A_Lower
    loop

  DeselectPowered:
    TNT1 AA 0 A_Lower
    SAWG C 1 A_Lower
    goto Deselect

  Select:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
    TNT1 A 0 A_Raise
    SAWG C 1 A_Raise
    loop

  SelectPowered:
    TNT1 AA 0 A_Raise
    SAWG C 1 A_Raise
    goto Select

  Fire:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
    SAWG A 1 A_Saw("weapons/sawfull","weapons/sawhit",2,"StrMeleePuff")
    SAWG AAA 1 A_Saw("","",2,"StrMeleePuff")
    SAWG B 1 A_Saw("weapons/sawfull","weapons/sawhit",2,"StrMeleePuff")
    SAWG BBB 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 A 0 A_ReFire
    goto Ready

  FirePowered:
    TNT1 A 0 A_Saw("weapons/sawfull","weapons/sawhit",2,"StrMeleePuff")
    TNT1 AA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG A 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 AAA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG A 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 A 0 A_Saw("weapons/sawfull","weapons/sawhit",2,"StrMeleePuff")
    TNT1 AA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG B 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 AAA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG B 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 A 0 A_Saw("weapons/sawfull","weapons/sawhit",2,"StrMeleePuff")
    TNT1 AA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG A 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 AAA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG A 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 A 0 A_Saw("weapons/sawfull","weapons/sawhit",2,"StrMeleePuff")
    TNT1 AA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG B 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 AAA 0 A_Saw("","",2,"StrMeleePuff")
    SAWG B 1 A_Saw("","",2,"StrMeleePuff")
    TNT1 A 0 A_ReFire
    goto Ready

  Spawn:
    CSAW A -1
    stop
  }
}

////////////////////////////////////////////////////////
/////////////////////////// FIST ///////////////////////
////////////////////////////////////////////////////////
actor DEFist : Weapon replaces Fist
{
  obituary "$OB_MPFIST"
  weapon.selectionorder 3700
  weapon.kickback 100
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  +NOALERT
  Tag "$TAG_FIST"
  AttackSound "*fist"
  States
  {
  Ready:
    PUNG A 1 A_WeaponReady
    loop

  Deselect:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
    TNT1 A 0 A_Lower
    PUNG A 1 A_Lower
    loop

  DeselectPowered:
    TNT1 AA 0 A_Lower
    PUNG A 1 A_Lower
    goto Deselect

  Select:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
    TNT1 A 0 A_Raise
    PUNG A 1 A_Raise
    loop

  SelectPowered:
    TNT1 AA 0 A_Raise
    PUNG A 1 A_Raise
    goto Select

  Fire:
    PUNG B 4
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
    TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"FireBerserk")
    TNT1 A 0 A_CustomPunch(random(1, 10)*2, 1, 0, "StrMeleePuff")
    goto FireFinish

  FireBerserk:
    TNT1 A 0 A_CustomPunch((random(1, 10)*2)*10, 1, 0, "StrMeleePuff")
    goto FireFinish

  FirePowered:
    TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"FirePoweredBerserk")
    TNT1 AAAAAAAAAAAAAAAA 0 A_CustomPunch(random(1, 10)*2, 1, 0, "StrMeleePuff")
    goto FireFinish

  FirePoweredBerserk:
    TNT1 AAAAAAAAAAAAAAAA 0 A_CustomPunch((random(1, 10)*2)*10, 1, 0, "StrMeleePuff")

  FireFinish:
    PUNG C 4
    PUNG D 5
    PUNG C 4
    PUNG B 5 A_ReFire
    goto Ready
  }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// DEVASTATOR ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
Actor DevPuff
{
   Radius 3
   Height 3
   RENDERSTYLE ADD
   ALPHA 0.75
   +ALWAYSPUFF
   +PUFFONACTORS
   +NOGRAVITY
   +MTHRUSPECIES
   Species "Player"
   DamageType "PlayerAllyBFG"
   SeeSound "weapons/buzz"
   States
   {
   Spawn:
      DLIT JKLJKLJKL 3 Bright
      stop
   Crash:
      TNT1 A 1
      stop
   }
}

ACTOR Devastator : Weapon 20101
{
//$Category Stronghold_Weapons
   SpawnID 242
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "$STR_GOTDEVASTATOR"
   Obituary "$STR_OB_DEVASTATOR"
   Weapon.AmmoType "Cell"
   Weapon.AmmoGive 60
   Weapon.AmmoUse 60
   Tag "$STR_WEAPONTAG_DEVASTATOR"
   +WEAPON.EXPLOSIVE
   +WEAPON.BFG
   +WEAPON.NOAUTOFIRE
   States
   {
   Spawn:
      WDEV A -1
      stop

   Ready:
      DEVG A 1 A_WeaponReady
      loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      DEVG A 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      DEVG A 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      DEVG A 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      DEVG A 1 A_Raise
      goto Select

   Fire:
      TNT1 A 0 A_PlayWeaponSound("weapons/devchr2")
      DEVG BCDEF 10 bright
      TNT1 A 0 A_ReFire
      DEVG J 5 bright A_GunFlash
      TNT1 A 0 A_PlaySound("weapons/devfir2")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_FireCustomMissile("DevastatorBall",0,1,0,0)
      goto FireFinish

   FirePowered:
      TNT1 A 0 A_FireCustomMissile("DevastatorCannonProjectile",0,0,0,0)
      TNT1 A 0 A_FireCustomMissile("DevastatorCannonProjectile2",0,1,0,0)
      goto FireFinish

   Hold:
      TNT1 A 0 A_PlayWeaponSound("weapons/devfch2")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"HoldPowered")
      DEVG GHIGHI 4 bright A_FireBullets(4,4,4,4,"DevPuff",0,256)
      TNT1 A 0 A_ReFire
      goto HoldContinue

   HoldPowered:
      DEVG GHIGHI 4 bright A_FireBullets(4,4,4,4,"DevPuff",0,Random(256,512))
      TNT1 A 0 A_ReFire

   HoldContinue:
      DEVG J 5 bright A_GunFlash
      TNT1 A 0 A_PlaySound("weapons/devfir2")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"HoldContinuePowered")
      TNT1 A 0 A_FireCustomMissile("DevastatorBall1",0,1,0,0)
      goto FireFinish

   HoldContinuePowered:
      TNT1 A 0 A_FireBullets(25,25,250,50,"DevPuff",0)
      TNT1 A 0 A_FireCustomMissile("DevastatorCannonProjectile",0,0,0,0)
      TNT1 A 0 A_FireCustomMissile("DevastatorCannonProjectile2",0,1,0,0)

   FireFinish:
      DEVG K 10 bright
      DEVG L 7 bright
      goto Ready

   Flash:
      TNT1 A 5 A_Light1
      TNT1 A 10 A_Light2
      TNT1 A 7 A_Light1
      goto LightDone
   }
}

ACTOR DevastatorBall
{
   Radius 12
   Height 8
   Speed 30
   Damage 256
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.9
   DamageType "PlayerAllyBFG"
   Species "Player"
   +THRUSPECIES
   +FORCEXYBILLBOARD
   DeathSound "weapons/devexp2"
   Decal DevastatorLightning
   States
   {
   Spawn:
      DBAL AB 4 Bright 
      DBAL A 4 Bright
      TNT1 AAA 0 A_CustomMissile("LiteShot",0,0,Random(0,360),4)
      DBAL B 4 Bright
      TNT1 AAA 0 A_CustomMissile("LiteShot",0,0,Random(0,360),4)
      Goto Spawn+2
   Death:
      DBAL C 5 Bright
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,0,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,18,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,36,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,54,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,72,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,90,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,108,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,126,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,144,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,162,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,180,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,196,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,216,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,234,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,252,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,270,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,288,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,306,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,324,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,342,6)
      TNT1 A 0 A_Explode(320,256,1)
      DBAL D 5 Bright A_Explode(320,384,0)
      TNT1 A 0 A_Explode(320,256,1)
      TNT1 A 0 A_Explode(320,384,0)
      DBAL EFG 5 Bright
      DBAL HI 3 Bright
      Stop
   }
}

ACTOR DevastatorBall1 : DevastatorBall
{
   Damage 100
   +FORCERADIUSDMG
   States
   {
   Spawn:
      DBAL AB 4 Bright
      DBAL A 4 Bright A_Explode(128,128,1)
      TNT1 A 0 A_Jump(160,3)
      TNT1 AAA 0 A_SpawnItemEx("DevShooter",0,0,0,0,0,0,0,SXF_SETMASTER)
      DBAL B 4 Bright A_Explode(128,128,1)
      TNT1 A 0 A_Jump(160,3)
      TNT1 AAA 0 A_SpawnItemEx("DevShooter",0,0,0,0,0,0,0,SXF_SETMASTER)
      Goto Spawn+2
   Death:
      DBAL C 5 Bright
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,0,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,18,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,36,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,54,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,72,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,90,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,108,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,126,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,144,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,162,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,180,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,196,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,216,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,234,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,252,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,270,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,288,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,306,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,324,6)
      TNT1 A 0 A_CustomMissile("DevastatorBall2",0,0,342,6)
      TNT1 A 0 A_Explode(360,256,1)
      DBAL D 5 Bright A_Explode(360,384,0)
      TNT1 A 0 A_Explode(360,256,1)
      TNT1 A 0 A_Explode(360,384,0)
      DBAL EFG 5 Bright
      DBAL HI 3 Bright
      stop
   }
}

ACTOR DevastatorBall2
{
   Radius 8
   Height 8
   Speed 10
   Damage 10
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.9
   Damagetype "PlayerAllyBFG"
   Decal DoomImpScorch
   Species "Player"
   +THRUSPECIES
   +FORCEXYBILLBOARD
   +FORCERADIUSDMG
   SeeSound "weapons/devbal2"
   DeathSound "weapons/devex2_2"
   States
   {
   Spawn:
      DBA2 AB 4 Bright
      Loop
   Death:
      TNT1 A 0 A_Explode(64,64,0)
      DBA2 CDE 4 Bright
      Stop
   }
}

actor DevShooter
{  
   Radius 8
   Height 8
   MONSTER
   Speed 0
   +FRIENDLY
   +NOGRAVITY
   +NOCLIP
   +NEVERTARGET
   +NOBLOOD
   +THRUACTORS
   -SHOOTABLE
   -SOLID
   +NOTAUTOAIMED
   +INVULNERABLE // precautionary
   +NODAMAGE // ignore things like massacre. also precautionary
   +DONTSPLASH
   +DONTBLAST
   +DONTREFLECT
   +THRUSPECIES
   Species "Player"
   States
   {
   Spawn:
      TNT1 A 0 A_Look
   See:
      TNT1 A 0 A_Chase
   Missile:
      TNT1 A 0 A_FaceTarget
      TNT1 A 0 A_CustomMissile("LiteShot",0,0,0,4)
      Stop
    }
}

ACTOR LiteShot
{
   Radius 8
   Height 12
   Speed 32
   Damage 10
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.85
   Damagetype "PlayerAllyBFG"
   Species "Player"
   +THRUSPECIES
   +RIPPER
   +FORCEXYBILLBOARD
   SeeSound "weapons/devlit2"
   DeathSound "weapons/devzap2"
   States
   {
   Spawn:
      DLIT AAABBBCCC 1 Bright A_SpawnItemEx("LiteTrail",0,0,0,0,0,0,0,0,128)
      Loop
   Death:
      DLIT DEFGHIJKLMNO 1 Bright
      Stop
   }
}

ACTOR LiteTrail
{
   Radius 1
   Height 1
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.67
   +FORCEXYBILLBOARD
   States
   {
   Spawn:
      DLIT ABC 3 Bright
      stop
   }
}

//////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////  RAILGUN   ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Railgun : Weapon 20102
{
//$Category Stronghold_Weapons
   SpawnID 241
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "$STR_GOTRAILGUN"
   Obituary "$STR_OB_RAILGUN"
   Weapon.SisterWeapon "RailgunPowered"
   Weapon.AmmoType "Cell"
   Weapon.AmmoGive 40
   Weapon.AmmoUse 5
   Decal RailScorch
   Tag "$STR_WEAPONTAG_RAILGUN"
   +NOALERT
   States
   {
   Spawn:
      WRAL A -1
      stop

   Ready:
	  RAIG A 1 A_WeaponReady(WRF_ALLOWZOOM)
	  loop

   ReadyZoom:
	  RSCP A 1 bright A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
	  loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("RailgunScope",1,1)
      goto Deselect+2
      TNT1 A 0 A_PlaySound("weapons/railzoomout")
	  TNT1 A 0 A_TakeInventory("RailgunScope",1)
	  TNT1 A 0 A_TakeInventory("RailCharge",3)
	  TNT1 A 0 A_ZoomFactor(1)
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      RAIG A 1 A_Lower
      goto Deselect+2

   DeselectPowered:
      TNT1 AA 0 A_Lower
      RAIG A 1 A_Lower
      goto Deselect+2

   Select:
	  TNT1 A 0 A_TakeInventory("RailgunScope",1)
	  TNT1 A 0 A_TakeInventory("RailCharge",3)
	  TNT1 A 0 A_ZoomFactor(1,ZOOM_INSTANT)
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      RAIG A 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      RAIG A 1 A_Raise
      goto Select

   Fire:
      TNT1 A 0 A_JumpIfInventory("RailgunScope",1,"ZoomFire")
      TNT1 A 0 A_JumpIfInventory("RailCharge",2,"Charge2")
      TNT1 A 0 A_JumpIfInventory("RailCharge",1,"Charge1")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered") // Intentional. For now, at least.
      goto Charge0

   FireNoSound:
      TNT1 A 0 A_JumpIfInventory("RailgunScope",1,"ZoomFireNoSound")
      TNT1 A 0 A_JumpIfInventory("RailCharge",2,"Charge2")
      TNT1 A 0 A_JumpIfInventory("RailCharge",1,"Charge1")
      goto Charge0NoSound

   Charge0:
      RAIG B 6 bright A_PlayWeaponSound("weapons/railch")
      TNT1 A 0 A_JumpIfInventory("Cell",10,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto Charge0NoAmmo
      RAIG B 6 bright A_GiveInventory("RailCharge",1)
      goto Charge0Continue

   Charge0NoSound:
      RAIG B 6 bright
      TNT1 A 0 A_JumpIfInventory("Cell",10,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto Charge0NoAmmo
      RAIG B 6 bright A_GiveInventory("RailCharge",1)
      goto Charge0Continue

   Charge0NoAmmo:
      RAIG B 6 bright
      goto Charge0Continue

   Charge0Continue:
      TNT1 A 0 A_ReFire("FireNoSound")
      RAIG E 4 bright A_GunFlash
      TNT1 A 0 A_PlayWeaponSound("weapons/railf1_2")
      TNT1 A 0 A_TakeInventory("Cell",5,1)
      TNT1 A 0 A_AlertMonsters
      RAIG F 6 bright A_RailAttack(100,0,0,"FF 00 00","FF FF FF",1,0,"RailPuff")
      RAIG A 4
      RAIG K 15
      TNT1 A 0 A_TakeInventory("RailCharge",3)
      goto Ready

   Charge1:
      RAIG C 6 bright
      TNT1 A 0 A_JumpIfInventory("Cell",15,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto Charge1NoAmmo
      RAIG C 6 bright A_GiveInventory("RailCharge",1)
      goto Charge1Continue

   Charge1NoAmmo:
      RAIG C 6 bright
      goto Charge1Continue

   Charge1Continue:
      TNT1 A 0 A_ReFire
      RAIG G 4 bright A_GunFlash
      TNT1 A 0 A_PlayWeaponSound("weapons/railf2_2")
      TNT1 A 0 A_TakeInventory("Cell",10,1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_RailAttack(0,0,0,"FF FF 00","FF FF FF",1,0,"RailPuff")
      RAIG H 6 bright A_RailAttack(150,0,0,"FF FF 00","FF 45 00",1,0,"RailPuff")
      RAIG A 4
      RAIG K 15
      TNT1 A 0 A_TakeInventory("RailCharge",3)
      goto Ready

   Charge2:
      RAIG D 12 bright
      goto Charge2Continue

   Charge2Continue:
      TNT1 A 0 A_ReFire
      RAIG I 4 bright A_GunFlash
      TNT1 A 0 A_PlayWeaponSound("weapons/railf3_2")
      TNT1 A 0 A_TakeInventory("Cell",15,1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_RailAttack(0,0,0,"00 00 FF","00 45 FF",1,0,"RailPuff")
      TNT1 A 0 A_RailAttack(0,0,0,"00 00 FF","FF FF FF",1,0,"RailPuff")
      RAIG J 6 bright A_RailAttack(200,0,0,"00 00 FF","00 FF 00",1,0,"RailPuff")
      RAIG A 4
      RAIG K 15
      TNT1 A 0 A_TakeInventory("RailCharge",3)
      goto Ready

   ZoomFire:
	  TNT1 A 0 A_JumpIfInventory("RailCharge",2,"ZoomCharge2")
      TNT1 A 0 A_JumpIfInventory("RailCharge",1,"ZoomCharge1")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ZoomFirePowered") // Yep.
      goto ZoomCharge0

   ZoomFireNoSound:
	  TNT1 A 0 A_JumpIfInventory("RailCharge",2,"ZoomCharge2")
      TNT1 A 0 A_JumpIfInventory("RailCharge",1,"ZoomCharge1")
      goto ZoomCharge0NoSound

   ZoomCharge0:
      RSCP A 6 bright A_PlayWeaponSound("weapons/railch")
      TNT1 A 0 A_JumpIfInventory("Cell",10,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto ZoomCharge0NoAmmo
      RSCP A 6 bright A_GiveInventory("RailCharge",1)
      goto ZoomCharge0Continue

   ZoomCharge0NoSound:
      TNT1 A 0 A_JumpIfInventory("Cell",10,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto ZoomCharge0NoAmmo
      RSCP A 6 bright A_GiveInventory("RailCharge",1)
      goto ZoomCharge0Continue

   ZoomCharge0NoAmmo:
      RSCP A 6 bright
      goto ZoomCharge0Continue

   ZoomCharge0Continue:
      TNT1 A 0 A_ReFire("FireNoSound")
      RSCP A 4 bright A_GunFlash
      TNT1 A 0 A_PlayWeaponSound("weapons/railf1_2")
      TNT1 A 0 A_TakeInventory("Cell",5,1)
      TNT1 A 0 A_AlertMonsters
      RSCP A 6 bright A_RailAttack(125,0,0,"FF 00 00","FF FF FF",1,0,"RailPuff")
      RSCP A 4 bright
      RSCP A 15 bright
      TNT1 A 0 A_TakeInventory("RailCharge",3)
      goto ReadyZoom

   ZoomCharge1:
      RSCP A 6 bright
      TNT1 A 0 A_JumpIfInventory("Cell",15,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto ZoomCharge1NoAmmo
      RSCP A 6 bright A_GiveInventory("RailCharge",1)
      goto ZoomCharge1Continue

   ZoomCharge1NoAmmo:
      RSCP A 6 bright
      goto ZoomCharge1Continue

   ZoomCharge1Continue:
      TNT1 A 0 A_ReFire
      RSCP A 4 bright A_GunFlash
      TNT1 A 0 A_PlayWeaponSound("weapons/railf2_2")
      TNT1 A 0 A_TakeInventory("Cell",10,1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_RailAttack(0,0,0,"FF FF 00","FF FF FF",1,0,"RailPuff")
      RSCP A 6 bright A_RailAttack(175,0,0,"FF FF 00","FF 45 00",1,0,"RailPuff")
      RSCP A 4 bright
      RSCP A 15 bright
      TNT1 A 0 A_TakeInventory("RailCharge",3)
      goto ReadyZoom

   ZoomCharge2:
      RSCP A 12 bright
      goto ZoomCharge2Continue

   ZoomCharge2Continue:
      TNT1 A 0 A_ReFire
      RSCP A 4 bright A_GunFlash
      TNT1 A 0 A_PlayWeaponSound("weapons/railf3_2")
      TNT1 A 0 A_TakeInventory("Cell",15,1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_RailAttack(0,0,0,"00 00 FF","00 45 FF",1,0,"RailPuff")
      TNT1 A 0 A_RailAttack(0,0,0,"00 00 FF","FF FF FF",1,0,"RailPuff")
      RSCP A 6 bright A_RailAttack(225,0,0,"00 00 FF","00 FF 00",1,0,"RailPuff")
      RSCP A 4 bright
      RSCP A 15 bright
      TNT1 A 0 A_TakeInventory("RailCharge",3)
      goto ReadyZoom

   FirePowered:
      RAIG A 1 A_TakeInventory("RailCharge",3)
      TNT1 A 0 A_PlayWeaponSound("weapons/railch")
      RAIG BCD 1 bright
      RAIG I 1 bright A_GunFlash("FlashPowered")
      TNT1 A 0 A_PlayWeaponSound("weapons/railf2_2")
   FirePoweredContinue:
      TNT1 A 0 A_TakeInventory("Cell",2,1)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_RailAttack(45,0,0,"00 00 FF","FF FF FF",1,0,"RailPuff")
      RAIG J 1 bright A_RailAttack(45,0,0,"00 FF 00","FF FF FF",1,0,"RailPuff")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,1)
      goto FireEndPowered
      TNT1 A 0 A_ReFire("HoldPowered")
      goto FireEndPowered

   HoldPowered:
      RAIG A 1 A_TakeInventory("RailCharge",3)
      RAIG BCD 1 bright
      RAIG I 1 bright A_GunFlash("FlashPowered")
      TNT1 A 0 A_PlayWeaponSound("weapons/railf1_2")
      goto FirePoweredContinue

   FireEndPowered:
      RAIG K 10
      goto Ready

   ZoomFirePowered:
	  RSCP A 1 bright A_TakeInventory("RailCharge",3)
      TNT1 A 0 A_PlayWeaponSound("weapons/railch")
      RSCP A 7 bright
      RSCP A 1 bright A_GunFlash("FlashPowered")
      TNT1 A 0 A_PlayWeaponSound("weapons/railf2_2")
   ZoomFirePoweredContinue:
      TNT1 A 0 A_TakeInventory("Cell",4,1)
      TNT1 A 0 A_AlertMonsters
      TNT1 AAAA 0 A_RailAttack(30,0,0,"00 FF FF","FF FF FF",1,0,"RailPuff") // total damage: 120
      RSCP A 1 bright
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,1)
      goto ZoomFireEndPowered
      TNT1 A 0 A_ReFire("ZoomHoldPowered")
      goto ZoomFireEndPowered

   ZoomHoldPowered:
	  RSCP A 1 bright A_TakeInventory("RailCharge",3)
      RSCP A 7 bright
      RSCP A 1 bright A_GunFlash("FlashPowered")
      TNT1 A 0 A_PlayWeaponSound("weapons/railf3_2")
      goto ZoomFirePoweredContinue

   ZoomFireEndPowered:
      RSCP A 10 bright
      goto ReadyZoom

   AltFire:
   Zoom:
      TNT1 A 0 A_JumpIfInventory("RailgunScope",1,4)
      TNT1 A 0 A_PlaySound("weapons/railzoomin")
	  TNT1 A 0 A_GiveInventory("RailgunScope",1)
	  RSCP A 8 bright A_ZoomFactor(3)
	  goto ReadyZoom
      TNT1 A 0 A_PlaySound("weapons/railzoomout")
	  TNT1 A 0 A_TakeInventory("RailgunScope",1)
	  RAIG A 8 A_ZoomFactor(1)
	  goto Ready

   Flash:
      TNT1 A 4 A_Light1
      TNT1 A 6 A_Light2
      goto LightDone

   FlashPowered:
      TNT1 A 1 A_Light1
      TNT1 A 1 A_Light2
      goto LightDone
   }
}

actor RailgunPowered : Railgun { Weapon.SisterWeapon "Railgun" Weapon.AmmoUse 2 +POWERED_UP }

actor RailPuff : BulletPuff
{
    Mass 1
    Alpha 1
    DamageType "PlayerAllyRailgun"
    Species "Player"
    +MTHRUSPECIES
	States
	{
	Spawn:
        PUFF AB 4 bright
        goto Super::Melee
	}
}

actor RailCharge : Inventory { Inventory.MaxAmount 3 +UNDROPPABLE -INVBAR }
actor RailgunScope : Inventory { Inventory.MaxAmount 1 +UNDROPPABLE -INVBAR }

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// HOMING ROCKET LAUNCHER ///////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR HRL : Weapon 20103
{
//$Category Stronghold_Weapons
   spawnid 240
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "$STR_GOTHRL"
   Obituary "$STR_OB_HRL"
   Weapon.AmmoType "RocketAmmo"
   Weapon.AmmoGive 4
   Weapon.AmmoUse 1
   +ALT_USES_BOTH // only one ammo type, so this makes alt fire behave as intended
   +NOAUTOFIRE
   Tag "$STR_WEAPONTAG_HRL"
   States
   {
   Spawn:
      WHRL A -1
      stop

   Ready:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      HRLG A 1 A_WeaponReady(WRF_NOSECONDARY)
      loop
      HRLG A 1 A_WeaponReady
      loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      HRLG A 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      HRLG A 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      HRLG A 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      HRLG A 1 A_Raise
      goto Select

   Fire:
      HRLG A 4
      TNT1 A 0 A_GunFlash
      HRLG C 5 bright
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_FireCustomMissile("HomRocket1",-4,1,0,0)
      TNT1 A 0 A_FireCustomMissile("HomRocket1",4,1,0,0)
      goto FireFinish

   FirePowered:
      TNT1 A 0 A_FireCustomMissile("PowerHomRocket1",-4,1,0,4.5)
      TNT1 A 0 A_FireCustomMissile("PowerHomRocket1",4,1,0,4.5)

   FireFinish:
      HRLG D 6 bright
      HRLG E 5 bright
      HRLG F 4 bright
      HRLG B 3 A_ReFire
      goto Ready

   AltFire: // powered only
      HRLG A 4
      TNT1 A 0 A_GunFlash
      HRLG C 5 bright
      HRLG DDDD 4 bright A_FireCustomMissile("PowerHomRocket1",Random(600,-600)/100.0,1,0,4.5,0,Random(150,-150)/100.0)
      HRLG E 5 bright
      HRLG F 4 bright
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2) // so you can't exploit it
      TNT1 A 0 A_CheckReload
      goto AltFireFinish
      TNT1 A 0 A_ReFire

   AltFireFinish:
      HRLG B 3
      goto Ready

   Flash:
      TNT1 A 5 A_Light1
      TNT1 A 6 A_Light2
      goto FlashFinish

   AltFlash:
      TNT1 A 5 A_Light1
      TNT1 AAAA 4 A_Light2

   FlashFinish:
      TNT1 A 5 A_Light1
      goto LightDone
   }
}

actor HomRocket1 : Rocket
{
   Speed 30
   Damage 15
   SeeSound "weapons/hrlfir2"
   DeathSound "weapons/hrlexp2"
   DamageType "PlayerAllyRocket"
   Decal Scorch
   Species "Player"
   +THRUSPECIES
   +SEEKERMISSILE
   -DEHEXPLOSION
   States
   {
   Spawn:
      HMIS A 4 bright A_SeekerMissile(10,25,SMF_LOOK)
      loop
   Death:
      TNT1 A 0 A_SetTranslucent(0.67,1)
      MISL B 8 bright A_Explode
      MISL C 6 bright
      MISL D 4 bright
      stop
   }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// ROCKET LAUNCHER //////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

ACTOR DERLauncher : RocketLauncher replaces RocketLauncher
{
   +ALT_USES_BOTH
   States
   {
   Ready:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      MISG A 1 A_WeaponReady(WRF_NOSECONDARY)
      loop
      MISG A 1 A_WeaponReady
      loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      MISG A 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      MISG A 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      MISG A 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      MISG A 1 A_Raise
      goto Select

   Fire:
      MISF A 4 bright A_GunFlash
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_FireCustomMissile("Rocket2",0,1,0,0)
      MISF BCD 5 bright
      TNT1 A 0 A_ReFire
      goto Ready

   FirePowered:
      TNT1 A 0 A_GunFlash("FlashPowered")
      MISF B 2 bright A_FireCustomMissile("PowerRocket2",0,1,0,0)
      MISF CD 4 bright
      TNT1 A 0 A_ReFire
      goto Ready

   AltFire:
      MISF A 4 bright A_GunFlash
      MISF B 2 bright A_FireCustomMissile("PowerRocket2",Random(5,-5)/10.0,1,0,0,0,Random(6,-6)/10.0)
      MISF A 4 bright A_GunFlash("AltFlash2")
      MISF B 2 bright A_FireCustomMissile("PowerRocket2",Random(10,-10)/10.0,1,0,0,0,Random(8,-8)/10.0)
      MISF A 4 bright A_GunFlash("AltFlash3")
      MISF B 2 bright A_FireCustomMissile("PowerRocket2",Random(15,-15)/10.0,1,0,0,0,Random(10,-10)/10.0)
      MISF CD 4 bright
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      TNT1 A 0 A_CheckReload
      goto Ready
      TNT1 A 0 A_ReFire
      goto Ready

   Flash:
      TNT1 A 4 A_Light1
      TNT1 A 5 A_Light2
      TNT1 A 5 A_Light1
      goto LightDone

   FlashPowered:
      TNT1 A 2 A_Light2

   FlashPoweredFinish:
      TNT1 A 4 A_Light1
      goto LightDone

   AltFlash:
      TNT1 A 4 A_Light1
      TNT1 A 2 A_Light2
   AltFlash2:
      TNT1 A 4 A_Light1
      TNT1 A 2 A_Light2
   AltFlash3:
      TNT1 A 4 A_Light1
      goto FlashPowered
   }
}

actor Rocket2 : Rocket
{
  Decal Scorch
  Damagetype "PlayerAllyRocket"
  Species "Player"
  +THRUSPECIES
  -DEHEXPLOSION
  states
  {
  Death:
    TNT1 A 0 A_SetTranslucent(0.67,1)
    goto Super::Death
  }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PLASMA RIFLE /////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

ACTOR DEPlasmaRifle : PlasmaRifle replaces PlasmaRifle
{
   +ALT_USES_BOTH
   States
   {
   Ready: // This is messy, but it must be done.
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG A 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready2
      PLSG A 1 bright A_WeaponReady
   Ready2:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG A 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready3
      PLSG A 1 bright A_WeaponReady
   Ready3:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG A 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready4
      PLSG A 1 bright A_WeaponReady
   Ready4:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG B 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready5
      PLSG B 1 bright A_WeaponReady
   Ready5:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG B 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready6
      PLSG B 1 bright A_WeaponReady
   Ready6:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG B 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready7
      PLSG B 1 bright A_WeaponReady
   Ready7:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG C 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready8
      PLSG C 1 bright A_WeaponReady
   Ready8:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG C 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready9
      PLSG C 1 bright A_WeaponReady
   Ready9:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      PLSG C 1 bright A_WeaponReady(WRF_NOSECONDARY)
      goto Ready
      PLSG C 1 bright A_WeaponReady
      goto Ready

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      PLSG E 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      PLSG E 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      PLSG E 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      PLSG E 1 A_Raise
      goto Select

   Fire:
      TNT1 A 0 A_GunFlash
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_FireCustomMissile("StrPlasmaBall",0,1,0,0)
      goto FireFinish

   FirePowered:
      TNT1 A 0 A_FireCustomMissile("PowerPlasmaBall",0,1,0,0)

   FireFinish:
      PLSG FGH 1 bright
      PLSG D 20 bright A_ReFire
      goto Ready

   AltFire:
      TNT1 A 0 A_FireCustomMissile("PowerPlasmaBall2",Random(150,-150)/100.0,1,0,0,0,Random(150,-150)/100.0)
      PLSG FGH 1 bright
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
      TNT1 A 0 A_CheckReload
      goto AltFireFinish
      TNT1 A 0 A_ReFire

   AltFireFinish:
      PLSG D 20 bright
      goto Ready

   Flash:
      TNT1 A 1 A_Light1
      TNT1 A 1 A_Light2
      TNT1 A 1 A_Light1
      goto LightDone
   }
}

ACTOR StrPlasmaBall : PlasmaBall
{
    Species "Player"
    +THRUSPECIES
    DamageType "PlayerAllyPlasma"
    Decal PlasmaScorch
}

//////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////  BFG9000   ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

actor StrBFGReadySoundCheck : Inventory { Inventory.MaxAmount 1 +UNDROPPABLE -INVBAR }

actor DEBFG9000 : BFG9000 Replaces BFG9000
{
  Weapon.SisterWeapon "DEBFG9000Powered"
  States
  {
  Ready:
      TNT1 A 0 A_PlaySound("weapons/bfg_hum",6,1,1)
      TNT1 A 0 A_GiveInventory("StrBFGReadySoundCheck")
      BFGG ABCD 3 bright A_WeaponReady
      loop

  Deselect:
      TNT1 A 0 A_JumpIfInventory("StrBFGReadySoundCheck",1,1)
      goto Deselect+3
      TNT1 A 0 A_StopSound(6)
      TNT1 A 0 A_TakeInventory("StrBFGReadySoundCheck")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      BFGG E 1 A_Lower
      goto Deselect+3

  DeselectPowered:
      TNT1 AA 0 A_Lower
      BFGG E 1 A_Lower
      goto Deselect+3

  Select:
      TNT1 A 0 A_TakeInventory("StrBFGReadySoundCheck")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      BFGG E 1 A_Raise
      loop

  SelectPowered:
      TNT1 AA 0 A_Raise
      BFGG E 1 A_Raise
      goto Select

  Fire:
      TNT1 A 0 A_JumpIfInventory("StrBFGReadySoundCheck",1,1)
      goto Fire+3
      TNT1 A 0 A_StopSound(6)
      TNT1 A 0 A_TakeInventory("StrBFGReadySoundCheck")
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_PlayWeaponSound("weapons/bfgf")
      BFGG FGHIJ 5 bright
      BFGG K 5 bright A_GunFlash
      TNT1 A 0 A_JumpIfInventory("Cell",40,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto FireFinish
      TNT1 A 0 A_TakeInventory("Cell",40,1)
      TNT1 A 0 A_FireCustomMissile("StrBFGBall",0,0,0,0)

  FireFinish:
      BFGG LM 5 bright
      BFGG NEEE 5 bright A_ReFire
      goto Ready

  FirePowered:
      BFGG FHJ 1 bright
      BFGG K 1 bright A_GunFlash("FlashPowered")
      TNT1 A 0 A_JumpIfInventory("Cell",5,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto FirePoweredFinish
      TNT1 A 0 A_TakeInventory("Cell",5,1)
      TNT1 A 0 A_FireCustomMissile("PowerBFGBall",0,0,0,0)

  FirePoweredFinish:
      BFGG LN 1 bright
      BFGG E 7 bright A_ReFire
      goto Ready

  Flash:
      TNT1 A 5 A_Light1
      TNT1 A 5 A_Light2
      TNT1 A 5 A_Light1
      goto LightDone

  FlashPowered:
      TNT1 A 1 A_Light1
      TNT1 A 1 A_Light2
      TNT1 A 1 A_Light1
      goto LightDone
  }
}

actor DEBFG9000Powered : DEBFG9000 { Weapon.SisterWeapon "DEBFG9000" Weapon.AmmoUse 5 +POWERED_UP }

ACTOR StrBFGBall : BFGBall
{
Decal BFGLightning
//Damage 100
DamageType "PlayerAllyBFG"
Species "Player"
+THRUSPECIES
+FORCERADIUSDMG
  States
  {
  Spawn:
    BFS1 AB 4 Bright
    Loop
  Death:
    BFE1 A 8 bright A_BFGSpray("StrBFGExtra",40,8)
    BFE1 B 8 bright A_Explode(128,256,0)
    TNT1 A 0 A_BFGSpray("StrBFGExtra",40,16)
    BFE1 CDEF 8 bright
    Stop
  }
}

ACTOR StrBFGExtra : BFGExtra { DamageType "PlayerAllyBFGSplash" Species "Player" +MTHRUSPECIES }

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PYRO CANNON //////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR PyroCannon : Weapon 20107
{
//$Category Stronghold_Weapons
   spawnid 239
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "$STR_GOTPYROCANNON"
   Obituary "$STR_OB_PYROCANNON"
   Weapon.AmmoType "Gas"
   Weapon.AmmoGive 48
   Weapon.AmmoUse 48
   Weapon.BobRangeX 0
   Tag "$STR_WEAPONTAG_PYROCANNON"
   +WEAPON.EXPLOSIVE
   +WEAPON.BFG
   //+WEAPON.DONTBOB
   +WEAPON.STAFF2_KICKBACK
   +NOAUTOFIRE
   States
   {
   Spawn:
      WPYR A -1
      stop

   Ready:
      PYRG A 1 A_WeaponReady
      loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      PYRG A 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      PYRG A 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      PYRG A 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      PYRG A 1 A_Raise
      goto Select

   Fire:
      PYRG A 5
      PYRG B 8 bright A_GunFlash
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_FireCustomMissile("PyroShot",0,1,0,0)
      goto FireFinish

   FirePowered:
      TNT1 A 0 A_FireCustomMissile("ClusterPyroShot",0,1,0,0)

   FireFinish:
      PYRG C 10 bright
      PYRG DEFG 5 bright
      PYRG A 5 A_ReFire
      goto Ready

   Flash:
      TNT1 A 8 A_Light1
      TNT1 A 10 A_Light2
      TNT1 A 15 A_Light1
      goto LightDone
   }
}

ACTOR Gas : Ammo 22106
{
//$Category Stronghold_Weapons
   SpawnID 222
   RENDERSTYLE Normal
   Inventory.PickupSound "misc/pickup"
   Inventory.Amount 24
   Inventory.MaxAmount 240
   Ammo.BackpackAmount 24
   Ammo.BackpackMaxAmount 480
   Inventory.PickupMessage "$STR_GOTGAS"
   Inventory.Icon AGASA0
   Tag "$STR_AMMOTAG_GAS"
   States
   {
   Spawn:
      AGAS A -1
      stop
   }
}

ACTOR Fuel : Gas 22107
{
//$Category Stronghold_Weapons
   SpawnID 221
   Inventory.Amount 96
   Inventory.PickupMessage "$STR_GOTFUEL"
   States
   {
   Spawn:
      AGAS B -1
      stop
   }
}

ACTOR PyroShot
{
   Radius 8
   Height 12
   Speed 30
   Damage 180
   PROJECTILE
   RENDERSTYLE ADD
   DamageType "PlayerAllyFire"
   ALPHA 0.95
   Scale 0.75
   Species "Player"
   +THRUSPECIES
   SeeSound "weapons/hellfi2"
   DeathSound "weapons/hellex2"
   Decal BigScorch
   States
   {
   Spawn:
      PBAL ABA 4 Bright
      PBAL BBAA 2 Bright A_Explode(128,128)
      goto Spawn+3
   Death:
      TNT1 A 0 A_NoGravity
      PYXP AB 2 Bright
      PYXP C 2 Bright A_Explode(192,128)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,0,4)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,45,4)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,90,4)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,135,4)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,180,4)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,225,4)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,270,4)
      TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,315,4)
      TNT1 A 0 A_GiveInventory("StrAMushroomBypassOn") // here is a hack that "nullifies" the hardcoded (and unchangeable) A_Explode effect that A_Mushroom is unfortunate to have
      TNT1 A 0 A_Mushroom("PyroFlare",16,MSF_DontHurt) // MSF_DontHurt tracks kills, but it doesn't hurt the owner at the same time. can't have one or the other. lame.
      TNT1 A 0 A_GiveInventory("StrAMushroomBypassOff") // stupid hackaround for hardcoded stuff argh why zdoom
      TNT1 A 0 A_SetDamageType("PlayerAllyFire")
      TNT1 A 0 A_Explode(128,128) // ensure everything (including the owner) takes the same amount of damage by this, as per A_Mushroom without MSF_DontHurt
      PYXP DEFGHIJKLMNOPQRSTU 2 Bright
      stop
   }
}

ACTOR PyroSpawner
{
   Radius 1
   Height 1
   Damage (0)
   PROJECTILE
   Speed 15
   RENDERSTYLE NONE
   +THRUACTORS
   +BLOODLESSIMPACT
   States
   {
   Spawn:
      TNT1 AAAAA 5 A_CustomMissile("PyroBoom",0,0,0,4)
      stop
   }
}

ACTOR PyroBoom
{
   Radius 0
   Height 1
   PROJECTILE
   RENDERSTYLE ADD
   DamageType "PlayerAllyFire"
   ALPHA 0.95
   Scale 0.67
   Species "Player"
   +THRUSPECIES
   +FORCERADIUSDMG
   SeeSound "weapons/boom1_2"
   States
   {
   Spawn:
      PYXP AB 2 Bright
      TNT1 A 0 A_Explode(192,128)
      PYXP CDEFGHIJKLMNOPQRSTU 2 Bright
      stop
   }
}

ACTOR PyroFlare
{
   Radius 8
   Height 11
   Speed 35
   Damage 10
   PROJECTILE
   RENDERSTYLE ADD
   DamageType "PlayerAllyFire"
   Decal DoomImpScorch
   ALPHA 0.95
   Species "Player"
   +THRUSPECIES
   -NOGRAVITY
   DeathSound "weapons/bang1_2"
   States
   {
   Spawn:
      TNT1 A 1 A_SpawnItemEx("PyroFX",0,0,0,0,0,0,0,128)
      loop
   Death:
      TNT1 A 0 A_NoGravity
      FRFX HIJ 2 Bright
      FRFX J 0 A_Explode(64,64)
      FRFX J 0 A_SpawnItemEx("PyroDropFire",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
      FRFX KLMNO 2 Bright
      stop
   }
}

ACTOR PyroFX
{
   Radius 0
   Height 1
   Speed 0
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.85
   States
   {
   Spawn:
      TNT1 A 3
      PYFX ABCDE 3 Bright
      Stop
   }
}

ACTOR PyroDropFire
{
   Radius 8
   Height 40
   PROJECTILE
   RENDERSTYLE ADD
   DamageType "PlayerAllyFire"
   ALPHA 0.9
   //DONTHURTSHOOTER // No feeling safe for you today.
   Species "Player"
   +THRUSPECIES
   -NOGRAVITY
   +LOWGRAVITY
   +NOEXPLODEFLOOR
   +NODAMAGETHRUST
   +DONTSPLASH
   States
   {
   Spawn:
      FLME A 0 Bright
      FLME A 0 Bright A_Playsound("weapons/onfire2")
      FLME A 2 Bright A_Explode(5,32)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16)
      TNT1 A 0 A_Playsound("weapons/onfire2")
      FLME A 2 Bright A_Explode(5,32)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16)
      TNT1 A 0 A_Playsound("weapons/onfire2")
      FLME A 2 Bright A_Explode(5,32)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16)
      TNT1 A 0 A_Playsound("weapons/onfire2")
      FLME A 2 Bright A_Explode(5,32)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16)
      TNT1 A 0 A_Playsound("weapons/onfire2")
      FLME A 2 Bright A_Explode(5,32)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16)
      FLME A 0 A_Jump(192,1)
      loop
   Death:
      TNT1 A 0
      stop
   }
}

ACTOR ClusterPyroShot : PyroShot
{
	States
	{
	Death:
    TNT1 A 0 A_GiveInventory("StrAMushroomBypassOn")
	TNT1 A 0 A_Mushroom("PyroShot",4,MSF_DontHurt)
	TNT1 A 0 A_Mushroom("PyroFlare",32,MSF_DontHurt)
    TNT1 A 0 A_GiveInventory("StrAMushroomBypassOff")
    TNT1 A 0 A_SetDamageType("PlayerAllyFire")
    TNT1 A 0 A_Explode(256,128)
	Stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// SHOTGUN //////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

actor DEShotgun : Shotgun replaces Shotgun
{
  AttackSound "weapons/shotgf"
  +ALT_USES_BOTH
  States
  {
  Ready:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    SHTG A 1 A_WeaponReady(WRF_NOSECONDARY)
    loop
    SHTG A 1 A_WeaponReady
    loop

  Deselect:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
    TNT1 A 0 A_Lower
    SHTG A 1 A_Lower
    loop

  DeselectPowered:
    TNT1 AA 0 A_Lower
    SHTG A 1 A_Lower
    goto Deselect

  Select:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
    TNT1 A 0 A_Raise
    SHTG A 1 A_Raise
    loop

  SelectPowered:
    TNT1 AA 0 A_Raise
    SHTG A 1 A_Raise
    goto Select

  Fire:
    SHTG A 3 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
    TNT1 A 0 A_GunFlash
    TNT1 A 0 A_FireBullets(4,3,8,5,"NewPuffPlayer")
    SHTG EFG 2 bright
    SHTG BCDCB 4
    SHTG A 3
    SHTG A 5 A_ReFire
    goto Ready

  FirePowered:
    SHTG A 2
    TNT1 A 0 A_GunFlash
    TNT1 A 0 A_FireBullets(3,2.5,10,12,"ElectricalPuff")
    SHTG EFG 2 bright
    SHTG BCDCB 2
    SHTG A 1 A_ReFire
    goto Ready

  AltFire:
    SHTG A 3
    TNT1 A 0 A_GunFlash
    TNT1 A 0 A_FireBullets(12,10,8,9,"RepPuffNoDamage")
    SHTG EFG 3 bright
    SHTG BCDCB 3
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    TNT1 A 0 A_CheckReload
    goto AltFireFinish
    TNT1 A 0 A_ReFire

  AltFireFinish:
    SHTG A 1
    goto Ready

  Flash:
    TNT1 A 3 A_Light1
    TNT1 A 3 A_Light2
    goto LightDone

  AltFlash:
    TNT1 A 4 A_Light1
    TNT1 A 5 A_Light2
    goto LightDone
  }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// FLAMER ///////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR Flamer : Weapon 20105
{
//$Category Stronghold_Weapons
   spawnid 238
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "$STR_GOTFLAMER"
   Obituary "$STR_OB_FLAMER"
   Weapon.AmmoType "Gas"
   Weapon.AmmoGive 48
   Weapon.AmmoUse 1
   Tag "$STR_WEAPONTAG_FLAMER"
   +WEAPON.EXPLOSIVE
   States
   {
   Spawn:
      WFLM A -1
      stop

   Ready: // Not as messy as the plasma rifle.
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG B 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG B 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG B 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG C 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG C 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG C 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG D 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG D 1 A_WeaponReady
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReadyPowered")
      FLMG D 1 A_WeaponReady
      loop

   ReadyPowered:
      FLMG A 1 A_WeaponReady
      goto Ready

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      FLMG A 0 A_Lower
      FLMG A 1 A_Lower
      loop

   DeselectPowered:
      FLMG AA 0 A_Lower
      FLMG A 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      FLMG A 0 A_Raise
      FLMG A 1 A_Raise
      loop

   SelectPowered:
      FLMG AA 0 A_Raise
      FLMG A 1 A_Raise
      goto Select

   Fire:
      TNT1 A 0 A_GunFlash
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      FLMG E 1 bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
      FLMG F 1 bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
      FLMG G 1 bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
      TNT1 A 0 A_GunFlash
      FLMG E 1 bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
      FLMG F 1 bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
      FLMG G 1 bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
      TNT1 A 0 A_GunFlash
      FLMG E 1 bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
      FLMG F 1 bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
      FLMG G 1 bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
      FLMG H 2 bright A_ReFire
      goto Ready

   FirePowered:
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
      TNT1 A 0 A_GunFlash
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
      TNT1 A 0 A_GunFlash
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,0,random(-4,4),0)
      FLMG A 1 A_FireCustomMissile("PowerFTFire",0,1,random(-4,4),0)
      FLMG A 2 A_ReFire
      goto Ready

   Flash:
      TNT1 A 2 A_Light1
      TNT1 A 2 A_Light2
      TNT1 A 1 A_Light1
      goto LightDone
   }
}

ACTOR FTFire
{
   Radius 2
   Height 4
   Speed 16
   Damage 1
   PROJECTILE
   RENDERSTYLE ADD
   DamageType "PlayerAllyFire"
   ALPHA 0.67
   Scale 0.67
   SeeSound "weapons/flamer2"
   DeathSound "weapons/scorch2"
   DONTHURTSHOOTER
   Species "Player"
   +THRUSPECIES
   +DONTSPLASH
   States
   {
   Spawn:
      TNT1 A 2
      FRFX ABCD 2 Bright A_Explode(5,8,0)
      TNT1 A 0 A_LowGravity
      FRFX EFG 2 Bright A_Explode(5,16,0)
      FRFX HIJ 2 Bright A_Explode(5,32,0)
      FRFX KLM 2 Bright A_Explode(5,64,0)
      FRFX NO 2 Bright
      stop
   Death:
      TNT1 A 0 A_NoGravity
      FRFX HIJ 2 Bright A_Explode(5,32,0)
      TNT1 A 0 A_CustomMissile("DropFire",0,0,0,4)
      FRFX KLM 2 Bright A_Explode(5,64,0)
      FRFX NO 2 Bright
      stop
   }
}

ACTOR DropFire
{
   Radius 8
   Height 40
   PROJECTILE
   RENDERSTYLE ADD
   DamageType "PlayerAllyFire"
   ALPHA 0.9
   DONTHURTSHOOTER
   Species "Player"
   +THRUSPECIES
   -NOGRAVITY
   +LOWGRAVITY
   +NOEXPLODEFLOOR
   +NODAMAGETHRUST
   +DONTSPLASH
   States
   {
   Spawn:
      FLME A 0 Bright
      FLME A 0 Bright A_Playsound("weapons/onfire3")
      FLME A 2 Bright A_Explode(5,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
      TNT1 A 0 A_Playsound("weapons/onfire3")
      FLME A 2 Bright A_Explode(5,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
      TNT1 A 0 A_Playsound("weapons/onfire3")
      FLME A 2 Bright A_Explode(5,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
      TNT1 A 0 A_Playsound("weapons/onfire3")
      FLME A 2 Bright A_Explode(5,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
      TNT1 A 0 A_Playsound("weapons/onfire3")
      FLME A 2 Bright A_Explode(5,32,0)
      FLME BCDEFGHIJKLMN 2 Bright A_Explode(5,16,0)
      FLME A 0 A_Jump(192,1)
      loop
   Death:
      TNT1 A 0
      stop
   }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// CHAINGUN /////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

actor StrChaingunFiringSoundCheck : Inventory { Inventory.MaxAmount 1 +UNDROPPABLE -INVBAR }

actor DEChaingun : Chaingun replaces Chaingun
{
  +ALT_USES_BOTH
  States
  {
  Ready:
    TNT1 A 0 A_TakeInventory("StrChaingunFiringSoundCheck")
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    CHGG A 1 A_WeaponReady(WRF_NOSECONDARY)
    loop
    CHGG A 1 A_WeaponReady
    loop

  Deselect:
    TNT1 A 0 A_JumpIfInventory("StrChaingunFiringSoundCheck",1,1)
    goto Deselect+3
    TNT1 A 0 A_StopSoundEx("Weapon")
    TNT1 A 0 A_TakeInventory("StrChaingunFiringSoundCheck")
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
    TNT1 A 0 A_Lower
    CHGG A 1 A_Lower
    goto Deselect+3

  DeselectPowered:
    TNT1 AA 0 A_Lower
    CHGG A 1 A_Lower
    goto Deselect+3

  Select:
    TNT1 A 0 A_TakeInventory("StrChaingunFiringSoundCheck")
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
    TNT1 A 0 A_Raise
    CHGG A 1 A_Raise
    loop

  SelectPowered:
    TNT1 AA 0 A_Raise
    CHGG A 1 A_Raise
    goto Select

  Fire:
    TNT1 A 0 A_PlaySoundEx("weapons/chnglp","Weapon",1)
    TNT1 A 0 A_GiveInventory("StrChaingunFiringSoundCheck")
    TNT1 A 0 A_GunFlash
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
    CHGG C 2 bright A_FireBullets (4,2.3,1,5,"NewPuffPlayer")
    CHGG D 1 bright
    TNT1 A 0 A_GunFlash
    CHGG E 2 bright A_FireBullets (4,2.3,1,5,"NewPuffPlayer")
    CHGG F 1 bright
    goto FireFinish

  FirePowered:
    CHGG C 2 bright A_FireBullets (0.75,0.75,-1,20,"ElectricalPuff",0)
    CHGG D 1 bright
    TNT1 A 0 A_GunFlash
    CHGG E 2 bright A_FireBullets (0.75,0.75,-1,20,"ElectricalPuff",1)
    CHGG F 1 bright
    TNT1 A 0 A_GunFlash
    CHGG C 2 bright A_FireBullets (0.75,0.75,-1,20,"ElectricalPuff",0)
    CHGG D 1 bright
    TNT1 A 0 A_GunFlash
    CHGG E 2 bright A_FireBullets (0.75,0.75,-1,20,"ElectricalPuff",1)
    CHGG F 1 bright

  FireFinish:
    CHGG A 0 A_ReFire
    CHGG A 0 A_StopSoundEx("Weapon")
    CHGG A 0 A_PlaySoundEx("weapons/chngdn","Weapon",0)
    TNT1 A 0 A_TakeInventory("StrChaingunFiringSoundCheck")
    goto Ready

  AltFire:
    TNT1 A 0 A_PlaySoundEx("weapons/chnglp","Weapon",1)
    TNT1 A 0 A_GiveInventory("StrChaingunFiringSoundCheck")
    TNT1 A 0 A_GunFlash
    CHGG C 2 bright A_FireBullets (7.5,3,-1,16,"RepPuffNoDamage",1)
    CHGG D 1 bright
    TNT1 A 0 A_GunFlash
    CHGG E 2 bright A_FireBullets (7.5,3,-1,16,"RepPuffNoDamage",1)
    CHGG F 1 bright
    TNT1 A 0 A_GunFlash
    CHGG C 2 bright A_FireBullets (7.5,3,-1,16,"RepPuffNoDamage",1)
    CHGG D 1 bright
    TNT1 A 0 A_GunFlash
    CHGG E 2 bright A_FireBullets (7.5,3,-1,16,"RepPuffNoDamage",1)
    CHGG F 1 bright
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    TNT1 A 0 A_CheckReload
    goto AltFireFinish
    TNT1 A 0 A_ReFire

  AltFireFinish:
    TNT1 A 0 A_StopSoundEx("Weapon")
    TNT1 A 0 A_PlaySoundEx("weapons/chngdn","Weapon",0)
    TNT1 A 0 A_TakeInventory("StrChaingunFiringSoundCheck")
    goto Ready

  Flash:
    TNT1 A 1 A_Light1
    TNT1 A 1 A_Light2
    TNT1 A 1 A_Light1
    goto LightDone

  Spawn:
    CHNG A -1
    stop
  }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// PISTOL ///////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

actor DEPistol : Weapon replaces Pistol 
{
  Obituary "$OB_MPPISTOL"
  AttackSound "weapons/pistol"
  Weapon.SelectionOrder 1900
  Weapon.KickBack 100
  Weapon.AmmoType1 "Clip"
  Weapon.AmmoType2 "Clip"
  Weapon.AmmoUse1 1
  Weapon.AmmoUse2 8
  Weapon.AmmoGive 20
  Tag "$TAG_PISTOL"
  Inventory.PickupMessage "$PICKUP_PISTOL_DROPPED" // "Picked up a pistol."
  +WEAPON.WIMPY_WEAPON
  States
  {
  Spawn:
    PISW A -1
    stop

  Ready:
    PISG A 1 A_JumpIfInventory("PCount",1,1)
    goto ReloadAnim
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    PISG A 1 A_WeaponReady(WRF_NOSECONDARY)
    loop
    PISG A 1 A_WeaponReady
    loop

  Deselect:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
    TNT1 A 0 A_Lower
    PISG A 1 A_Lower
    loop

  DeselectPowered:
    TNT1 AA 0 A_Lower
    PISG A 1 A_Lower
    goto Deselect

  Select:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
    TNT1 A 0 A_Raise
    PISG A 1 A_Raise
    loop

  SelectPowered:
    TNT1 AA 0 A_Raise
    PISG A 1 A_Raise
    goto Select

  Fire:
    PISG A 3 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
    TNT1 A 0 A_FireBullets(5.6,0,1,5,"NewPuffPlayer")
    PISF A 3 bright A_GunFlash
    PISG C 3
    PISG B 3 A_ReFire
    goto Ready

  FirePowered:
    PISG A 0 A_FireBullets(4,2.5,-1,12,"ElectricalPuff")
    PISG B 3 A_GunFlash("FlashPowered")
    PISG CBA 3 A_ReFire
    goto Ready

  AltFire:
    PISG A 4
    TNT1 A 0 A_FireBullets(10,7,8,12,"ElectricalPuff")
    PISF A 4 bright A_GunFlash
    PISG CB 4
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    TNT1 A 0 A_CheckReload
    goto AltFireFinish
    TNT1 A 0 A_ReFire

  AltFireFinish:
    PISG A 4
    goto Ready

  ReloadAnim:
    PISG D 5 A_PlaySound("weapons/clipou")
    PISG EFGHIJK 4
    PISG L 4 A_PlaySound("weapons/clipin")
    PISG MNO 4
    PISG P 4 A_PlaySound("weapons/clipre")
    PISG QR 4
    PISG S 4 A_GiveInventory("PCount",1)
    goto Ready

  Flash:
    TNT1 A 4 A_Light1
    goto LightDone

  FlashPowered:
    PISF A 2 bright A_Light1
    goto LightDone

  AltFlash:
    TNT1 A 5 A_Light1
    goto LightDone
  }
}

actor PCount : Inventory { Inventory.MaxAmount 1 +UNDROPPABLE -INVBAR }

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// SUPER SHOTGUN ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

actor DESuperShotgun : SuperShotgun replaces SuperShotgun
{
  AttackSound "weapons/sshotf"
  Weapon.AmmoType2 "Shell"
  Weapon.AmmoUse2 4
  States
  {
  Ready:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    SHT2 A 1 A_WeaponReady(WRF_NOSECONDARY)
    loop
    SHT2 A 1 A_WeaponReady
    loop

  Deselect:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
    TNT1 A 0 A_Lower
    SHT2 A 1 A_Lower
    loop

  DeselectPowered:
    TNT1 AA 0 A_Lower
    SHT2 A 1 A_Lower
    goto Deselect

  Select:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
    TNT1 A 0 A_Raise
    SHT2 A 1 A_Raise
    loop

  SelectPowered:
    TNT1 AA 0 A_Raise
    SHT2 A 1 A_Raise
    goto Select

  Fire:
    SHT2 A 3 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
    TNT1 A 0 A_GunFlash
    SHT2 M 2 bright A_FireBullets (11.2,7.1,20,5,"NewPuffPlayer")
    SHT2 N 3 bright
    SHT2 O 2 bright
    SHT2 B 3
    SHT2 C 4
    SHT2 D 4 A_CheckReload
    SHT2 C 3
    SHT2 E 3 A_PlayWeaponSound("weapons/sshoto")
    SHT2 F 3
    SHT2 GH 4
    SHT2 I 3 A_PlayWeaponSound("weapons/sshotl")
    SHT2 JK 3
    SHT2 L 4
    TNT1 A 0 A_PlayWeaponSound("weapons/sshotc")
    SHT2 E 6 A_ReFire
    SHT2 A 5 A_ReFire
    goto Ready

  FirePowered:
    SHT2 A 2
    TNT1 A 0 A_GunFlash("AltFlash")
    TNT1 A 0 A_FireBullets(14.6,9.4,20,12,"ElectricalPuff")
    SHT2 MNO 2 bright
    SHT2 A 2
    TNT1 A 0 A_GunFlash("AltFlash")
    TNT1 A 0 A_FireBullets(14.6,9.4,20,12,"ElectricalPuff")
    SHT2 MNO 2 Bright
    SHT2 BC 2
    SHT2 D 2 A_CheckReload
    SHT2 C 2
    SHT2 E 2 A_PlayWeaponSound("weapons/sshoto")
    SHT2 FGH 2
    SHT2 I 2 A_PlayWeaponSound("weapons/sshotl")
    SHT2 JKL 2
    TNT1 A 0 A_PlayWeaponSound("weapons/sshotc")
    SHT2 EA 2 A_ReFire
    goto Ready

  AltFire:
    SHT2 A 2
    TNT1 A 0 A_GunFlash
    TNT1 A 0 A_FireBullets(14.6,9.4,20,3,"RepPuffNoDamage")
    SHT2 MNO 2 Bright
    SHT2 A 2
    TNT1 A 0 A_GunFlash
    TNT1 A 0 A_FireBullets(14.6,9.4,20,3,"RepPuffNoDamage")

  FirePoweredFinish:
    SHT2 MNO 2 Bright
    SHT2 BC 2
    SHT2 D 2 A_CheckReload
    SHT2 C 2
    SHT2 E 2 A_PlayWeaponSound("weapons/sshoto")
    SHT2 FGH 2
    SHT2 I 2 A_PlayWeaponSound("weapons/sshotl")
    SHT2 JKL 2
    TNT1 A 0 A_PlayWeaponSound("weapons/sshotc")
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    TNT1 A 0 A_CheckReload
    goto AltFireFinish
    TNT1 A 0 A_ReFire

  AltFireFinish:
    SHT2 E 2
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,2)
    TNT1 A 0 A_CheckReload
    goto AltFireFinish2
    TNT1 A 0 A_ReFire

  AltFireFinish2:
    SHT2 A 2
    goto Ready

  Flash:
    TNT1 A 2 A_Light1
    TNT1 A 3 A_Light2
    goto FlashFinish

  AltFlash:
    TNT1 A 2 A_Light1
    TNT1 A 2 A_Light2

  FlashFinish:
    TNT1 A 2 A_Light1
    goto LightDone
  }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// AUTO SHOTGUN /////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
actor AutoShotgun : Weapon 5118
{
//$Category Stronghold_Weapons
  SpawnID 237
  Obituary "$STR_OB_AUTOSHOTGUN"
  Inventory.PickupMessage "$STR_GOTAUTOSHOTGUN"
  Tag "$STR_WEAPONTAG_AUTOSHOTGUN"
  Weapon.SelectionOrder 700
  Weapon.AmmoType1 "ASGClip"
  Weapon.AmmoType2 "Shell"
  Weapon.AmmoGive1 0
  Weapon.AmmoGive2 20 // 8
  Weapon.AmmoUse1 1
  AttackSound "weapons/asgfir"
  +AMMO_OPTIONAL
  +NOALERT
  states
  {
  Spawn:
    WASG A -1
    stop

  Ready:
  	TNT1 A 0 A_JumpIfInventory("ASGClip",0,2)
	TNT1 A 0 A_JumpIfInventory("Shell",1,2)
	ASGG A 1 A_WeaponReady(WRF_NOSECONDARY)
	loop
    ASGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    loop

  Deselect:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
    TNT1 A 0 A_Lower
    ASGG A 1 A_Lower
    loop

  DeselectPowered:
    TNT1 AA 0 A_Lower
    ASGG A 1 A_Lower
    goto Deselect

  Select:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
    TNT1 A 0 A_Raise
    ASGG A 1 A_Raise
    loop

  SelectPowered:
    TNT1 AA 0 A_Raise
    ASGG A 1 A_Raise
    goto Select

  Fire:
    TNT1 A 0 A_JumpIfInventory("ASGClip",1,"FireActual")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, "FireActual")
    TNT1 A 0 A_JumpIfInventory("Shell",1,"Reload")
    ASGG A 2 A_PlayWeaponSound("weapons/asgout")
    goto Ready

  FireActual:
    TNT1 A 0 A_AlertMonsters
    ASGG A 1 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
    TNT1 A 0 A_GunFlash
    ASGG L 2 bright A_FireBullets(3.1,2.6,7,5,"NewPuffPlayer")
    ASGG M 3 bright
    ASGG N 2 bright
    ASGG A 7 A_PlaySoundEx("weapons/asgld1","auto",0)
    TNT1 A 0 A_ReFire
    goto Ready

  FirePowered:
    TNT1 A 0 A_GunFlash("FlashPowered")
    TNT1 A 0 A_FireBullets(8.6,5.1,8,5,"ElectricalPuff",0) //Yes, I nerfed its accuracy. However, you get a chain-shotgun.
    ASGG LMN 1 bright
    ASGG A 1 A_PlaySoundEx("weapons/asgld1","auto",0)
    TNT1 A 0 A_GunFlash("FlashPowered")
    TNT1 A 0 A_FireBullets(8.6,5.1,8,5,"ElectricalPuff",0)
    ASGG LMN 1 bright
    ASGG A 1 A_PlaySoundEx("weapons/asgld1","auto",0)
    TNT1 A 0 A_GunFlash("FlashPowered")
    TNT1 A 0 A_FireBullets(8.6,5.1,8,5,"ElectricalPuff",0)
    ASGG LMN 1 bright
    ASGG A 1 A_PlaySoundEx("weapons/asgld1","auto",0)
    TNT1 A 0 A_GunFlash("FlashPowered")
    TNT1 A 0 A_FireBullets(8.6,5.1,8,5,"ElectricalPuff")
    ASGG LMN 1 bright
    ASGG A 1 A_PlaySoundEx("weapons/asgld1","auto",0)
    TNT1 A 0 A_ReFire
    goto Ready

  AltFire:
  Reload:
    TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ReloadPowered")
    ASGG BCDEF 3
    ASGG G 3 A_PlayWeaponSound("weapons/asgout")
    ASGG HIJ 3
    ASGG K 15
    ASGG JIH 3

  Reloading:
    TNT1 A 0 A_JumpIfInventory("ASGClip",0,"ReloadFinish")
    TNT1 A 0 A_JumpIfInventory("Shell",1,1)
    goto ReloadFinish
    TNT1 A 0 A_TakeInventory("Shell",1)
    TNT1 A 0 A_GiveInventory("ASGClip",1)
    loop

  ReloadFinish:
    ASGG G 3 A_PlayWeaponSound("weapons/asgin")
    ASGG FEDCB 3
    ASGG A 8 A_PlaySoundEX("weapons/asgld1","auto",0)
    goto Ready

  ReloadPowered:
    ASGG BCDEF 2
    ASGG G 3 A_PlayWeaponSound("weapons/asgout")
    ASGG HIJ 2
    ASGG K 10
    ASGG JIH 2
    goto Reloading

  Flash:
    TNT1 A 4 A_Light1
    TNT1 A 3 A_Light2
    goto LightDone

  FlashPowered:
    TNT1 A 2 A_Light1
    TNT1 A 2 A_Light2
    goto LightDone
  }
}

ACTOR ASGClip: Ammo
{
    Inventory.Amount 1
    Inventory.MaxAmount 20
    Ammo.BackpackAmount 0
    Ammo.BackpackMaxAmount 20
    +IGNORESKILL
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// REPEATER /////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

ACTOR Repeater : Weapon 20100
{
//$Category Stronghold_Weapons
   SpawnID 236
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "$STR_GOTREPEATER"
   Obituary "$STR_OB_REPEATER"
   Weapon.AmmoType1 "Cell"
   Weapon.AmmoGive 20
   Weapon.AmmoUse 1
   AttackSound "weapons/repfir2"
   Tag "$STR_WEAPONTAG_REPEATER"
   +WEAPON.EXPLOSIVE
   +NOAUTOFIRE
   States
   {
   Spawn:
      WREP A -1
      stop

   Ready:
      REPG A 1 A_WeaponReady
      loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      REPG A 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      REPG A 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      REPG A 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      REPG A 1 A_Raise
      goto Select

   Fire:
      REPG A 4 A_PlaySound("weapons/repidl")
      REPG BCD 4
      REPG ABCD 3
      REPG ABCD 2
   Hold:
      TNT1 A 0 A_GunFlash
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_FireBullets(4,4,2,9,"RepPuff",1,10240)
      REPG EF 2 bright
      TNT1 A 0 A_GunFlash
      TNT1 A 0 A_FireBullets(4,4,2,9,"RepPuff",1,10240)
      REPG GH 2 bright
      goto HoldContinue

   FirePowered:
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(10,6,-1,20,"RepPuffNoDamage",1,10240)
      goto HoldPoweredContinue

   HoldPowered:
      TNT1 A 0 A_GunFlash
      REPG EFG 1 bright A_FireBullets(20,10,2,10,"RepPuffNoDamage",0,10240)
      REPG H 1 bright A_FireBullets(20,10,2,10,"RepPuffNoDamage",1,10240)
      goto HoldPoweredContinue

   HoldContinue:
      TNT1 A 0 A_ReFire
   HoldFinish:
      REPG A 2 A_PlaySound("weapons/repstp")
      REPG BCD 2
      REPG ABCD 3
      REPG ABCD 4
      REPG ABCD 5
      TNT1 A 0 A_JumpIfInventory("Cell",1,2)
      TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteAmmoChecker") == 1, 1)
      goto HoldFinishNoAmmo
      TNT1 A 0 A_ReFire("Fire") // needed because of +NOAUTOFIRE
      goto Ready

   HoldFinishNoAmmo: // if there's no ammo, don't switch weapon to retain original behaviour
      TNT1 A 0 A_ClearReFire
      goto Ready

   HoldPoweredContinue:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,1)
      goto HoldContinue
      TNT1 A 0 A_ReFire("HoldPowered")
      goto HoldFinish

   Flash:
      TNT1 A 2 A_Light2
      TNT1 A 2 A_Light1
      goto LightDone
   }
}

Actor RepPuff
{
   Radius 3
   Height 3
   RENDERSTYLE ADD
   ALPHA 0.75
   DamageType "PlayerAllyRepeater"
   ExplosionDamage 32
   ExplosionRadius 88
   Species "Player"
   +MTHRUSPECIES
   +ALWAYSPUFF
   +PUFFONACTORS
   +NOGRAVITY
   +NOBLOCKMAP
   +PUFFGETSOWNER
   +NOTIMEFREEZE
   AttackSound "weapons/rephit"
   SeeSound "weapons/rephit"
   Decal MummyScorch
   States
   {
   Spawn:
      BLAS A 0 Bright
      BLAS A 0 Bright A_Explode
      BLAS A 3 Bright A_ChangeFlag("NOTIMEFREEZE",0)
      BLAS BCDEFG 3 Bright
      stop
   }
}

actor RepPuffNoDamage : RepPuff { DamageType "MarineAllyRepeater" }

ACTOR Heat : Ammo
{
    Inventory.MaxAmount 100
    Ammo.BackpackAmount 0
    Ammo.BackpackMaxAmount 100
    Inventory.Icon STTPRCNT
    +IGNORESKILL
}

actor RepSteam
{
   Radius 0
   Height 1
   Speed 25
   PROJECTILE
   RENDERSTYLE TRANSLUCENT
   ALPHA 0.35
   SeeSound "weapons/repstm"
   +DONTSPLASH
   +CLIENTSIDEONLY
   States
   {
   Spawn:
      TNT1 A 3
      WSMK ABCDE 2
      Stop
   }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// MINE LAYER ///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR LandMineLayer : Weapon 25084
{
//$Category Stronghold_Weapons
   SpawnId 200
   Inventory.PickupMessage "$STR_GOTLANDMINELAYER"
   Inventory.PickupSound "misc/w_pkup"
   Weapon.AmmoType "Mines"
   Weapon.AmmoGive 5
   Weapon.AmmoUse 1
   Tag "$STR_WEAPONTAG_LANDMINELAYER"
   +ALT_USES_BOTH
   +NOALERT
   States
   {
   Spawn:
      MINE A -1
      stop

   Ready:
      LAYR A 1 A_WeaponReady
      loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      LAYR A 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      LAYR A 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      LAYR A 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      LAYR A 1 A_Raise
      goto Select

   Fire:
      LAYR B 8 A_PlaySound("weapons/minebeep")
      LAYD ABCDE 1
      TNT1 A 5 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 9 A_FireCustomMissile("Landmine3",0,1,0,0)
      LAYU ABCDE 1
      LAYR A 4
      goto Ready

   FirePowered:
      TNT1 A 3
      TNT1 A 0 A_FireCustomMissile("PowerLandmine3B",-7,0,0,0)
      TNT1 A 0 A_FireCustomMissile("PowerLandmine3B",7,0,0,0)
      TNT1 A 6 A_FireCustomMissile("PowerLandmine3A",0,1,0,0)
      LAYU ABCDE 1
      LAYR A 2
      goto Ready

   AltFire:
      LAYR B 8 A_PlaySound("weapons/minebeep")
      LAYD ABCDE 1
      TNT1 A 10 A_JumpIfInventory("PowerWeaponLevel2",1,"AltFirePowered")
      LAYT A 1 A_PlaySound("weapons/minethrow")
      LAYT BCDEFGH 1
      LAYT I 1 A_FireCustomMissile("Landmine2",0,1,0,8)
      LAYT JKLMNOPQR 1
      TNT1 A 10
      goto AltFireFinish

   AltFirePowered:
      TNT1 A 5
      LAYT A 1 A_PlaySound("weapons/minethrow")
      LAYT BCDEFGH 1
      TNT1 A 0 A_FireCustomMissile("PowerLandmine2B",-7,0,0,8)
      TNT1 A 0 A_FireCustomMissile("PowerLandmine2B",7,0,0,8)
      LAYT I 1 A_FireCustomMissile("PowerLandmine2A",0,1,0,8)
      LAYT JKLMNOPQR 1
      TNT1 A 5

   AltFireFinish:
      LAYU ABCDE 1
      LAYR A 6
      goto Ready
   }
}

ACTOR Landmine
{
   Radius 8 //was 5
   Height 5
   Mass 1000000
   Health 20
   Damage 10
   //SeeSound "weapons/tink"
   DamageType "MarineAllyRocket"
   ExplosionDamage 384
   ExplosionRadius 160
   DONTHURTSHOOTER
   Speed 0
   Projectile
   Species "Player"
   +THRUSPECIES
   +NOBLOOD
   -SHOOTABLE
   -NOGRAVITY
   +MOVEWITHSECTOR
   States
   {
   Spawn:
      MINE A 0
      MINE A 0 A_PlaySound("weapons/tink",2)
      MINE AB 5
      goto Spawn+2
   Death:
      TNT1 A 0 A_PlaySound("weapons/mineexpl",2)
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_NoGravity
      MISL B 8 bright A_Explode
      MISL C 6 bright
      MISL D 4 bright
      stop
   }
}

ACTOR Landmine2
{
   Radius 8 //was 5
   Height 5
   Mass 1000000
   Health 20
   Damage 10
   BounceSound "weapons/tink"
   DamageType "MarineAllyRocket"
   DONTHURTSHOOTER
   Speed 15
   Projectile
   BounceType "Doom"
   Species "Player"
   +THRUSPECIES
   +NOBLOOD
   -SHOOTABLE
   -NOGRAVITY
   +MOVEWITHSECTOR
   States
   {
   Spawn:
      MINE AB 5
      loop
   Death:
      MINE A 0 A_StopSound(2)
      MINE A 0 A_SpawnItemEx("Landmine",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
      stop
   }
}

ACTOR Landmine3 : Landmine2 { Speed 2 }

ACTOR Mines : Ammo 25085
{
//$Category Stronghold_Weapons
   SpawnId 201
   Inventory.Amount 2
   Inventory.MaxAmount 20
   Ammo.BackpackAmount 5
   Ammo.BackpackMaxAmount 40
   Inventory.PickupMessage "$STR_GOTMINES"
   Inventory.PickupSound "misc/ammo_pkup"
   Inventory.Icon MINEA0
   Tag "$STR_AMMOTAG_MINES"
   States
   {
   Spawn:
      MINE A -1
      stop
   }
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// STUNNER RIFLE ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
ACTOR StunnerRifle : PlasmaRifle 23099
{
//$Category Stronghold_Weapons
   SpawnID 202
   Weapon.AmmoUse 5
   Inventory.PickupMessage "$STR_GOTSTUNNERRIFLE"
   Tag "$STR_WEAPONTAG_STUNNERRIFLE"
   States
   {
   Spawn:
      STPP A -1
      stop

   Ready:
      STPO ABC 3 bright A_WeaponReady
      loop

   Deselect:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"DeselectPowered")
      TNT1 A 0 A_Lower
      STPO E 1 A_Lower
      loop

   DeselectPowered:
      TNT1 AA 0 A_Lower
      STPO E 1 A_Lower
      goto Deselect

   Select:
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"SelectPowered")
      TNT1 A 0 A_Raise
      STPO E 1 A_Raise
      loop

   SelectPowered:
      TNT1 AA 0 A_Raise
      STPO E 1 A_Raise
      goto Select

   Fire:
      TNT1 A 0 A_GunFlash
      TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
      TNT1 A 0 A_FireCustomMissile("StunnerBall",0,1,0,0)
      STPO FGH 3 bright
      STPO D 4 bright
      goto FireFinish

   FirePowered:
      TNT1 A 0 A_FireCustomMissile("PowerStunnerBall",0,1,0,0)
      STPO FGH 3 bright
      STPO D 16 bright

   FireFinish:
      STPO D 6 bright A_ReFire
      goto Ready

   Flash:
      TNT1 A 1 A_Light1
      TNT1 A 1 A_Light2
      TNT1 A 1 A_Light1
      goto LightDone
   }
}

actor StunnerBall
{
  spawnid 10
  radius 6
  height 8
  speed 20
  scale 0.45
  DamageType "Stunner"
  damage 1
  decal StunnerScorch
  renderstyle Add
  seesound "weapons/stunner/shot"
  deathsound "weapons/stunner/hit"
  translation "192:207=250:254"
  PROJECTILE
  Species "Player"
  +THRUSPECIES
  +RANDOMIZE
  +RIPPER
  +STRIFEDAMAGE
  states
  {
  Spawn:
    STPR A 1 bright A_SpawnItemEx("StunnerBallFX",0,0,0,0,0,0,0,128)
    loop
  Death:
    PLSE ABCDE 4 bright
    stop
  }
}

actor StunnerBallFX
{
  +NOBLOCKMAP
  +NOGRAVITY
  scale 0.45
  renderstyle Add
  radius 1
  height 2
  States
  {
  Spawn:
    STPR A 1 bright A_FadeOut(0.1)
    Loop
  }
}

/////////////////////
// NEW BULLET PUFF //
/////////////////////

ACTOR NewPuff : BulletPuff replaces BulletPuff
{
	DamageType "Bullet"
	Alpha 1
	Mass 1
	Decal BulletChip
	States
	{
	Spawn:
	Melee:
	    PUFF A 0 bright
	    PUFF A 0 bright A_SpawnItemEx("Puff2",0,0,0,0,0,0,0,32)
	    PUFF AB 4 bright
	    goto Super::Melee
	}
}

ACTOR NewPuffPlayer : NewPuff
{
    DamageType "PlayerAlly"
    Species "Player"
    +MTHRUSPECIES
}

ACTOR Puff2
{
	PROJECTILE
	+NOCLIP
    +CLIENTSIDEONLY
	Mass 1
	States
	{
	Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(128,1)
        stop
		TNT1 A 0 A_PlaySound("puff/ric")
		PUFF EFGH 4 bright
		Stop
	}
}

actor StrFakeGoalBase : FakeGoalBase replaces FakeGoalBase
{
    Tag "$STR_MONSTERTAG_GOAL"
    DamageFactor "Massacre", 0
    DamageFactor "MarineAlly", 0
    DamageFactor "MarineAllyRocket", 0
    DamageFactor "MarineAllyPlasma", 0
    DamageFactor "MarineAllyRailgun", 0
    DamageFactor "MarineAllyRepeater", 0
    DamageFactor "MarineAllyFire", 0
    DamageFactor "MarineAllyBFG", 0
    DamageFactor "MarineAllyBFGSplash", 0
    DamageFactor "MarineAllyIce", 0
    DamageFactor "SmartBomb", 0
    DamageFactor "CoreFriendly", 0
    DamageFactor "CoreFriendlyFire", 0
    DamageFactor "BossFactor", 0
    DamageFactor "PlayerAlly", 0
    DamageFactor "PlayerAllyRocket", 0
    DamageFactor "PlayerAllyPlasma", 0
    DamageFactor "PlayerAllyRailgun", 0
    DamageFactor "PlayerAllyRepeater", 0
    DamageFactor "PlayerAllyFire", 0
    DamageFactor "PlayerAllyBFG", 0
    DamageFactor "PlayerAllyBFGSplash", 0
    DamageFactor "PlayerAllyIce", 0
    DamageFactor "Stunner", 0
    DamageFactor "Cyber2Factor", 0.5
    DamageFactor "Cyber2FactorExplosive", 0.5
    DamageFactor "Cyber2FactorFire", 0.5
    DamageFactor "STBoss", 0.02
    DamageFactor "STBossFactor", 0.25
    DamageFactor "SpiritBoss", 0.02
    DamageFactor "SpiritBossFactor", 0.25
    DamageFactor "Animate", 0.1
    DamageFactor "Bullet", 0.8
    DamageFactor "Melee", 0.8
    Species "Player"
    +THRUSPECIES
    +DONTMORPH
    +DONTRIP
    +NORADIUSDMG
    +ISMONSTER
    +NOTAUTOAIMED
    States
    {
      Death:
        TNT1 A 1 A_Scream
        stop
    }
}

actor StrFakeGoalSparks : FakeGoalSparks replaces FakeGoalSparks { +DONTSPLASH }

actor StrGoal_Red : StrFakeGoalBase replaces Goal_Red
{
    Tag "$STR_MONSTERTAG_GOAL_RED"
	Translation "80:111=170:191"
	States
	{
	Death:
		TNT1 A 0 ACS_ExecuteWithResult(926, 1)
		goto Super::Death
	}
}

actor StrGoal_Orange : StrFakeGoalBase replaces Goal_Orange
{
    Tag "$STR_MONSTERTAG_GOAL_ORANGE"
	Translation "80:111=209:223"
	States
	{
	Death:
		TNT1 A 0 ACS_ExecuteWithResult(926, 2)
		goto Super::Death
	}
}

actor StrGoal_Blue : StrFakeGoalBase replaces Goal_Blue
{
    Tag "$STR_MONSTERTAG_GOAL_BLUE"
	Translation "80:111=192:207"
	States
	{
	Death:
		TNT1 A 0 ACS_ExecuteWithResult(926, 3)
		goto Super::Death
	}
}

actor StrGoal_Green : StrFakeGoalBase replaces Goal_Green
{
    Tag "$STR_MONSTERTAG_GOAL_GREEN"
	Translation "80:111=112:127"
	States
	{
	Death:
		TNT1 A 0 ACS_ExecuteWithResult(926, 4)
		goto Super::Death
	}
}

actor StrGoal_White : StrFakeGoalBase replaces Goal_White
{
    Tag "$STR_MONSTERTAG_GOAL_WHITE"
	States
	{
	Death:
		TNT1 A 0 ACS_ExecuteWithResult(926, 5)
		goto Super::Death
	}
}

ACTOR StrPowerCore : PowerCore replaces PowerCore
{
    Tag "$STR_MONSTERTAG_POWERCORE"
    DamageFactor "Massacre", 0
    DamageFactor "MarineAlly", 0
    DamageFactor "MarineAllyRocket", 0
    DamageFactor "MarineAllyPlasma", 0
    DamageFactor "MarineAllyRailgun", 0
    DamageFactor "MarineAllyRepeater", 0
    DamageFactor "MarineAllyFire", 0
    DamageFactor "MarineAllyBFG", 0
    DamageFactor "MarineAllyBFGSplash", 0
    DamageFactor "MarineAllyIce", 0
    DamageFactor "SmartBomb", 0
    DamageFactor "CoreFriendly", 0
    DamageFactor "CoreFriendlyFire", 0
    DamageFactor "BossFactor", 0
    DamageFactor "PlayerAlly", 0
    DamageFactor "PlayerAllyRocket", 0
    DamageFactor "PlayerAllyPlasma", 0
    DamageFactor "PlayerAllyRailgun", 0
    DamageFactor "PlayerAllyRepeater", 0
    DamageFactor "PlayerAllyFire", 0
    DamageFactor "PlayerAllyBFG", 0
    DamageFactor "PlayerAllyBFGSplash", 0
    DamageFactor "PlayerAllyIce", 0
    DamageFactor "Stunner", 0
    DamageFactor "STBoss", 0.015
    DamageFactor "STBossFactor", 0.1
    DamageFactor "SpiritBoss", 0.015
    DamageFactor "SpiritBossFactor", 0.1
    DamageFactor "Animate", 0
    DamageFactor "Cyber2Factor", 0.25
    DamageFactor "Cyber2FactorExplosive", 0.25
    DamageFactor "Cyber2FactorFire", 0.25
    DamageFactor "Normal", 0.5
    DamageFactor "Bullet", 0.4
    DamageFactor "Melee", 0.4
    DamageFactor "OvermindFactor", 0.25
    DamageFactor "OvermindFactorFire", 0.25
    Species "Player"
    +THRUSPECIES
    +DONTRIP
    +NORADIUSDMG
    +DONTMORPH
    +NOTAUTOAIMED
    States
    {
      Spawn:
        SECG ABCD 6
        TNT1 A 0 A_JumpIfHealthLower(340, "Spawn_33Continue") // display according to the percentage
        TNT1 A 0 A_JumpIfHealthLower(670, "Spawn_66Continue")
        loop

      Pain:
        TNT1 A 0 A_JumpIfHealthLower(340, "Spawn_33")
        TNT1 A 0 A_JumpIfHealthLower(670, "Spawn_66")
        TNT1 A 0 ACS_Execute(929, 0, 1)
        goto Spawn

      Spawn_66:
        TNT1 A 0 ACS_Execute(929, 0, 2)
      Spawn_66Continue:
        SECY ABCD 6
        TNT1 A 0 A_JumpIfHealthLower(340, "Spawn_33Continue")
        TNT1 A 0 A_JumpIfHealthLower(670, "Spawn_66Continue")
        goto Spawn

      Spawn_33:
        TNT1 A 0 ACS_Execute(929, 0, 3)
      Spawn_33Continue:
        SECR ABCD 6
        TNT1 A 0 A_JumpIfHealthLower(340, "Spawn_33Continue")
        TNT1 A 0 A_JumpIfHealthLower(670, "Spawn_66Continue")
        goto Spawn

      Death:
        TNT1 A 0 ACS_ExecuteWithResult(929, 4)
        SECR E 6 A_Scream
        SECR FGHJKLMNO 6
        SECR P -1
        stop
    }
}

actor StrOvermindCore : OvermindCore replaces OvermindCore
{
    Health 50000
    Tag "$STR_MONSTERTAG_OVERMINDCORE"
    DamageFactor "Massacre", 0
    DamageFactor "MarineAlly", 0
    DamageFactor "MarineAllyRocket", 0
    DamageFactor "MarineAllyPlasma", 0
    DamageFactor "MarineAllyRailgun", 0
    DamageFactor "MarineAllyRepeater", 0
    DamageFactor "MarineAllyFire", 0
    DamageFactor "MarineAllyBFG", 0
    DamageFactor "MarineAllyBFGSplash", 0
    DamageFactor "MarineAllyIce", 0
    DamageFactor "SmartBomb", 0
    DamageFactor "CoreFriendly", 0
    DamageFactor "CoreFriendlyFire", 0
    DamageFactor "BossFactor", 0
    DamageFactor "PlayerAlly", 0
    DamageFactor "PlayerAllyRocket", 0
    DamageFactor "PlayerAllyPlasma", 0
    DamageFactor "PlayerAllyRailgun", 0
    DamageFactor "PlayerAllyRepeater", 0
    DamageFactor "PlayerAllyFire", 0
    DamageFactor "PlayerAllyBFG", 0
    DamageFactor "PlayerAllyBFGSplash", 0
    DamageFactor "PlayerAllyIce", 0
    DamageFactor "Stunner", 0
    DamageFactor "Normal", 5
    DamageFactor "Bullet", 4
    DamageFactor "Melee", 4
    DamageFactor "Cyber2Factor", 9
    DamageFactor "Cyber2FactorExplosive", 9
    DamageFactor "Cyber2FactorFire", 9
    DamageFactor "Animate", 8
    DamageFactor "STBoss", 6
    DamageFactor "STBossFactor", 7
    DamageFactor "SpiritBoss", 6
    DamageFactor "SpiritBossFactor", 7
    DamageFactor "OvermindFactor", 10
    DamageFactor "OvermindFactorFire", 10
    Species "Player"
    +THRUSPECIES
    +NORADIUSDMG
    +DONTRIP
    +DONTMORPH
    +NOTAUTOAIMED
}

///////////////////
// VOTING PISTOL //
///////////////////
actor StrVotingDevice : Weapon
{
    Tag "$STR_WEAPONTAG_VOTINGDEVICE"
    +UNDROPPABLE
    +NOALERT
    +CHEATNOTWEAPON
    States
    {
      Ready:
        PISG A 1 A_WeaponReady
        loop

      Deselect:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        loop

      Select:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop

      Fire:
        TNT1 A 0 ACS_NamedExecuteWithResult("StrVotingPistol", 1)
        goto FireFinish

      AltFire:
        TNT1 A 0 ACS_NamedExecuteWithResult("StrVotingPistol", 2)

      FireFinish:
        PISG BCB 3
        TNT1 A 0 A_TakeInventory("StrVotingDevice")
        goto Ready

      Spawn:
        PIST A -1
        stop
    }
}

///////////////////////////////////////
// ALARM ACTOR ////////////////////////
///////////////////////////////////////

actor StrAlarmSounder replaces AlarmSounder 21235
{
//$Category Stronghold
	RenderStyle none
	-SOLID
	-SHOOTABLE
	+NOBLOCKMAP
	+FRIENDLY
    +NOTAUTOAIMED

	states
	{
	Spawn:
		TNT1 A 1 A_LookEx(LOF_NOSOUNDCHECK,0,192,0,360,"See")
		Loop
	See:
		TNT1 A 0 A_SpawnItemEx("AlarmSounding",0,0,0,0,0,0,0,32)
		TNT1 A 56 A_SpawnItemEx("AlarmSoundingAnnouncer",0,0,0,0,0,0,0,32) // the 56 is the delay
		goto spawn
	}
}

actor AlarmSounding
{
	-SOLID
	-SHOOTABLE
	+NOBLOCKMAP
	+CLIENTSIDEONLY

	States
	{
	Spawn:
		TNT1 A 0
	ScrewYouImMakingNoise: //Intentional Fallthrough
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrSilentAlarmCVarChecker") == 1, "AlrightImShuttingUp")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrSilentAlarmCVarChecker") >= 3, "AlrightImShuttingUp")
		TNT1 A 0 A_PlaySoundEx("alarm/sounding", "Auto", 0, 1)
	AlrightImShuttingUp: // Another intentional fallthrough.
		TNT1 A 1
		stop
	}
}

actor AlarmSoundingAnnouncer : AlarmSounding
{
	-CLIENTSIDEONLY // Turns out it results in garbage data somehow.

	States
	{
	Spawn:
		TNT1 A 0
	ScrewYouImMakingNoise:
	AlrightImShuttingUp:
		TNT1 A 1 ACS_NamedExecute("StrAnnouncerDeadlineSoundPlayer")
		stop
	}
}
