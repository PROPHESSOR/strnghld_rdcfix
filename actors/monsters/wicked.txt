ACTOR Wicked 30133
{
	//$Category Stronghold_Monsters
	FloatSpeed 1
	painchance "Stunner",128
	Health 275
	Radius 25
	Height 88
	Mass 200
	Speed 2.5
	PainChance 112
	MONSTER
	+NOGRAVITY
	+FLOAT
	Obituary "%o got blasted by a Wicked."
	SeeSound "monster/wiksit"
	PainSound "monster/wikpai"
	DeathSound "monster/wikdth"
	ActiveSound "monster/wikact"
	States
	{
	Spawn:
		WICK ABCD 8 A_Look
		Loop
	See:
		WICK A 1 A_Chase
		WICK AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK A 1 A_Chase
		WICK AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK B 1 A_Chase
		WICK BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK B 1 A_Chase
		WICK BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK C 1 A_Chase
		WICK CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK C 1 A_Chase
		WICK CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK D 1 A_Chase
		WICK DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK D 1 A_Chase
		WICK DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK A 0 A_Jump(64,"See2")
		Loop
	See2:
		WICK E 0 A_Chase
		WICK E 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK E 0 A_Chase
		WICK E 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK F 0 A_Chase
		WICK F 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK F 0 A_Chase
		WICK F 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK G 0 A_Chase
		WICK G 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK G 0 A_Chase
		WICK G 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK H 0 A_Chase
		WICK H 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK H 0 A_Chase
		WICK H 1 A_Chase("","",CHF_NOPLAYACTIVE)
		WICK E 0 A_Jump(32,"See")
		Loop
	Missile:
		WICK I 1 A_PlaySound("monster/wikatk")
		WICK I 4 A_FaceTarget
		WICK J 5 BRIGHT A_FaceTarget
		WICK K 8 BRIGHT A_CustomMissile("WickedShot",60)
		WICK L 5
		Goto See
	Pain:
		WICK M 3
		WICK M 6 A_Pain
		Goto See
	Pain.Stunner:
		WICK M 3 A_ChangeFlag("NoPain",1)
		WICK M 300 A_Pain
		WICK M 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		WICK N 5 A_Scream
		WICK OP 5
		WICK P 0 A_SpawnItemEx("WickedTorso",0,0,48)
		WICT A 5 A_NoBlocking
		WICT G 3000 A_SetFloorClip
		WICT G 1 A_FadeOut(0.05)
		Wait
	Raise:
		WICT G 0 A_KillChildren
		WICK G 0 A_SpawnItemEx("WickedTorsoRes")
		WICT G 5 A_UnSetFloorClip
		WICT FEDBC 5
		WICK PON 5
		Goto See
	}
}

ACTOR WickedShot
{
	Radius 8
	Height 8
	Speed 10
	Damage 5
	RENDERSTYLE ADD
	DamageType fire
	ALPHA 0.67
	PROJECTILE
	+SEEKERMISSILE
	+FORCEXYBILLBOARD
	Seesound "weapons/firmfi"
	DeathSound "weapons/firex5"
	var int user_count;
	States
	{
	Spawn:
		WIBL A 1 Bright LIGHT("WickedballA") A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1))
		WIBL B 1 Bright LIGHT("WickedballB") A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1))
		WIBL A 0 A_SetUserVar("user_count",user_count+1)
		WIBL A 0 A_JumpIf(user_count>=14,1)
		Loop
		WIBL A 0 A_SetUserVar("user_count",0)
		WIBL A 0 A_SeekerMissile(90,360)
		loop
	Death:
		WIBL J 3 Bright LIGHT("WickedballJ")
		WIBL K 3 Bright LIGHT("WickedballK")
		WIBL L 3 Bright LIGHT("WickedballL")
		WIBL M 3 Bright LIGHT("WickedballM")
		WIBL N 3 Bright LIGHT("WickedballN")
		stop
	}
}

ACTOR WickedFX
{
	+CLIENTSIDEONLY
	Radius 2
	Height 2
	+NOBLOCKMAP
	+NOGRAVITY
	+DONTSPLASH
	+FORCEXYBILLBOARD
	RENDERSTYLE ADD
	ALPHA 0.67
	States
	{
	Spawn:
		TNT1 A 3
		WIBL EFGHI 3 BRIGHT
		Stop
	}
}

ACTOR WickedTorso
{
	Mass 1000000
	Radius 1
	Height 1
	Projectile
	-NOGRAVITY
	SeeSound "monster/tenpn1"
	DeathSound "monster/tenpn2"
	States
	{
	Spawn:
		WICK R -1
		Loop
	Death:
		WICK S 5 A_SetFloorClip
		WICK TUVW 5
		WICK W 3000
		WICK W 1 A_FadeOut(0.05)
		Wait
	}
}

ACTOR WickedTorsoRes
{
	Radius 24
	Height 24
	+NOGRAVITY
	+NOBLOCKMAP
	+NOCLIP
	States
	{
	Spawn:
		WICK WVUTSRQ 5
		stop
	}
}