//Hellion
actor Hellion 30132
{
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	obituary "A Hellion burned %o to a crisp."
	health 70
	height 56
	mass 120
	speed 5
	painchance 24
	seesound "monster/hlnsit"
	painsound "monster/hlnpai"
	deathsound "monster/hlndth"
	activesound "monster/hlnact"
	MONSTER
	+FLOORCLIP
	states
	{
	Spawn:
		HELN AB 10 A_Look
		loop
	See:
		HELN A 1 A_Chase
		HELN A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN A 1 A_Chase
		HELN A 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN B 1 A_Chase
		HELN B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN B 1 A_Chase
		HELN B 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN C 1 A_Chase
		HELN C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN C 1 A_Chase
		HELN C 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN D 1 A_Chase
		HELN D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN D 1 A_Chase
		HELN D 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN E 1 A_Chase
		HELN E 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN E 1 A_Chase
		HELN E 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN F 1 A_Chase
		HELN F 1 A_Chase("","",CHF_NOPLAYACTIVE)
		HELN F 1 A_Chase
		HELN F 1 A_Chase("","",CHF_NOPLAYACTIVE)
		loop
	Missile:
		HELN G 0 A_VileStart
		HELN G 6 Bright LIGHT("HellionG") A_FaceTarget
		HELN H 6 Bright LIGHT("HellionH") A_FaceTarget
		HELN I 6 Bright LIGHT("HellionI") A_FaceTarget
		HELN J 6 Bright LIGHT("HellionJ") A_FaceTarget
		HELN K 6 Bright LIGHT("HellionK") A_FaceTarget
		HELN L 6 Bright LIGHT("HellionL") A_CustomMissile("HellionBall")
		HELN L 2 LIGHT("HellionL")
		goto See
	Pain:
		HELN M 2
		HELN M 2 A_Pain
		goto See
	Pain.Stunner:
		HELN M 2 A_ChangeFlag("NoPain",1)
		HELN M 300 A_Pain
		HELN M 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		HELN N 6 ACS_NamedExecuteAlways("AwardPoints",0)
		HELN O 6 A_Scream
		HELN PQR 6
		HELN S 6 A_NoBlocking
		HELN T 3000
		HELN T 1 A_FadeOut(0.05)
		Wait
	XDeath:
		HELN U 5 ACS_NamedExecuteAlways("AwardPoints",0,1)
		HELN V 5 A_XScream
		HELN W 5
		HELN X 5 A_NoBlocking
		HELN YZ 5
		HELN "[\]" 5
		HELN ] 3000
		HELN ] 1 A_FadeOut(0.05)
		Wait
	Raise:
		HELN TSRQPON 6
		goto See
	}
}

ACTOR HellionBall : CacodemonBall
{
	Damage 8
	Speed 15
	Alpha 0.80
	DamageType fire
	+FORCEXYBILLBOARD
	SeeSound "Monster/hlnatk"
	DeathSound "Monster/hlnexp"
	states
	{
	Spawn:
		HLBL A 3 bright LIGHT("HellionballA") A_SpawnItemEx("HellionPuff")
		HLBL B 3 bright LIGHT("HellionballB") A_SpawnItemEx("HellionPuff")
		loop
	Death:
		HLBL J 3 bright LIGHT("HellionballJ")
		HLBL K 3 bright LIGHT("HellionballK")
		HLBL L 3 bright LIGHT("HellionballL")
		HLBL M 3 bright LIGHT("HellionballM")
		HLBL N 3 bright LIGHT("HellionballN")
		stop
	}
}

ACTOR HellionPuff
{
	+CLIENTSIDEONLY
	Radius 3
	Height 3
	RENDERSTYLE ADD
	Alpha 0.67
	+NOGRAVITY
	+NOBLOCKMAP
	+DONTSPLASH
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		TNT1 A 3
		HLBL C 3 BRIGHT LIGHT("HellionballC")
		HLBL D 3 BRIGHT LIGHT("HellionballD")
		HLBL E 3 BRIGHT LIGHT("HellionballE")
		HLBL F 3 BRIGHT LIGHT("HellionballF")
		HLBL G 3 BRIGHT LIGHT("HellionballG")
		HLBL H 3 BRIGHT LIGHT("HellionballH")
		HLBL I 3 BRIGHT LIGHT("HellionballI")
		Stop
	}
}