/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARINE REINFORCEMENTS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor MarineItem : CustomInventory 25003
{
//$Category Stronghold_Items
  inventory.pickupmessage "$STR_GOTMARINE"
  inventory.maxamount 5
  Tag "$STR_POWERUPTAG_MARINE"
  inventory.icon MARIA0
  +INVENTORY.INVBAR
  Scale 0.75
  States
  {
  Spawn:
   	MARP A -1 BRIGHT
    stop
  Use:
        TNT1 A 0 A_SpawnItemEx("TeleportFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
        TNT1 A 0 A_Jump(128, "ShotgunMarine", "ChaingunMarine")
        TNT1 A 0 A_Jump(128, "SSGMarine", "ASGMarine")
		TNT1 A 0 A_Jump(128, "RocketMarine", "HRLMarine", "FlamerMarine")
		TNT1 A 0 A_Jump(256, "PlasmaMarine", "RailMarine", "RepeaterMarine")
  	Fail
  ShotgunMarine:
    TNT1 A 0 A_SpawnItemEx("STMarineShotgun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  ChaingunMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineChaingun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  SSGMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineSSG",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  ASGMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineAutoShotgun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  FlamerMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineFlamer",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  RocketMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineRocket",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  HRLMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineHRL",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  PlasmaMarine:
	TNT1 A 0 A_SpawnItemEx("STMarinePlasma",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  RailMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineRailgun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  RepeaterMarine:
	TNT1 A 0 A_SpawnItemEx("STMarineRepeater",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
	Stop
  }
}

actor STMarineBase
{
    PainChance 128
    MONSTER
    Species "Player"
    +THRUSPECIES
    +NEVERRESPAWN
    -COUNTKILL
    -ACTIVATEMCROSS
    +NODROPOFF
    +FRIENDLY
    +SLIDESONWALLS
    +LOOKALLAROUND
    +QUICKTORETALIATE
    +NOBLOCKMONST
    +FLOORCLIP
    +NOTAUTOAIMED
    Radius 16
    Height 56
    Mass 100
    Health 300
    DamageFactor "MarineAlly", 0
    DamageFactor "MarineAllyRocket", 0
    DamageFactor "MarineAllyPlasma", 0
    DamageFactor "MarineAllyRailgun", 0
    DamageFactor "MarineAllyRepeater", 0
    DamageFactor "MarineAllyFire", 0
    DamageFactor "MarineAllyBFG", 0
    DamageFactor "MarineAllyBFGSplash", 0
    DamageFactor "MarineAllyIce", 0
    DamageFactor "PlayerAlly", 0
    DamageFactor "PlayerAllyRocket", 0
    DamageFactor "PlayerAllyPlasma", 0
    DamageFactor "PlayerAllyRailgun", 0
    DamageFactor "PlayerAllyRepeater", 0
    DamageFactor "PlayerAllyFire", 0
    DamageFactor "PlayerAllyBFG", 0
    DamageFactor "PlayerAllyBFGSplash", 0
    DamageFactor "PlayerAllyIce", 0
    DamageFactor "Stunner", 0
    DamageFactor "SmartBomb", 0
    DamageFactor "CoreFriendly", 0
    DamageFactor "CoreFriendlyFire",0
	SeeSound "marine/sight"
    ActiveSound "marine/active"
    DeathSound "marine/death"
    PainSound "marine/pain"
    Obituary "$STR_OB_MARINE"
    Tag "$STR_MONSTERTAG_MARINE"
}

Actor STMarineShotgun : STMarineBase
{
    Speed 16
	AttackSound "weapons/shotgf"
	States
	{
	Spawn:
		PLA3 A 4 A_Look
		Loop
	See:
		PLA3 AABBCCDD 2 A_Chase
		Loop
	Missile:
		PLA3 E 0 A_JumpIfInventory("ShotgunMarineTimer", 1, "See")
		PLA3 E 3 A_FaceTarget
        TNT1 A 0 A_GiveInventory("ShotgunMarineTimer")
		PLA3 F 7 bright A_CustomBulletAttack(4, 3, 7, 5, "MarineImmunePuff")
		Goto See
	Pain:
		PLA3 G 4
		PLA3 G 4 A_Pain
		TNT1 A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Death:
		PLA3 H 10
		PLA3 I 10 A_Scream
		PLA3 J 10 A_NoBlocking
		PLA3 KLM 10
		PLA3 N 3000
		PLA3 N 2 A_FadeOut(0.1)
		wait
	XDeath:
		PLA3 O 5
		PLA3 P 5 A_XScream
		PLA3 Q 5 A_NoBlocking
		PLA3 RSTUV 5
		PLA3 W 3000
		PLA3 W 2 A_FadeOut(0.1)
		wait
	}
}

actor STMarineShotgun2 : STMarineShotgun replaces MarineShotgun {}

actor ShotgunMarineTimer : Powerup { Powerup.Duration 27 }

Actor STMarineSSG : STMarineBase
{
	Speed 14
	AttackSound "weapons/sshotf"
	States
	{
	Spawn:
		PLA9 A 4 A_Look
		Loop
	See:
		PLA9 AABBCCDD 2 A_Chase
		Loop
	Missile:
		PLA9 E 3 A_FaceTarget
		PLA9 F 7 bright A_CustomBulletAttack(11.2, 7.1, 20, 5, "MarineImmunePuff")
		PLA9 E 14
		PLA9 E 14 A_PlayWeaponSound("weapons/sshoto")
		PLA9 E 13 A_PlayWeaponSound("weapons/sshotl")
		PLA9 E 11 A_PlayWeaponSound("weapons/sshotc")
		Goto See
	Pain:
		PLA9 G 4
		PLA9 G 4 A_Pain
		TNT1 A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Death:
		PLA9 H 10
		PLA9 I 10 A_Scream
		PLA9 J 10 A_NoBlocking
		PLA9 KLM 10
		PLA9 N 3000
		PLA9 N 2 A_FadeOut(0.1)
		wait
	XDeath:
		PLA9 O 5
		PLA9 P 5 A_XScream
		PLA9 Q 5 A_NoBlocking
		PLA9 RSTUV 5
		PLA9 W 3000
		PLA9 W 2 A_FadeOut(0.1)
		wait
	}
}

actor STMarineSSG2 : STMarineSSG replaces MarineSSG {}

Actor STMarineChaingun : STMarineBase
{
	Speed 15
	AttackSound "weapons/chngun"
	States
	{
	Spawn:
		PLA4 A 4 A_Look
		Loop
	See:
		PLA4 AABBCCDD 2 A_Chase
		Loop
	Missile:
		PLA4 E 2 A_FaceTarget
		PLA4 FF 4 bright A_CustomBulletAttack(0, 0, 1, 5, "MarineImmunePuff")
		TNT1 A 0 A_CPosRefire
    MissileHold:
		PLA4 FF 4 bright A_CustomBulletAttack(4, 2.3, 1, 5, "MarineImmunePuff")
		TNT1 A 0 A_CPosRefire
		loop
	Pain:
		PLA4 G 4
		PLA4 G 4 A_Pain
		TNT1 A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Death:
		PLA4 H 10
		PLA4 I 10 A_Scream
		PLA4 J 10 A_NoBlocking
		PLA4 KLM 10
		PLA4 N 3000
		PLA4 N 2 A_FadeOut(0.1)
		wait
	XDeath:
		PLA4 O 5
		PLA4 P 5 A_XScream
		PLA4 Q 5 A_NoBlocking
		PLA4 RSTUV 5
		PLA4 W 3000
		PLA4 W 2 A_FadeOut(0.1)
		wait
	}
}

actor STMarineChaingun2 : STMarineChaingun replaces MarineChaingun {}

Actor STMarineRocket : STMarineBase
{
	Speed 10
	States
	{
	Spawn:
		PLA5 A 4 A_Look
		Loop
	See:
		PLA5 AABBCCDD 2 A_Chase
		Loop
	Missile:
		PLA5 E 8 A_FaceTarget
		PLA5 F 12 bright A_CustomMissile("ZMarineRocket")
		PLA5 E 8 A_SpidRefire
		Goto Missile+1
	Pain:
		PLA5 G 4
		PLA5 G 4 A_Pain
		TNT1 A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Death:
		PLA5 H 10
		PLA5 I 10 A_Scream
		PLA5 J 10 A_NoBlocking
		PLA5 KLM 10
		PLA5 N 3000
		PLA5 N 2 A_FadeOut(0.1)
		wait
	XDeath:
		PLA5 O 5
		PLA5 P 5 A_XScream
		PLA5 Q 5 A_NoBlocking
		PLA5 RSTUV 5
		PLA5 W 3000
		PLA5 W 2 A_FadeOut(0.1)
		wait
	}
}

actor STMarineRocket2 : STMarineRocket replaces MarineRocket {}

actor ZMarineRocket : Rocket
{
  Decal Scorch
  DamageType "MarineAllyRocket"
  Species "Player"
  +THRUSPECIES
  -DEHEXPLOSION
  states
  {
  Death:
    TNT1 A 0 A_SetTranslucent(0.67,1)
    MISL B 8 bright A_Explode(128,128,0)
    MISL C 6 bright
    MISL D 4 bright
    stop
  }
}

Actor STMarinePlasma : STMarineBase
{
	Speed 12
	States
	{
	Spawn:
		PLA6 A 4 A_Look
		Loop
	See:
		PLA6 AABBCCDD 2 A_Chase
		Loop
	Missile:
		PLA6 E 5 A_FaceTarget
		PLA6 F 2 bright A_CustomMissile("MarinePlasmaBall")
		PLA6 E 1 A_SpidRefire
		Goto Missile+1
	Pain:
		PLA6 G 4
		PLA6 G 4 A_Pain
		TNT1 A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Death:
		PLA6 H 10
		PLA6 I 10 A_Scream
		PLA6 J 10 A_NoBlocking
		PLA6 KLM 10
		PLA6 N 3000
		PLA6 N 2 A_FadeOut(0.1)
		wait
	XDeath:
		PLA6 O 5
		PLA6 P 5 A_XScream
		PLA6 Q 5 A_NoBlocking
		PLA6 RSTUV 5
		PLA6 W 3000
		PLA6 W 2 A_FadeOut(0.1)
		wait
	}
}

actor STMarinePlasma2 : STMarinePlasma replaces MarinePlasma {}

Actor MarinePlasmaBall : PlasmaBall
{
	DamageType "MarineAllyPlasma"
    Species "Player"
    +THRUSPECIES
    Decal PlasmaScorch
}

Actor STMarineRailgun : STMarineBase
{
	Speed 15
	AttackSound "weapons/railgf"
	Decal RailScorch
	States
	{
	Spawn:
		PRAI A 4 A_Look
		Loop
	See:
		PRAI AABBCCDD 2 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_Jump(256, "Red", "Yellow", "Blue")
	Red:
		TNT1 A 0 A_PlayWeaponSound("weapons/railch")
		PRAI EE 6 A_FaceTarget
		TNT1 A 0 A_PlayWeaponSound("weapons/railf1_2")
		PRAI F 4 bright A_CustomRailgun(75, 0, "red", "white", 1, 1, 0, "MarineRailPuff")
		PRAI E 23
		Goto See
	Yellow:
		TNT1 A 0 A_PlayWeaponSound("weapons/railch")
		PRAI EEEE 6 A_FaceTarget
		TNT1 A 0 A_PlayWeaponSound("weapons/railf2_2")
		PRAI G 4 bright A_CustomRailgun(150, 0, "yellow", "white", 1, 1, 0, "MarineRailPuff")
		PRAI E 23
		Goto See
	Blue:
		TNT1 A 0 A_PlayWeaponSound("weapons/railch")
		PRAI EEEEEE 6 A_FaceTarget
		TNT1 A 0 A_PlayWeaponSound("weapons/railf3_2")
		PRAI H 4 bright A_CustomRailgun(225, 0, "blue", "white", 1, 1, 0, "MarineRailPuff")
		PRAI E 23
		Goto See
	Pain:
		PRAI I 4
		PRAI I 4 A_Pain
		TNT1 A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Death:
		PRAI J 10
		PRAI K 10 A_Scream
		PRAI L 10 A_NoBlocking
		PRAI MNO 10
		PRAI P 3000
		PRAI P 2 A_FadeOut(0.1)
		wait
	XDeath:
		PRAI Q 5
		PRAI R 5 A_XScream
		PRAI S 5 A_NoBlocking
		PRAI TUVWX 5
		PRAI Y 3000
		PRAI Y 2 A_FadeOut(0.1)
		wait
	}
}

ACTOR MarineImmunePuff : BulletPuff
{
	Alpha 1
	Mass 1
    Decal BulletChip
	Species "Player"
	+MTHRUSPECIES
	DamageType "MarineAlly"
	States
	{
	Spawn:
	Melee:
	    PUFF A 0 bright
	    PUFF A 0 bright A_SpawnItemEx("MPuff2",0,0,0,0,0,0,0,32)
	    PUFF AB 4 bright
	    goto Super::Melee
	}
}

ACTOR MPuff2
{
	PROJECTILE
	+NOCLIP
    +CLIENTSIDEONLY
	Mass 1
	States
	{
	Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(128,1)
        stop
		TNT1 A 0 A_PlaySound("puff/ric")
		PUFF EFGH 4 bright
		Stop
	}
}

actor MarineRailPuff : BulletPuff
{
    Mass 1
    Alpha 1
    DamageType "MarineAllyRailgun"
    Species "Player"
    +MTHRUSPECIES
	States
	{
	Spawn:
        PUFF AB 4 bright
        goto Super::Melee
	}
}

actor STMarineBerserk : STMarineBase replaces MarineBerserk // dummy for MAP99
{
    Speed 16
	States
	{
	Spawn:
		PLAY A 4 A_Look
		Loop
	See:
		PLAY AABBCCDD 2 A_Chase
		Loop
	Melee:
		PLAY E 4 A_FaceTarget
        PLAY E 13 A_CustomMeleeAttack((random(1,10)*2)*10,"*fist","","MarineAlly",1)
        //PLAY E 5
		Goto See
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		TNT1 A 0 A_Chase("", "", CHF_FASTCHASE)
		Goto See
	Death:
		PLAY H 10
		PLAY I 10 A_Scream
		PLAY J 10 A_NoBlocking
		PLAY KLM 10
		PLAY N 3000
		PLAY N 2 A_FadeOut(0.1)
		wait
	XDeath:
		PLAY O 5
		PLAY P 5 A_XScream
		PLAY Q 5 A_NoBlocking
		PLAY RSTUV 5
		PLAY W 3000
		PLAY W 2 A_FadeOut(0.1)
		wait
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SENTRY ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor Sentry 12856
{
//$Category Stronghold_Marines
    Mass 0x7FFFFFFF
    DamageFactor "BossFactor", 0
    DamageFactor "MarineAlly", 0
    DamageFactor "MarineAllyRocket", 0
    DamageFactor "MarineAllyPlasma", 0
    DamageFactor "MarineAllyRailgun", 0
    DamageFactor "MarineAllyRepeater", 0
    DamageFactor "MarineAllyFire", 0
    DamageFactor "MarineAllyBFG", 0
    DamageFactor "MarineAllyBFGSplash", 0
    DamageFactor "MarineAllyIce", 0
    DamageFactor "PlayerAlly", 0
    DamageFactor "PlayerAllyRocket", 0
    DamageFactor "PlayerAllyPlasma", 0
    DamageFactor "PlayerAllyRailgun", 0
    DamageFactor "PlayerAllyRepeater", 0
    DamageFactor "PlayerAllyFire", 0
    DamageFactor "PlayerAllyBFG", 0
    DamageFactor "PlayerAllyBFGSplash", 0
    DamageFactor "PlayerAllyIce", 0
    DamageFactor "Stunner", 0
    DamageFactor "SmartBomb", 0
    DamageFactor "CoreFriendly", 0
    DamageFactor "CoreFriendlyFire",0
    Radius 16
    Height 16
    Health 300
    MaxStepHeight 512
    MaxDropOffHeight 512
    RadiusDamageFactor 0.25
	-solid
	+shootable
	+noblood
	+friendly
	+lookallaround
    +ALWAYSFAST
	+missilemore
	+missileevenmore
	+thruspecies
    +ISMONSTER
    -COUNTKILL
    +NOTAUTOAIMED
	Species "Player"
    MinMissileChance 0
    Speed 0
    Obituary "$STR_OB_SENTRY"
    Tag "$STR_MONSTERTAG_SENTRY"
	states
 	{
 	spawn:
 		DGUN A 4 A_Look
 		loop

 	see:
 		DGUN A 4 A_Chase
 		loop
 	missile:
 		DGUN A 4 A_FaceTarget
 		DGUN B 4 bright A_CustomMissile("SentryPlasma", 16, -10, 0)
 		DGUN C 4 bright A_CustomMissile("SentryPlasma", 16, 10, 0)
 		DGUN A 7 A_FaceTarget
 		goto See
 	death:
 		TNT1 A 4
 		stop
 	}
}

actor SentryPlasma
{
	radius 2
	height 2
	speed 20
        damage (12)
        scale 0.3
	damagetype "bossfactor"
    +NOEXTREMEDEATH
	+DONTBLAST
    +PAINLESS
    +THRUSPECIES
    Species "Player"
	projectile
        translation "192:207=208:223","240:246=223:223"
        SeeSound "weapons/repfir"
        DeathSound "weapons/railf1"
	states
 	{
 	spawn:
   		PLSS AB 4 Bright
   		loop
 	Death:
   		PLSE ABCDE 4 Bright
  		Stop
 	}
}

actor SentryItem : CustomInventory 25004
{
//$Category Stronghold_Items
  inventory.pickupmessage "$STR_GOTSENTRY"
  inventory.maxamount 5
  inventory.icon SENIA0
  Tag "$STR_POWERUPTAG_SENTRY"
  +INVENTORY.INVBAR
  scale 0.75
   states
  {
  Spawn:
   		SENP A -1 BRIGHT
    	stop
  Use:
		TNT1 A 0 A_SpawnItemEx("ItemFogSentry",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("Sentry",64,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
  		stop
  }
}

actor ItemFogSentry : ItemFog
{
    +NOTIMEFREEZE
    States
    {
      Spawn:
        IFOG A 0 bright
        IFOG A 0 bright A_PlaySound("DSSTPLAC",2)
        IFOG A 0 bright A_ChangeFlag("NOTIMEFREEZE",0)
        goto Super::Spawn
    }
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// BARREL SPAWNER ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor BarrelItem : CustomInventory 25005
{
//$Category Stronghold_Items
  inventory.pickupmessage "$STR_GOTBARREL"
  inventory.maxamount 50
  inventory.icon BARIA0
  Tag "$STR_POWERUPTAG_BARREL"
  +INVENTORY.INVBAR
  scale 0.75
  states
  {
  Spawn:
   	BARP A -1 BRIGHT
    	stop
  Use:
        TNT1 A 0 A_SpawnItemEx("ItemFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("ExplosiveBarrelTimer",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
  	stop
  }
}

actor ExplosiveBarrelTimer
{
  Obituary "$STR_OB_BARREL"
  Tag "$STR_MONSTERTAG_BARREL"
  health 20
  radius 10
  height 42
  deathsound "world/barrelx"
  speed 0
  MONSTER
  +SOLID
  +SHOOTABLE
  +NOBLOOD
  +DONTGIB
  +NOICEDEATH
  +FRIENDLY
  +NORADIUSDMG
  +INVULNERABLE
  +NODAMAGE
  +NOTAUTOAIMED
  //+DONTRIP
  states
  {
  Spawn:
    BAR1 A 35
    TNT1 A 0 A_ChangeFlag("INVULNERABLE",0)
    TNT1 A 0 A_ChangeFlag("NODAMAGE",0)
    TNT1 A 0 A_JumpIfInventory("ExplosiveBarrelTimerLoop",0,"SpawnExplode")
    BAR1 AB 6 A_Look
    TNT1 A 0 A_GiveInventory("ExplosiveBarrelTimerLoop")
    goto Spawn+3
  SpawnExplode:
    TNT1 A 0 A_GiveInventory("ExplosiveBarrelTimerFizzle")
    TNT1 A 0 A_Die
    goto Spawn+4
  See:
    TNT1 A 0 A_ChangeFlag("INVULNERABLE",0)
    TNT1 A 0 A_ChangeFlag("NODAMAGE",0)
    TNT1 A 0 A_JumpIfInventory("ExplosiveBarrelTimerLoop",0,"SeeExplode")
    BAR1 ABABAB 2 A_Chase
    TNT1 A 0 A_GiveInventory("ExplosiveBarrelTimerLoop")
    goto See+2
  SeeExplode:
    TNT1 A 0 A_GiveInventory("ExplosiveBarrelTimerFizzle")
    TNT1 A 0 A_Die
    goto See+3
  Melee: // ignores timer
    TNT1 A 0 A_ChangeFlag("INVULNERABLE",0)
    TNT1 A 0 A_ChangeFlag("NODAMAGE",0)
    BAR1 ABABABABABABABABABABABABABABABABAB 6
    TNT1 A 0 A_Die
    goto Melee+2
  Death:
    BEXP A 5 bright
    BEXP B 5 bright A_Scream
    BEXP C 5 bright
    TNT1 A 0 A_JumpIfInventory("ExplosiveBarrelTimerFizzle",1,"Fizzle")
    TNT1 A 0 A_SetDamageType("PlayerAllyRocket")
    TNT1 A 0 A_Explode
    goto Finish
  Fizzle:
    TNT1 A 0 A_SetDamageType("MarineAllyRocket") // prevent potential player deaths in case owner disconnected
    TNT1 A 0 A_Explode(64,128)
    goto Finish
  Finish:
    BEXP D 5 bright
    BEXP E 10 bright
    stop
  }
}

actor ExplosiveBarrelTimerLoop : Inventory { Inventory.MaxAmount 350 +UNDROPPABLE -INVBAR }
actor ExplosiveBarrelTimerFizzle : Inventory { Inventory.MaxAmount 1 +UNDROPPABLE -INVBAR }

actor StrExplosiveBarrel: ExplosiveBarrel replaces ExplosiveBarrel
{
    Tag "$FN_BARREL"
    //+DONTRIP
    States
    {
      Death:
        TNT1 A 0 A_SetDamageType("PlayerAllyRocket")
        goto Super::Death
    }
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// COINS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor CoinItem : Inventory 25006
{
//$Category Stronghold_Items
	ConversationID 300
	gravity 0.6
	inventory.pickupmessage "$STR_GOTCOIN"
	inventory.maxamount 100000
	inventory.icon SACKA0
	Tag "$STR_ITEMTAG_COIN"
	+INVENTORY.KEEPDEPLETED
    +UNDROPPABLE
	states
	{
	Spawn:
		COIN A -1
		stop
	}
}

// Item that monsters give player when they die. An ACS script gives the player the equal amount in CoinItem.
actor AwardItem : Inventory { Inventory.MaxAmount 0x7FFFFFFF +UNDROPPABLE -INVBAR }

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// LIFES ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor LifeItemSpawner : CustomInventory 25926
{
//$Category Stronghold_Items
  scale 0.75
  inventory.pickupmessage "$STR_GOTLIFE"
  +NOTIMEFREEZE
  states
  {
  Spawn:
    HERP A 0 bright
   	HERP A 0 bright A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteLivesChecker") == 1, "SpawnStop")
    HERP A -1 bright A_ChangeFlag("NOTIMEFREEZE",0)
    stop

  SpawnStop:
    HERP A 0 bright
    stop

  Pickup:
   	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteLivesChecker") == 1, "PickupFail")
    TNT1 A 0 A_JumpIfInventory("LifeItem",0,"PickupFail")
    TNT1 A 0 A_GiveInventory("LifeItem")
    stop

  PickupFail:
    TNT1 A 0
    fail
  }
}

actor LifeItem : CustomInventory
{
//$Category Stronghold_Items
  scale 0.75
  inventory.pickupmessage "$STR_GOTLIFE"
  inventory.maxamount 9
  inventory.icon HERIA0
  Tag "$STR_POWERUPTAG_LIFE"
  +INVENTORY.INVBAR
  +INVENTORY.KEEPDEPLETED
  +INVENTORY.UNDROPPABLE
  +NOTIMEFREEZE
  states
  {
  Spawn:
    HERP A 0 bright
   	HERP A 0 bright A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteLivesChecker") == 1, "SpawnStop")
    HERP A -1 bright A_ChangeFlag("NOTIMEFREEZE",0)
    stop

  SpawnStop:
    HERP A 0 bright
    stop

  Use:
   	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrInfiniteLivesChecker") == 1, "UseFail")
    TNT1 A 0 ACS_NamedExecuteWithResult("StrJailOpenHandler", 0, 0, 0) // Allows the script to fail
  	stop

  UseFail:
    TNT1 A 0
    fail
  }
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// JAILTRACKER //////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Player is given this when they're in jail
actor JailTracker : Inventory { Inventory.MaxAmount 1 +UNDROPPABLE -INVBAR +INTERHUBSTRIP }

// Player is given one of these per each player in jail
actor JailTrackerAllPlayers : Inventory { Inventory.MaxAmount 10000 +UNDROPPABLE -INVBAR +INTERHUBSTRIP }

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MONSTERTRACKER ///////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
// Player is given one of these for each of the number of monsters left to kill this wave
actor MonsterTracker : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
	inventory.maxamount 10000
}
*/
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// WAVETRACKER //////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
// Player is given one of these for each of the number of monsters left to kill this wave
actor WaveTracker : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
	inventory.maxamount 10000
}

actor HUDCredits : Inventory // Displays top bar when in Intermission
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
	Inventory.MaxAmount 1
}*/

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// THRUSTDUMMY //////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Used for scripts that thrust player to detect when he's not where he should be
actor ThrustDummy : Inventory { Inventory.MaxAmount 1 +UNDROPPABLE -INVBAR +INTERHUBSTRIP }

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SUPER SHIELD ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor SuperShield : BasicArmorPickup 10716
{
//$Category Stronghold_Items
  armor.savepercent 100
  armor.saveamount 300
  inventory.icon "ARMXA0"
  inventory.pickupmessage "$STR_GOTSUPERSHIELD"
  states
  {
  Spawn:
    ARMX AB 3 Bright
    loop
  }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// YELLOW ARMOR ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor StrYellowArmor : GreenArmor 10717
{
    Armor.SavePercent 40
    Armor.SaveAmount 150
    Inventory.Icon "ARM4A0"
    Inventory.PickupMessage "$STR_GOTYELLOWARMOR"
    States
    {
      Spawn:
        ARM4 A 6
        ARM4 B 7 bright
        loop
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// RED ARMOR ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor StrRedArmor : BlueArmor 10718
{
    Armor.SavePercent 66.66666
    Armor.SaveAmount 250 // 200
    Inventory.Icon "ARM3A0"
    Inventory.PickupMessage "$STR_GOTREDARMOR"
    DamageFactor "Fire", 0.125
    DamageFactor "Rocket", 0.125
    DamageFactor "Grenade", 0.125
    DamageFactor "PlayerAllyFire", 0.125
    DamageFactor "PlayerAllyRocket", 0.125
    DamageFactor "BruiserAttack", 0.125
    DamageFactor "Inferno", 0.125
    DamageFactor "OvermindFactor", 0.125
    DamageFactor "OvermindFactorFire", 0.125
    DamageFactor "STBoss", 0.125
    DamageFactor "STBossFactor", 0.125
    DamageFactor "Animate", 0.125
    DamageFactor "Cyber2FactorExplosive", 0.125
    DamageFactor "Cyber2FactorFire", 0.125
    DamageFactor "CoreFriendlyFire", 0.125
    States
    {
      Spawn:
        ARM3 A 6
        ARM3 B 6 bright
        loop
    }
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// PORTABLE MEDICINE ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor PSuperMedKit : HealthPickup 12791
{
//$Category Stronghold_Items
  health 100
  inventory.maxamount 10
  inventory.icon "PMH2A0"
  inventory.pickupmessage "$STR_GOTSUPERMEDKIT"
  Tag "$STR_POWERUPTAG_SUPERMEDKIT"
  +COUNTITEM
  states
  {
  Spawn:
    PM02 A -1
    stop
  }
}

actor PMedKit : HealthPickup 12792
{
//$Category Stronghold_Items
  health 25
  inventory.maxamount 10
  inventory.icon "PMH1A0"
  inventory.pickupmessage "$STR_GOTMEDKIT"
  Tag "$STR_POWERUPTAG_MEDKIT"
  +COUNTITEM
  states
  {
  Spawn:
    PM01 ABCD 2 
    loop
  }
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// POWER UPS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// TIME FREEZER //////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
actor TimeFreezerItem : PowerupGiver 12793
{
//$Category Stronghold_Items
 scale 0.75
 inventory.pickupmessage "$STR_GOTTIMEFREEZER"
 inventory.pickupannouncerentry "timefreeze"
 inventory.icon "FRZIA0"
 inventory.maxamount 1
 inventory.usesound "pickups/slowmo"
 powerup.color "00 33 FF" 0.1
 powerup.type "TimeFreezer"
 powerup.duration -30
 Tag "$STR_POWERUPTAG_TIMEFREEZER"
 +INVENTORY.FANCYPICKUPSOUND
 +INVENTORY.INVBAR
 states
 {
 Spawn:
   FRZP A -1 BRIGHT
   stop
 }
}

actor TimeFreezerPickup : CustomInventory 24027
{
    +INVENTORY.BIGPOWERUP
    +INVENTORY.PICKUPFLASH
    +COUNTITEM
    Inventory.PickupMessage "$STR_GOTTIMEFREEZERPOWERUP"
    Inventory.PickupSound "misc/p_pkup"
    Inventory.PickupAnnouncerEntry "timefreeze"
    states
    {
      Spawn:
        TIME ABCD 4 BRIGHT
        Loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerTimeFreezer",1,"PickupFail")
        TNT1 A 1 A_GiveInventory("TimeFreezerPickup2")
        stop

      PickupFail:
        TIME ABCD 4 BRIGHT
        fail
    }
}

actor TimeFreezerPickup2 : PowerupGiver
{
//$Category Stronghold_Items
 SpawnID 229
 scale 0.75
 inventory.pickupmessage "$STR_GOTTIMEFREEZERPOWERUP"
 inventory.pickupannouncerentry "timefreeze"
 //inventory.icon "TIMEA0"
 inventory.maxamount 10
 inventory.usesound "pickups/slowmo"
 powerup.color "00 33 FF" 0.1
 powerup.type "TimeFreezer"
 powerup.duration -30
 +INVENTORY.FANCYPICKUPSOUND
 +INVENTORY.AUTOACTIVATE
 states
 {
 Spawn:
   TIME ABCD 4 BRIGHT
   loop
 }
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
// QUAD DAMAGE ////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerStrQuadDamage : PowerDamage
{
  damagefactor "Normal", 4
  //inventory.icon "DOOMA0"
}

actor QuadDamageItem : PowerupGiver 12794
{
//$Category Stronghold_Items
 scale 0.75
 inventory.pickupmessage "$STR_GOTQUADDAMAGE"
 inventory.pickupannouncerentry "doomsphere"
 powerup.color RedMap
 inventory.maxamount 3
 inventory.usesound "pickups/slowmo"
 inventory.icon "DAMIA0"
 powerup.type StrQuadDamage
 powerup.duration -25
 Tag "$STR_POWERUPTAG_QUADDAMAGE"
 +INVENTORY.FANCYPICKUPSOUND
   states
 {
 Spawn:
   DAMP A -1 BRIGHT
   stop
 }
}

actor QuadDamagePowerup : CustomInventory 24028
{
    +INVENTORY.PICKUPFLASH
    +INVENTORY.BIGPOWERUP
    +COUNTITEM
    Inventory.PickupMessage "$STR_GOTQUADDAMAGEPOWERUP"
    Inventory.PickupSound "misc/p_pkup"
    Inventory.PickupAnnouncerEntry "doomsphere"
    states
    {
      Spawn:
        DOOM ABCD 4 BRIGHT
        Loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrQuadDamage",1,"PickupFail")
        TNT1 A 1 A_GiveInventory("QuadDamagePowerup2")
        stop

      PickupFail:
        DOOM ABCD 4 BRIGHT
        fail
    }
}

actor QuadDamagePowerup2 : PowerupGiver
{
//$Category Stronghold_Items
 SpawnID 228
 scale 0.75
 inventory.pickupmessage "$STR_GOTQUADDAMAGEPOWERUP"
 inventory.pickupannouncerentry "doomsphere"
 powerup.color RedMap
 inventory.usesound "pickups/slowmo"
 //inventory.icon "DOOMA0"
 powerup.type StrQuadDamage
 powerup.duration -25
 +INVENTORY.FANCYPICKUPSOUND
 +INVENTORY.AUTOACTIVATE
   states
 {
 Spawn:
   DOOM ABCD 4 BRIGHT
   loop
 }
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// INVULNERABILITY ///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor InvulnerabilityItem : PowerupGiver 12795
{
//$Category Stronghold_Items
 scale 0.75
 inventory.pickupmessage "$STR_GOTINVULNERABILITY"
 inventory.pickupannouncerentry "invulnerability"
 inventory.icon "INVIA0"
 inventory.maxamount 2
 inventory.usesound "pickups/slowmo"
 powerup.color InverseMap
 powerup.type "Invulnerable"
 powerup.duration -30
 Tag "$STR_POWERUPTAG_INVULNERABILITY"
 +INVENTORY.FANCYPICKUPSOUND
 states
 {
 Spawn:
   INVP A -1 BRIGHT
   stop
 }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MAX ARMOR+ //////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// The following is a hack separating the maximum armour bonus into two actors, with one that stays in the
// player's inventory, so that the game can count how many the player has. This is mainly needed so this
// item's amount can be limited in the shop.

actor MaxArmorBonus2 : CustomInventory 12796
{
    //$Category Stronghold_Items
    Radius 20
    Height 16
    Inventory.PickupMessage "$STR_GOTMAXARMORBONUS"
    +COUNTITEM
    +UNDROPPABLE
    -ALWAYSPICKUP
    States
    {
      Spawn:
        BON4 ABCDCB 6
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("MaxArmorBonus2ShopToken",0,"PickupHeal")
        TNT1 A 0 A_GiveInventory("MaxArmorBonus2Actual")
        TNT1 A 0 A_GiveInventory("MaxArmorBonus2ShopToken",5)
        stop

      PickupHeal:
        TNT1 A 0 A_GiveInventory("MaxArmorBonus2Actual")
        stop

      PickupFail:
        TNT1 A 0
        fail
    }
}

actor MaxArmorBonus2Super : MaxArmorBonus2 // meant to be used in the shop
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("MaxArmorBonus2ShopToken",0,"PickupFinish")
        TNT1 A 0 A_JumpIfInventory("CoinItem",25,1)
        stop
        TNT1 A 0 A_TakeInventory("CoinItem",25)
        TNT1 A 0 A_GiveInventory("MaxArmorBonus2Actual")
        TNT1 A 0 A_GiveInventory("MaxArmorBonus2ShopToken",5)
        loop

      PickupFinish:
        TNT1 A 0
        stop
    }
}

actor MaxArmorBonus2Actual : BasicArmorBonus
{
    Armor.SavePercent 33.335
    Armor.SaveAmount 5
    Armor.MaxSaveAmount 200
    Armor.MaxBonus 5
    Armor.MaxBonusMax 100
    Inventory.Icon BON4A0
    -ALWAYSPICKUP
}

actor MaxArmorBonus2ShopToken : Inventory { Inventory.MaxAmount 100 +UNDROPPABLE -INVBAR }

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MAX HEALTH+ /////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor MaxHealthBonus2 : CustomInventory 12797
{
    //$Category Stronghold_Items
    Radius 20
    Height 16
    Inventory.PickupMessage "$STR_GOTMAXHEALTHBONUS"
    +COUNTITEM
    -ALWAYSPICKUP
    States
    {
      Spawn:
        BON3 ABCDCB 6
        loop

      Pickup:
        TNT1 A 0 A_JumpIf(CheckClass("MorphedPlayer"), "PickupFail") // So you can't screw up the counters while morphed.
    	TNT1 A 0 A_JumpIf(CheckClass("MorphedPlayer2"), "PickupFail")
    	TNT1 A 0 A_JumpIf(CheckClass("ShrunkenPlayer"), "PickupFail")
    	TNT1 A 0 A_JumpIfInventory("MaxHealthBonus2ShopToken",0,"PickupHeal") // So you can still heal up.
        TNT1 A 0 A_GiveInventory("MaxHealthBonus2Actual",5)
        TNT1 A 0 A_GiveInventory("MaxHealthBonus2ShopToken",5)
        stop

      PickupHeal:
        TNT1 A 0 A_JumpIfHealthLower(400,1)
        fail
        TNT1 A 0 HealThing(400,400)
        stop

      PickupFail:
        TNT1 A 0
        fail
    }
}

actor MaxHealthBonus2Super : MaxHealthBonus2 // meant to be used in the shop
{
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIf(CheckClass("MorphedPlayer"), "PickupFail")
    	TNT1 A 0 A_JumpIf(CheckClass("MorphedPlayer2"), "PickupFail")
    	TNT1 A 0 A_JumpIf(CheckClass("ShrunkenPlayer"), "PickupFail")
        TNT1 A 0 A_JumpIfInventory("MaxHealthBonus2ShopToken",0,"PickupFinish")
        TNT1 A 0 A_JumpIfInventory("CoinItem",25,1)
        stop
        TNT1 A 0 A_TakeInventory("CoinItem",25)
        TNT1 A 0 A_GiveInventory("MaxHealthBonus2Actual",5)
        TNT1 A 0 A_GiveInventory("MaxHealthBonus2ShopToken",5)
        loop

      PickupFinish:
        TNT1 A 0
        stop
    }
}

actor MaxHealthBonus2Actual : UpgradeStamina { Inventory.MaxAmount 300 }

actor MaxHealthBonus2ShopToken : Inventory { Inventory.MaxAmount 300 +UNDROPPABLE -INVBAR }

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// REGENERATION //////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor RegenerationItem : PowerupGiver 24031
{
//$Category Stronghold_Items
 scale 0.75
 inventory.pickupmessage "$STR_GOTREGENERATION"
 inventory.pickupannouncerentry "regeneration"
 inventory.icon "RESIA0"
 inventory.maxamount 3
 inventory.usesound "pickups/slowmo"
 powerup.color "FF 00 AE" 0.1
 powerup.type "Regeneration"
 powerup.duration -90
 Tag "$STR_POWERUPTAG_REGENERATION"
 +INVENTORY.FANCYPICKUPSOUND
 states
 {
 Spawn:
   RESP A -1 BRIGHT
   stop
 }
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// BARRIER ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor WallItem : CustomInventory 12798
{
//$Category Stronghold_Items
  inventory.pickupmessage "$STR_GOTWALL"
  inventory.pickupsound "misc/i_pkup"
  inventory.maxamount 3
  inventory.icon WALIA0
  Tag "$STR_POWERUPTAG_WALL"
  +INVENTORY.INVBAR
  scale 0.75
   states
  {
  Spawn:
   		WALP A -1 BRIGHT
    	stop
  use:
  		TNT1 A 0 A_SpawnItemEx("WallSpawner",64,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
  }
}

actor WallSpawner
{
  +ISMONSTER
  +NOBLOCKMAP
  +NOBLOOD
  +FRIENDLY
  +NOTAUTOAIMED
  Radius 4
  Height 4
  Tag "$STR_MONSTERTAG_WALL"
  States
  {
  Spawn:
  		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        70,  32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        60,  32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        50,  32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  		TNT1 A 0 A_SpawnItemEx("WallPlaced",        40,  32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        30,  32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  		TNT1 A 0 A_SpawnItemEx("WallPlaced",        20,  32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  		TNT1 A 0 A_SpawnItemEx("WallPlaced",        10,  32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  		TNT1 A 0 A_SpawnItemEx("WallPlacedCenter",  0,   32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  		TNT1 A 0 A_SpawnItemEx("WallPlaced",        -10, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        -20, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  		TNT1 A 0 A_SpawnItemEx("WallPlaced",        -30, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  		TNT1 A 0 A_SpawnItemEx("WallPlaced",        -40, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        -50, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        -60, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
		TNT1 A 0 A_SpawnItemEx("WallPlaced",        -70, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION, 0)
  SpawnLoop:
		TNT1 A 1
  		loop
  }
}

actor WallBolt
{
  PROJECTILE
  +NOCLIP
  Speed 1
  Damage (0)
  RenderStyle Add
  Alpha 1
  Scale 0.33
  States
  {
  Spawn:
    WLXP A 1 A_FadeOut(0.025)
	Loop
  }
}

actor WallBolt2 : WallBolt
{
  States
  {
  Spawn:
    WLXP B 1 A_FadeOut(0.025)
	Loop
  }
}

actor WallPlaced
{
    Health 300
    Mass 0x7FFFFFFF
    Radius 8
    Height 128
    Speed 0
    Scale 0.5
	Damage (0)
	+FRIENDLY
	+DONTBLAST
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+NORADIUSDMG
    +DONTMORPH
	+ISMONSTER
    -COUNTKILL
    +NOTAUTOAIMED
	+THRUSPECIES
	Species "Player"
    DamageFactor "MarineAlly", 0
    DamageFactor "MarineAllyRocket", 0
    DamageFactor "MarineAllyPlasma", 0
    DamageFactor "MarineAllyRailgun", 0
    DamageFactor "MarineAllyRepeater", 0
    DamageFactor "MarineAllyFire", 0
    DamageFactor "MarineAllyBFG", 0
    DamageFactor "MarineAllyBFGSplash", 0
    DamageFactor "MarineAllyIce", 0
    DamageFactor "PlayerAlly", 0
    DamageFactor "PlayerAllyRocket", 0
    DamageFactor "PlayerAllyPlasma", 0
    DamageFactor "PlayerAllyRailgun", 0
    DamageFactor "PlayerAllyRepeater", 0
    DamageFactor "PlayerAllyFire", 0
    DamageFactor "PlayerAllyBFG", 0
    DamageFactor "PlayerAllyBFGSplash", 0
    DamageFactor "PlayerAllyIce", 0
    DamageFactor "Stunner", 0
    DamageFactor "SmartBomb", 0
    DamageFactor "CoreFriendly", 0
    DamageFactor "CoreFriendlyFire",0
    Tag "$STR_MONSTERTAG_WALL"
	states
 	{
 	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("WallTimer",0,"Death")
		TNT1 A 1 A_SpawnItemEx("RedSparkleTiny",frandom(-10.0,10.0),0,0,0,0,frandom(0.05,2.00),0,128,192)
		TNT1 A 1 A_SpawnItemEx("RedSparkleSmall",frandom(-10.0,10.0),0,0,0,0,frandom(0.05,2.00),0,128,192)
		TNT1 A 1 A_SpawnItemEx("RedSparkleMedium",frandom(-10.0,10.0),0,0,0,0,frandom(0.05,2.00),0,128,192)
		TNT1 A 1 A_SpawnItemEx("RedSparkleLarge",frandom(-10.0,10.0),0,0,0,0,frandom(0.05,2.00),0,128,192)
		TNT1 A 1 A_SpawnItemEx("RedSparkle",frandom(-10.0,10.0),0,0,0,0,frandom(0.05,2.00),0,128,192)
		TNT1 A 0 A_GiveInventory("WallTimer",1)
  		Loop
 	Death:
 		TNT1 A 0 A_Scream
		TNT1 A 0 A_KillSiblings
		TNT1 AAAA 0 A_CustomMissile("WallBolt",random(10,50),0,random(0,360),2,random(-15,15))
		TNT1 AAAA 0 A_CustomMissile("WallBolt2",random(10,50),0,random(0,360),2,random(-15,15))
 		Stop
  	}
}

actor WallPlacedCenter : WallPlaced { DeathSound "WallItem/Explode" }

actor WallTimer : Inventory { Inventory.MaxAmount 2222 +UNDROPPABLE -INVBAR } // 2100

actor RedSparkle
{
  height 2
  radius 1
  speed 1
  PROJECTILE
  +NOCLIP
  renderstyle Add
  alpha 0.9
  Scale 0.5
  translation "80:96=175:191"
  states
  {
  Spawn:
    PTCL ABCABCABCABCABCABC 2 bright
  Death:
    PTCL ABC 2 bright A_FadeOut(0.1)
    loop
  }
}

actor RedSparkleRandom : RandomSpawner
{
  DropItem "RedSparkleTiny"
  DropItem "RedSparkleSmall"
  DropItem "RedSparkleMedium"
  DropItem "RedSparkleLarge"
  DropItem "RedSparkle"
}

actor RedSparkleTiny   : RedSparkle { scale 0.1 }
actor RedSparkleSmall  : RedSparkle { scale 0.2 }
actor RedSparkleMedium : RedSparkle { scale 0.3 }
actor RedSparkleLarge  : RedSparkle { scale 0.4 }

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// RANDOM WEAPON ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor RandomWeaponItem : CustomInventory 25023
{
//$Category Stronghold_Items
  inventory.pickupmessage "$STR_GOTRANDOMWEAPON"
  inventory.maxamount 3
  inventory.icon WEPIA0
  Tag "$STR_POWERUPTAG_RANDOMWEAPON"
  +INVENTORY.INVBAR
  scale 0.75
  states
  {
  Spawn:
   	WEPP A -1 BRIGHT
    stop

  Use:
    TNT1 A 0 A_Jump(5, "GetBFG", "GetPyroCannon", "GetDevastator")
    TNT1 A 0 A_Jump(256, "GetRepeater", "GetFlamer", "GetRailgun", "GetStunner", "GetPlasma", "GetRL", "GetHRL", "GetMines")
    fail

  GetBFG:
	TNT1 A 0 A_JumpIfInventory("DEBFG9000", 1, 1)
  	goto GetBFG+2
	TNT1 A 0 A_JumpIfInventory("Cell", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 1)
	TNT1 A 0 A_GiveInventory("DEBFG9000", 4)
  	stop

  GetPyroCannon:
	TNT1 A 0 A_JumpIfInventory("PyroCannon", 1, 1)
  	goto GetPyroCannon+2
	TNT1 A 0 A_JumpIfInventory("Gas", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 2)
	TNT1 A 0 A_GiveInventory("PyroCannon", 4)
  	stop

  GetDevastator:
	TNT1 A 0 A_JumpIfInventory("Devastator", 1, 1)
  	goto GetDevastator+2
	TNT1 A 0 A_JumpIfInventory("Cell", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 3)
	TNT1 A 0 A_GiveInventory("Devastator", 4)
  	stop

  GetRepeater:
	TNT1 A 0 A_JumpIfInventory("Repeater", 1, 1)
  	goto GetRepeater+2
	TNT1 A 0 A_JumpIfInventory("Cell", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 4)
	TNT1 A 0 A_GiveInventory("Repeater", 4)
  	stop

  GetFlamer:
	TNT1 A 0 A_JumpIfInventory("Flamer", 1, 1)
  	goto GetFlamer+2
	TNT1 A 0 A_JumpIfInventory("Gas", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 5)
	TNT1 A 0 A_GiveInventory("Flamer", 4)
  	stop

  GetRailgun:
	TNT1 A 0 A_JumpIfInventory("Railgun", 1, 1)
  	goto GetRailgun+2
	TNT1 A 0 A_JumpIfInventory("Cell", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 6)
	TNT1 A 0 A_GiveInventory("Railgun", 4)
  	stop

  GetStunner:
	TNT1 A 0 A_JumpIfInventory("StunnerRifle", 1, 1)
  	goto GetStunner+2
	TNT1 A 0 A_JumpIfInventory("Cell", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 7)
	TNT1 A 0 A_GiveInventory("StunnerRifle", 4)
  	stop

  GetPlasma:
	TNT1 A 0 A_JumpIfInventory("DEPlasmaRifle", 1, 1)
  	goto GetPlasma+2
	TNT1 A 0 A_JumpIfInventory("Cell", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 8)
	TNT1 A 0 A_GiveInventory("DEPlasmaRifle", 4)
  	stop

  GetRL:
	TNT1 A 0 A_JumpIfInventory("DERLauncher", 1, 1)
  	goto GetRL+2
	TNT1 A 0 A_JumpIfInventory("RocketAmmo", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 9)
	TNT1 A 0 A_GiveInventory("DERLauncher", 4)
  	stop

  GetHRL:
	TNT1 A 0 A_JumpIfInventory("HRL", 1, 1)
  	goto GetHRL+2
	TNT1 A 0 A_JumpIfInventory("RocketAmmo", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 10)
	TNT1 A 0 A_GiveInventory("HRL", 4)
  	stop

  GetMines:
	TNT1 A 0 A_JumpIfInventory("LandMineLayer", 1, 1)
  	goto GetMines+2
	TNT1 A 0 A_JumpIfInventory("Mines", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 11)
	TNT1 A 0 A_GiveInventory("LandMineLayer", 4)
  	stop

  /*GetASG:
	TNT1 A 0 A_JumpIfInventory("AutoShotgun", 1, 1)
  	goto GetASG+2
	TNT1 A 0 A_JumpIfInventory("Shell", 0, "UseFail")
	TNT1 A 0 A_PlaySound("misc/w_pkup",3|80)
    TNT1 A 0 A_SetBlend("FFFF00",0.1,5)
    TNT1 A 0 ACS_NamedExecuteWithResult("StrRWDWeaponNotifier", 12)
	TNT1 A 0 A_GiveInventory("AutoShotgun", 4)
  	stop*/

  UseFail:
    TNT1 A 0
    fail
  }
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// AUTO DRONE ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR AutoDroneItem : CustomInventory 24025
{
//$Category Stronghold_Items
  inventory.pickupmessage "$STR_GOTAUTODRONE"
  inventory.maxamount 2
  inventory.icon DRNIA0
  Tag "$STR_POWERUPTAG_AUTODRONE"
  +INVENTORY.INVBAR
  scale 0.75
  states
  {
  Spawn:
   	DRNP A -1 BRIGHT
    	stop
  Use:
       TNT1 A 0 A_JumpIfInventory("ForceCubeTimerRight",1,3)
       TNT1 A 0 A_GiveInventory("ForceCubeTimerRight",1)
       TNT1 A 0 ACS_NamedExecuteWithResult("StrAutoDroneSpawnerRight", 4200)
       Stop
       TNT1 A 0 A_JumpIfInventory("ForceCubeTimerLeft",1,3)
       TNT1 A 0 A_GiveInventory("ForceCubeTimerLeft",1)
       TNT1 A 0 ACS_NamedExecuteWithResult("StrAutoDroneSpawnerLeft", 4200)
       Stop
       TNT1 A 0
       Fail
   }
}

ACTOR ForceCube1Right
{
    Radius 0
    Height 0
    +NOTIMEFREEZE
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("ForceCubeGiverRight",0,0,0,0,0,0,0,49)
        Stop
    }
}

ACTOR ForceCube1Left : ForceCube1Right
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("ForceCubeGiverLeft",0,0,0,0,0,0,0,49)
        Stop
    }
}

ACTOR ForceCube2Right : CustomInventory
{
    +AUTOACTIVATE
    +NOTIMEFREEZE
    +UNDROPPABLE
    States
    {
    Pickup:
        TNT1 A 0 A_SpawnItemEx("RightCube",28,16,36,0,0,0,0,49)
        Stop
    }
}

ACTOR ForceCube2Left : ForceCube2Right
{
    States
    {
    Pickup:
        TNT1 A 0 A_SpawnItemEx("LeftCube",28,-16,36,0,0,0,0,49)
        Stop
    }
}

ACTOR ForceCubeTimerLeft : CustomInventory
{
   +NOTIMEFREEZE
   +INVENTORY.INTERHUBSTRIP
   +UNDROPPABLE
   States
   {
   Use:
       TNT1 A 0
       Stop
   }
}

ACTOR ForceCubeTimerRight : ForceCubeTimerLeft {}

ACTOR ForceCubeTimer2 : Inventory
{
    +NOTIMEFREEZE
    +UNDROPPABLE
    Inventory.Amount 1
    Inventory.MaxAmount 4200
    States
    {
    Spawn:
        TNT1 A 0
        Stop
    }
}

ACTOR ForceCubeGiverRight
{
    Health 1000000000
    Radius 0
    Height 0
    Speed 0
    +NONSHOOTABLE
    +MISSILEMORE
    +MISSILEEVENMORE
    +NOTIMEFREEZE
    MinMissileChance 0
    States
    {
    Spawn:
        TNT1 A 1 A_Look
        Loop
    See:
        TNT1 A 1 A_Chase
        Loop
    Missile:
        TNT1 A 0 A_JumpIfInTargetInventory("ForceCubeTimerRight",1,2)
        TNT1 A 0 A_Jump(256,"Death")
        TNT1 A 0 A_JumpIfInventory("ForceCubeTimer2",0,"Death")
        TNT1 A 0 A_GiveInventory("ForceCubeTimer2",1)
        TNT1 A 1 A_GiveToTarget("ForceCube2Right",1)
        Loop
    Death:
        TNT1 A 0 A_TakeFromTarget("ForceCubeTimerRight",1)
        Stop
    }
}

ACTOR ForceCubeGiverLeft : ForceCubeGiverRight
{
    States
    {
    Missile:
        TNT1 A 0 A_JumpIfInTargetInventory("ForceCubeTimerLeft",1,2)
        TNT1 A 0 A_Jump(256,"Death")
        TNT1 A 0 A_JumpIfInventory("ForceCubeTimer2",0,"Death")
        TNT1 A 0 A_GiveInventory("ForceCubeTimer2",1)
        TNT1 A 1 A_GiveToTarget("ForceCube2Left",1)
        Loop
    Death:
        TNT1 A 0 A_TakeFromTarget("ForceCubeTimerLeft",1)
        Stop
    }
}

ACTOR RightCube
{
    Radius 0
    Height 0
    Speed 0
    Scale 0.1
    +FRIENDLY
    +NOGRAVITY
    +NOTIMEFREEZE
    +MISSILEMORE
    +MISSILEEVENMORE
    +NOTAUTOAIMED
    MinMissileChance 0
    Tag "$STR_MONSTERTAG_AUTODRONE"
    States
    {
    Spawn:
    	DRON AA 1 A_LookEx(2,0,800,0,135,"See")
        Stop
    See:
        TNT1 A 0 A_Jump(240,3) // the higher the number, the less frequent it attacks
        TNT1 A 0 A_CustomMissile("AutoDroneBall",8,0,0,0)
        DRON A 1 A_PlaySound("ForceCube/Fire",1)
        DRON A 1
        Stop
    }
}

ACTOR LeftCube : RightCube {}

ACTOR AutoDroneBall
{
   Radius 8
   Height 8
   Speed 20
   Damage 48
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.9
   Scale 0.2
   damagetype "bossfactor"
   +NOTIMEFREEZE
   +THRUSPECIES
   Species "Player"
   SeeSound "weapons/autodronef"
   DeathSound "weapons/autodronex"
   States
   {
   Spawn:
      DBA2 AB 4 Bright
      Loop
   Death:
      DBA2 C 4 Bright
      DBA2 DE 4 Bright
      Stop
   }
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SMARTBOMB ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor SmartBombItem : CustomInventory 24026
{
//$Category Stronghold_Items
  inventory.pickupmessage "$STR_GOTSMARTBOMB"
  inventory.maxamount 1
  inventory.icon SMAIA0
  Tag "$STR_POWERUPTAG_SMARTBOMB"
  +INVENTORY.INVBAR
  scale 0.75
  states
  {
  Spawn:
   	SMAP A -1 BRIGHT
    	stop
  Use:
        TNT1 A 1 A_SpawnItemEx("SmartBombGrenade", 0, 0, 32, 0, 10, 6, 90)
        stop
  }
}

actor SmartBombGrenade
{
	projectile
	-nogravity
	+hexenbounce
	+bounceonactors
	+canbouncewater
	+noexplodefloor
	+slidesonwalls
	-noteleport
	+thruspecies
	species "Player"
	bouncefactor 0.6
	painsound ""
    DamageType "SmartBomb"
	Obituary "$STR_OB_SMARTBOMB"
        speed 6
	radius 2
	height 2
	Damage (0)
        States
        {
         Spawn:
          UVGR ABCD 1
          UVGR ABCD 2
          UVGR ABCD 4
          UVGR A 16 A_Stop
          UVGR A 0 A_PlaysoundEx("UVCharge","voice",0,1)
          UVGR A 0 A_SpawnItemEx("smallestlightemmit", 0, 0, 0, 0, 0, 0, 0, 128)
          UVGR A 0 A_SpawnItemEx("smallerlightemmit", 0, 0, 0, 0, 0, 0, 0, 128)
          UVGR E 170 BRIGHT A_SpawnItemEx("smalllightemmit", 0, 0, 0, 0, 0, 0, 0, 128)
          UVGR E 0 BRIGHT A_SpawnItemEx("Blindinglightemmit", 0, 0, 0, 0, 0, 0, 0, 128)
          UVGR E 0 BRIGHT A_SpawnItemEx("MoreBlindinglightemmit", 0, 0, 0, 0, 0, 0, 0, 128)
          UVGR E 0 BRIGHT A_SpawnItemEx("MostBlindinglightemmit", 0, 0, 0, 0, 0, 0, 0, 128)
          UVGR E 0 BRIGHT A_CustomMissile("LightRailer", 0, 0, 0)
          UVGR E 0 BRIGHT Radius_Quake(9,17,0,64,0)
          UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit1", 0, 0, 0, 0, 0, 0, 0, 128)
	  UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit2", 0, 0, 0, 0, 0, 0, 0, 128)
	  UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit3", 0, 0, 0, 0, 0, 0, 0, 128)
	  UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit4", 0, 0, 0, 0, 0, 0, 0, 128)
	  UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit5", 0, 0, 0, 0, 0, 0, 0, 128)
          TNT1 A 15
          stop
		}
}

actor smalllightemmit
{
 radius 0
 height 0
 Renderstyle ADD
 alpha 0.75
 scale 0.5
 States
 {
   Spawn:
    WHFL A 170 BRIGHT
    WHFL A 1 BRIGHT A_Fadeout(0.05)
    wait
 }
}

actor smallerlightemmit : smalllightemmit
{
scale 0.35
}

actor smallestlightemmit : smalllightemmit
{
scale 0.2
}

actor Blindinglightemmit : smalllightemmit
{
 alpha 0.5
 scale 1
 States
 {
  Spawn:
   WHFL A 50 BRIGHT
   WHFL A 1 BRIGHT A_Fadeout(0.05)
   wait
}
}

actor moreBlindinglightemmit : Blindinglightemmit
{
 scale 0.75
}

actor mostBlindinglightemmit : Blindinglightemmit
{
 scale 0.5
}

actor LightrailerPuff : BulletPuff
{
    Mass 1
    Alpha 1
    DamageType "SmartBomb"
	States
	{
	Spawn:
        PUFF AB 4 bright
        goto Super::Melee
	}
}

actor Lightrailer
{
    +MissileMore
    +LOOKALLAROUND
    obituary "$STR_OB_SMARTBOMB"
    reactiontime 25
    MinMissileChance 0
    damagetype "SmartBomb"
    Speed 0
    Radius 0
    Height 0
    States
    {
    Spawn:
        TNT1 A 0 A_Changeflag("FRIENDLY", 1)
        TNT1 A 1 A_Look
        TNT1 A 0 A_Changeflag("FRIENDLY", 0)
        TNT1 A 1 A_Look
        TNT1 A 0 A_Changeflag("FRIENDLY", 1)
        TNT1 A 1 A_Look
        TNT1 A 0 A_Changeflag("FRIENDLY", 0)
        TNT1 A 1 A_Look
        Stop
    See:
        TNT1 A 1 A_Facetarget
        TNT1 A 0 A_CustomRailgun(200, 0, NONE, WHITE, 1, 0, 0, "LightrailerPuff")
        TNT1 A 0 A_CustomRailgun(200, 0, NONE, GREY, 1, 0, 0, "LightrailerPuff")
        TNT1 A 0 A_CustomRailgun(200, 0, NONE, GREY, 1, 0, 0, "LightrailerPuff")
        TNT1 A 0 A_CustomRailgun(200, 0, NONE, WHITE, 1, 0, 0, "LightrailerPuff")
        TNT1 A 0 A_CustomRailgun(200, 0, NONE, GREY, 1, 0, 0, "LightrailerPuff")
        TNT1 A 0 A_CustomRailgun(200, 0, NONE, GREY, 1, 0, 0, "LightrailerPuff")
        TNT1 A 1 A_CustomRailgun(200, 0, NONE, WHITE, 1, 0, 0, "LightrailerPuff")
        TNT1 A 0 A_Countdown
        goto spawn
    }
}

actor RingLightemmit1
{
 radius 0
 height 0
 Renderstyle ADD
 alpha 0.5
 scale 5.0
 States
{
Spawn:
 WHFL B 2 BRIGHT
 stop
}
}

actor ringlightemmit2 : ringlightemmit1
{
 alpha 0.4
 scale 6.0
}

actor ringlightemmit3 : ringlightemmit1
{
 alpha 0.3
 scale 7.0
}

actor ringlightemmit4 : ringlightemmit1
{
 alpha 0.2
 scale 8.0
}

actor ringlightemmit5 : ringlightemmit1
{
 alpha 0.1
 scale 9.0
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// VAMPIRISM / DRAIN /////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor DrainItem : PowerupGiver 24040
{
//$Category Stronghold_Items
 scale 0.75
 inventory.pickupmessage "$STR_GOTDRAIN"
 inventory.pickupannouncerentry "drain"
 inventory.icon "VAMIA0"
 inventory.maxamount 3
 inventory.usesound "pickups/slowmo"
 powerup.color "AA 00 00" 0.2
 powerup.type "Drain"
 powerup.duration -60
 Tag "$STR_POWERUPTAG_DRAIN"
 +INVENTORY.FANCYPICKUPSOUND
 states
 {
 Spawn:
   VAMP A -1 BRIGHT
   stop
 }
}

///////////////////////////////////////////////////////////////////////////////////////////////////////
// PROTECTION /////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerStrQuarterDamage : PowerProtection
{
damagefactor "normal", 0.25
//inventory.icon "MEGAA0"
}

actor ProtectionItem : PowerupGiver 24041
{
//$Category Stronghold_Items
 scale 0.75
 inventory.pickupmessage "$STR_GOTPROTECTION"
 inventory.pickupannouncerentry "guardsphere"
 inventory.icon "PROIA0"
 inventory.maxamount 5
 inventory.usesound "pickups/slowmo"
 powerup.color "FF AA 00" 0.1
 powerup.type StrQuarterDamage
 powerup.duration -40
 Tag "$STR_POWERUPTAG_PROTECTION"
 +INVENTORY.FANCYPICKUPSOUND
 states
 {
 Spawn:
   PROP A -1 BRIGHT
   stop
 }
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////
// MORPHPOWERUP /////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////

actor MorphPlayerItem : CustomInventory 24042
{
//$Category Stronghold_Items
  Inventory.PickupMessage "$STR_GOTMORPHPLAYER"
  Inventory.PickupSound "misc/p_pkup"
  Inventory.UseSound "pickups/slowmo"
  Inventory.MaxAmount 2
  Inventory.Icon "MORIA0"
  Tag "$STR_POWERUPTAG_MORPHPLAYER"
  +INVENTORY.INVBAR
  +INVENTORY.FANCYPICKUPSOUND
  Scale 0.75
  States
  {
  Spawn:
   	MORP A -1 bright
    stop

  Use:
    TNT1 A 0 A_JumpIfInventory("PowerMorphPlayer",1,"UseFail")
    TNT1 A 0 A_JumpIfInventory("PowerMorphPlayer2",1,"UseFail")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrMorphTypeChecker") == 1, "UseBruiser")

  UseTerminator:
    TNT1 A 0 A_GiveInventory("PowerMorphPlayer")
    stop

  UseBruiser:
    TNT1 A 0 A_GiveInventory("PowerMorphPlayer2")
    stop

  UseFail:
    TNT1 A 0
    fail
  }
}

actor PowerMorphPlayer : PowerMorph
{
    PowerMorph.PlayerClass "MorphedPlayer"
    PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_WHENINVULNERABLE|MRF_NEWTIDBEHAVIOUR|MRF_LOSEACTUALWEAPON)
    Powerup.Duration -120
}

actor PowerMorphPlayer2 : PowerMorphPlayer { PowerMorph.PlayerClass "MorphedPlayer2" }

/////////////////////////////////////////////////////////////////////////////////////////////////
// I WIN BUTTON - POWERUP ///////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerOSPDamage : PowerDamage {
  damagefactor "normal", 4
  Powerup.Duration -65
}
actor PowerOSPSpeed : PowerSpeed {
  Speed 2.0
  Powerup.Duration -65
}
actor PowerOSPFlight : PowerFlight { -HUBPOWER }

actor OSPInv : PowerupGiver {
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Powerup.Duration -65
  Powerup.Type "Invulnerable"
  Powerup.Mode Reflective
  Powerup.Color GoldMap
  States
  {
  Spawn:
    MEGA A 6
    Loop
  }
}
actor OSPQuad : PowerupGiver {
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Powerup.Duration -65
  Powerup.Type "OSPDamage"
  Powerup.color "FF 00 00",0.35
  States
  {
  Spawn:
    MEGA A 6
    Loop
  }
}
actor OSPSpeed : PowerupGiver {
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Powerup.Duration -65
  Powerup.Type "OSPSpeed"
  States
  {
  Spawn:
    MEGA A 6
    Loop
  }
}
actor OSPFlight : PowerupGiver {
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Powerup.Duration -65
  Powerup.Type "OSPFlight"
  States
  {
  Spawn:
    MEGA A 6
    Loop
  }
}
actor OSPInfiniteAmmo : PowerupGiver {
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Powerup.Duration -65
  Powerup.Type "InfiniteAmmo"
  States
  {
  Spawn:
    MEGA A 6
    Loop
  }
}
actor OSPPower : PowerupGiver {
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Powerup.Duration -65
  Powerup.Type "WeaponLevel2"
  States
  {
  Spawn:
    MEGA A 6
    Loop
  }
}
actor OSPHealth : Health {
  Inventory.Amount 300
  Inventory.MaxAmount 300
  States
  {
  Spawn:
    MEGA A 6
    Loop
  }
}

actor IWinButtonItem : CustomInventory 23475
{
//$Category Stronghold_Items
  inventory.maxamount 1
  Inventory.Icon IWBIA0
  Inventory.PickupMessage "$STR_GOTIWINBUTTON"
  +INVENTORY.INVBAR
  Tag "$STR_POWERUPTAG_IWINBUTTON"
  states
  {
    Spawn:
      IWBP A -1 BRIGHT
      stop
    Use:
      TNT1 A 0 A_JumpIfInventory("PowerOSPDamage",1,"UseFail")
      TNT1 A 0 A_GiveInventory("OSPInv")
      TNT1 A 0 A_GiveInventory("OSPQuad")
      TNT1 A 0 A_GiveInventory("OSPSpeed")
      TNT1 A 0 A_GiveInventory("OSPFlight")
      TNT1 A 0 A_GiveInventory("OSPInfiniteAmmo")
      TNT1 A 0 A_GiveInventory("OSPPower")
      TNT1 A 0 A_GiveInventory("OSPHealth")
      TNT1 A 0 A_GiveInventory("SuperShield")
      TNT1 A 0 ACS_NamedExecuteWithResult("StrIWinButtonMusic")
      stop
    UseFail:
      TNT1 A 0
      fail
  }
}

/////////////////////////////////////////////////////////////////////////////////////////////////
// TROLL-PROOFED POWERUPS ///////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////

actor StrBackpackCooldownTimer : Inventory { Inventory.MaxAmount 0x7FFFFFFF +UNDROPPABLE -INVBAR }

actor StrBackpack : CustomInventory replaces Backpack
{
    Height 26
    Inventory.PickupMessage "$GOTBACKPACK"
    States
    {
      Spawn:
        BPAK A -1
        stop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("StrBackpackCooldownTimer",1,"PickupFail")
        TNT1 A 0 A_JumpIfInventory("Backpack",1,1)
        goto PickupSuccess
        TNT1 A 0 A_JumpIfInventory("Clip",0,1)
        goto PickupSuccess
        TNT1 A 0 A_JumpIfInventory("Shell",0,1)
        goto PickupSuccess
        TNT1 A 0 A_JumpIfInventory("RocketAmmo",0,1)
        goto PickupSuccess
        TNT1 A 0 A_JumpIfInventory("Cell",0,1)
        goto PickupSuccess
        TNT1 A 0 A_JumpIfInventory("Mines",0,1)
        goto PickupSuccess
        TNT1 A 0 A_JumpIfInventory("Gas",0,"PickupFail")

      PickupSuccess:
        TNT1 A 0 A_GiveInventory("Backpack")
        TNT1 A 0 ACS_NamedExecuteWithResult("StrBackpackCooldown", 30)
        stop

      PickupFail:
        BPAK A 1
        fail
    }
}

actor StrInvulnerabilitySphere : CustomInventory replaces InvulnerabilitySphere
{
    Inventory.PickupMessage "$GOTINVUL"
    Inventory.PickupSound "misc/p_pkup"
    Inventory.PickupAnnouncerEntry "invulnerability"
    +COUNTITEM
    +INVENTORY.BIGPOWERUP
    States
    {
      Spawn:
        PINV ABCD 6 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerInvulnerable",1,"PickupFail")
        TNT1 A 1 A_GiveInventory("InvulnerabilitySphere")
        stop

      PickupFail:
        PINV ABCD 6 bright
        fail
    }
}

actor StrBlurSphere : CustomInventory replaces BlurSphere
{
    Inventory.PickupMessage "$GOTINVIS"
    Inventory.PickupSound "misc/p_pkup"
    Inventory.PickupAnnouncerEntry "partialinvisibility"
    RenderStyle Translucent
    +COUNTITEM
    +INVENTORY.BIGPOWERUP
    +VISIBILITYPULSE
    States
    {
      Spawn:
        PINS ABCD 6 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerInvisibility",1,"PickupFail")
        TNT1 A 1 A_GiveInventory("BlurSphere")
        stop

      PickupFail:
        PINS ABCD 6 bright
        fail
    }
}

actor StrRadSuit : CustomInventory replaces RadSuit
{
    Height 46
    Inventory.PickupMessage "$GOTSUIT"
    Inventory.PickupSound "misc/p_pkup"
    States
    {
      Spawn:
        SUIT A -1 bright
        stop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerIronFeet",1,"PickupFail")
        TNT1 A 1 A_GiveInventory("RadSuit")
        stop

      PickupFail:
        SUIT A 1 bright
        fail
    }
}

actor StrInfrared : CustomInventory replaces Infrared
{
    Inventory.PickupMessage "$GOTVISOR"
    Inventory.PickupSound "misc/p_pkup"
    +COUNTITEM
    States
    {
      Spawn:
        PVIS A 6 bright
        PVIS B 6
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerLightAmp",1,"PickupFail")
        TNT1 A 1 A_GiveInventory("Infrared")
        stop

      PickupFail:
        PVIS A 6 bright
        PVIS B 6
        fail
    }
}

actor MegaMap : CustomInventory replaces Allmap
{
    +COUNTITEM
    +FANCYPICKUPSOUND
    Inventory.MaxAmount 0
    Inventory.PickupSound "misc/p_pkup"
    Inventory.PickupMessage "$GOTMAP"
    States
    {
      Spawn:
        PMAP ABCDCB 6 bright
        loop

      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerScanner",1,"PickupFail")
        TNT1 A 0 A_GiveInventory("Allmap")
        TNT1 A 0 A_GiveInventory("DoomScanner")
        stop

      PickupFail:
        PMAP ABCDCB 6 bright
        fail
    }
}

actor DoomScanner : PowerupGiver { Powerup.Duration -300 Powerup.Type "Scanner" +AUTOACTIVATE }

actor FixedHealthBonus : HealthBonus replaces HealthBonus { -INVENTORY.ALWAYSPICKUP }
actor FixedArmorBonus : ArmorBonus replaces ArmorBonus { -INVENTORY.ALWAYSPICKUP }

// [MP] Soulsphere and Megasphere fixes
actor FixedMegasphere : Megasphere replaces Megasphere 
{ 
  -NOGRAVITY
  -INVENTORY.ALWAYSPICKUP
  States
  {
  Pickup:
    TNT1 A 0 A_JumpIfHealthLower(200,"PickupSuccess")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrHealthAndArmorPickupChecker", 2, 200) == 1, "PickupFail")
  PickupSuccess: //Intentional Fallthrough
    TNT1 A 0 A_GiveInventory("BlueArmorForMegasphere", 1)
    TNT1 A 0 A_GiveInventory("MegasphereHealth", 1)
    Stop
  PickupFail:
    MEGA ABCD 6 Bright
    Fail
  }
}
actor FixedSoulsphere : Soulsphere replaces Soulsphere 
{ 
    -INVENTORY.AUTOACTIVATE
    -INVENTORY.ALWAYSPICKUP
    -NOGRAVITY 
}

actor FixedBerserk : Berserk replaces Berserk
{
    -AUTOACTIVATE
    -ALWAYSPICKUP
    States
    {
      Pickup:
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,1)
        goto PickupHeal
        TNT1 A 0 A_JumpIf(CheckClass("MorphedPlayer"), "PickupMorph")
    	TNT1 A 0 A_JumpIf(CheckClass("MorphedPlayer2"), "PickupMorph")
    	TNT1 A 0 A_JumpIf(CheckClass("ShrunkenPlayer"), "PickupMorph")
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrHealthAndArmorPickupChecker", 1, 100) == 1, "PickupFail")
        TNT1 A 0 A_JumpIfHealthLower(400,"PickupHeal")
        fail

      PickupMorph:
        TNT1 A 0 A_JumpIfHealthLower(100,"PickupHeal")
        fail

      PickupHeal:
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrBerserkSwitchCVarChecker") == 0, "PickupHealFinish")
        TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("StrBerserkSwitchCVarChecker") == 1, 1)
        goto PickupHeal+3
        TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"PickupHealFinish")
        TNT1 A 0 ACS_NamedExecuteAlways("StrBerserkFistSwitchHack") //A_SelectWeapon("DEFist")
      PickupHealFinish:
        TNT1 A 0 A_GiveInventory("PowerStrength")
        TNT1 AAAA 0 A_GiveInventory("Medikit") //HealThing(100, 0)
        stop

      PickupFail:
        PSTR A 1
        fail
    }
}

actor StrRedCardDN : RedCardDN replaces RedCardDN { ConversationID 301 Inventory.PickupMessage "$STR_GOTREDCARDDN" Tag "$STR_KEYTAG_REDCARDDN" }
actor StrAccessDiscBlue : AccessDiscBlue replaces AccessDiscBlue { Inventory.PickupMessage "$STR_GOTACCESSDISCBLUE" Tag "$STR_KEYTAG_ACCESSDISCBLUE" }
actor StrAccessDiscGreen : AccessDiscGreen replaces AccessDiscGreen { Inventory.PickupMessage "$STR_GOTACCESSDISCGREEN" Tag "$STR_KEYTAG_ACCESSDISCGREEN" }
actor StrYellowCardFamiliarMaps : YellowCard replaces YellowCard { Inventory.PickupMessage "$STR_GOTYELLOWCARDFAMILIARMAPS" Tag "$STR_KEYTAG_YELLOWCARDFAMILIARMAPS" -INVENTORY.INTERHUBSTRIP }
