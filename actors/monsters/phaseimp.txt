ACTOR PhaseImp 24105
{
//$Category Stronghold_Monsters
    PainChance "Stunner", 128
    SpawnID 232
    Health 150
    Radius 20
    Height 56
    Mass 120
    Speed 8
    PainChance 192
    MeleeDamage 4
    MONSTER
    +FLOORCLIP
    SeeSound "neodarkimp/sight"
    PainSound "neodarkimp/pain"
    DeathSound "neodarkimp/death"
    ActiveSound "neodarkimp/active"
    MeleeSound "neodarkimp/melee"
    Obituary "$STR_OB_PHASEIMP"
    HitObituary "$STR_OB_PHASEIMPHIT"
    Tag "$STR_MONSTERTAG_PHASEIMP"
    MissileType DarkImpBall
    States
    {
    Spawn:
        DIMP AB 10 A_Look
        Loop
    See:
        DIMP AABBCCDD 3 A_Chase
        Loop
        DIMP AABBCCDDAABBCCDDAABBCCDDAABBCCDD 1 A_ExtChase(0,0)
        DIMP AABBCCDDAABBCCDD 1 A_Chase("Melee", "MissileNoTeleport")
        Goto See+40
    Melee:
    MissileNoTeleport:
        TNT1 A 0 A_UnHideThing
        TNT1 A 0 A_SetTranslucent(1)
        DIMP EF 8 A_FaceTarget
        DIMP G 6 A_ComboAttack
        Goto See
    Missile:
        TNT1 A 0 A_Jump(64,1)
        Goto MissileNoTeleport
        TNT1 A 0 A_PlaySound("neodarkimp/teleport")
        DIMP E 8 Bright A_FaceTarget
        TNT1 A 0 A_FaceTarget
        DIMP E 1 Bright A_SetTranslucent(0.9)
        DIMP E 1 Bright A_SetTranslucent(0.8)
        DIMP E 1 Bright A_SetTranslucent(0.7)
        DIMP E 1 Bright A_SetTranslucent(0.6)
        DIMP E 1 Bright A_SetTranslucent(0.5)
        DIMP E 1 Bright A_SetTranslucent(0.4)
        DIMP E 1 Bright A_SetTranslucent(0.3)
        DIMP E 1 Bright A_SetTranslucent(0.2)
        DIMP E 1 Bright A_SetTranslucent(0.1)
        TNT1 A 0 A_HideThing
        Goto See+8
    Pain:
        TNT1 A 0 A_UnHideThing
        DIMP H 2 A_SetTranslucent(1)
        DIMP H 2 A_Pain
        Goto See
    Pain.Stunner:
        TNT1 A 0 A_UnHideThing
        TNT1 A 0 A_SetTranslucent(1)
       	DIMP H 2 A_ChangeFlag("NoPain", 1)
       	DIMP H 300 A_Pain
       	DIMP H 0 A_ChangeFlag("NoPain", 0)
       	goto See
    Death:
        TNT1 A 0 A_GiveToTarget("AwardItem", 6)
        TNT1 A 0 A_UnHideThing
        DIMP I 8 A_SetTranslucent(1)
        DIMP J 8 A_Scream
        DIMP K 6
        DIMP L 6 A_Fall
        DIMP M 3000 CanRaise
        DIMP M 2 A_FadeOut(0.1)
        wait
    XDeath:
        TNT1 A 0 A_GiveToTarget("AwardItem", 9)
        TNT1 A 0 A_UnHideThing
        DIMP N 5 A_SetTranslucent(1)
        DIMP O 5 A_XScream
        DIMP P 5 A_Fall
        DIMP QRST 5
        DIMP U 3000 CanRaise
        DIMP U 2 A_FadeOut(0.1)
        wait
    Raise:
        DIMP LK 8
        DIMP JI 6
        goto See
    }
}

ACTOR DarkImpBall
{
    Radius 6
    Height 8
    Speed 10
    Damage (Random(1,3)*Random(3,9)) //3-27. Huh. Odd damage formula.
    DamageType "Fire"
    PROJECTILE
    +SEEKERMISSILE
    SeeSound "neodarkimp/attack"
    DeathSound "neodarkimp/shotx"
    RenderStyle Add
    Alpha 0.75
    States
    {
    Spawn:
        DIMB AB 4 Bright A_SeekerMissile(20,0)
        loop
    Death:
        DIMB CDE 6 Bright
        stop
    }
}
