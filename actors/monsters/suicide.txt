/////////////////////
// Suicide Bomber ///
// by Tormentor667 //
/////////////////////

actor SuicideBomber 22099
{
//$Category Stronghold_Monsters
  PainChance "Stunner", 160
  spawnid 243
  obituary "$STR_OB_SUICIDEBOMBER"
  Tag "$STR_MONSTERTAG_SUICIDEBOMBER"
  health 80
  radius 20
  height 56
  mass 100
  speed 10
  painchance 20
  seesound "suicide/sit"
  painsound "shotguy/pain"
  deathsound "shotguy/death"
  MONSTER
  +FLOORCLIP
  states
  {
  Spawn:
    BMBE AB 10 A_Look
    loop
  See:
    BMBE AAABBBCCCDDD 2 A_Chase
    BMBE A 0 A_PlaySound("suicide/scream")
    loop
  Melee:
    BMBE A 0 A_Die
    goto Death
  Pain:
    BMBE A 3
    BMBE A 3 A_Pain
    goto See
  Pain.Stunner:
       BMBE A 2 A_ChangeFlag("NoPain", 1)
       BMBE A 300 A_Pain
       BMBE A 0 A_ChangeFlag("NoPain", 0)
       goto See
  Death:
    TNT1 A 0 A_GiveToTarget("AwardItem", 4)
    TNT1 A 0 A_SetDamageType("Rocket")
    TNT1 A 0 A_Explode(50,128,4)
    BMBE M 5 bright A_PlaySound("weapons/rocklx")
    BMBE N 5 bright A_XScream
    BMBE O 5 A_NoBlocking
    BMBE PQRST 5
    BMBE U 3000
    BMBE U 2 A_FadeOut(0.1)
    wait
  }
}
