//Shadow
ACTOR Shadow 3126
{
	//$Category Stronghold_Monsters
	painchance "Stunner",160
	Health 80
	Height 56
	Mass 100
	RenderStyle "Translucent"
	Alpha 0.7
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	PainSound "Shadow/pain"
	DeathSound "Shadow/death"
	ActiveSound "Shadow/active"
	Obituary "%o was assassinated by a shadow."
	States
	{
	Spawn:
		SHDW AB 10 A_Look
		Loop
	See:
		SHDW A 15
		SHDW AAA 1 A_Chase
		SHDW A 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		SHDW BBB 1 A_Chase
		SHDW B 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		SHDW CCC 1 A_Chase
		SHDW C 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		SHDW DDD 1 A_Chase
		SHDW D 0 A_SpawnItemEx("ShadowGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME)
		Goto See+1
	Missile:
		SHDW EEE 10 A_FaceTarget
		SHDW F 5 A_FaceTarget
		SHDW G 5 BRIGHT A_CustomMissile("ShadowBall")
		SHDW F 5
		SHDW EE 10 A_FaceTarget
		SHDW F 5 A_FaceTarget
		SHDW G 5 BRIGHT A_CustomMissile("ShadowBall")
		SHDW F 5
		SHDW EE 10 A_FaceTarget
		SHDW F 5 A_FaceTarget
		SHDW G 5 BRIGHT A_CustomMissile("ShadowBall")
		SHDW F 5
		SHDW EE 10 A_FaceTarget
		Goto See+1
	Pain:
		SHDW H 4
		SHDW H 4 A_Pain
		Goto See+1
	Pain.Stunner:
		SHDW H 4 A_ChangeFlag("NoPain",1)
		SHDW H 300 A_Pain
		SHDW H 0 A_ChangeFlag("NoPain",0)
		goto See+1
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		SHDW I 6 A_Scream
		SHDW J 5
		SHDW K 5 A_SpawnItemEx("Shadowtorso",0,0,0,1)
		SHDW L 5 A_Fall
		SHDW MNO 6
		SHDW P 3000
		SHDW P 1 A_FadeOut(0.05)
		Wait
	XDeath:
		SHDX A 8 ACS_NamedExecuteAlways("AwardPoints",0,1)
		SHDX B 6 A_XScream
		SHDX C 6
		SHDX D 6 A_Fall
		SHDX E 6
		SHDX F 6
		SHDX G 3000
		SHDX G 1 A_FadeOut(0.05)
		Wait
	Raise:
		SHDW MKLJI 8
		Goto See
	}
}

ACTOR ShadowBall
{
	Radius 6
	Height 8
	Speed 12
	Damage 4
	Projectile
	+RANDOMIZE
	RenderStyle "ADD"
	Alpha 0.75
	SeeSound "Shadow/attack"
	DeathSound "imp/shotx"
	Decal DoomImpScorch
	States
	{
	Spawn:
		SBAL ABC 4 BRIGHT LIGHT("ShadowBall") A_SpawnItemEx("ShadowTrail")
		Loop
	Death:
		SBAL C 5 Bright LIGHT("ShadowBall")
		SBAL D 4 BRIGHT LIGHT("ShadowBall1")
		SBAL E 4 BRIGHT LIGHT("ShadowBall2")
		SBAL F 4 BRIGHT LIGHT("ShadowBall3")
		SBAL G 4 BRIGHT LIGHT("ShadowBall4")
		SBAL H 4 BRIGHT LIGHT("ShadowBall5")
		Stop
	}
}

ACTOR ShadowGhost
{
	+CLIENTSIDEONLY
	Radius 4
	Height 8
	Mass 75
	RenderStyle "Translucent"
	Alpha 0.3
	PROJECTILE
	states
	{
	Spawn:
		"####" "#" 10
		Stop
	}
}

ACTOR Shadowtorso
{
	+CLIENTSIDEONLY
	Radius 12
	Height 15
	Speed 1
	Mass 5000
	RenderStyle "Translucent"
	alpha 0.7
	+Doombounce
	States
	{
	Spawn:
		SHDW QRS 5
		SHDW T 5 A_Fall
		SHDW U 5
		SHDW V 3000
		SHDW V 1 A_FadeOut(0.05)
		Wait
	}
}

actor ShadowTrail
{
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.6
	+NOCLIP
	States
	{
	Spawn:
		SHTR ABCDEF 4 Bright
		Stop
	}
}