actor Azazel
{
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	Health 2400
	Radius 22
	Height 64
	Speed 4
	PainChance 20
	Mass 1500
	MONSTER
	+BOSS
	+FLOORCLIP
	+MISSILEEVENMORE
	+MISSILEMORE
	+NORADIUSDMG
	MinMissileChance 32
	SeeSound "diablo2/goatman/sight"
	PainSound "diablo2/goatman/pain"
	DeathSound "Goat/death"
	ActiveSound "diablo2/goatman/idle"
	Obituary "%o was slaughtered by Azazel."
	States
	{
	Spawn:
		AZEW A 5 A_Look
		Loop
	See:
		AZEW A 1 A_Chase
		AZEW AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		AZEW A 1 A_Chase
		AZEW AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		AZEW B 1 A_Chase
		AZEW BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		AZEW B 1 A_Chase
		AZEW BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		AZEW C 1 A_Chase
		AZEW CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		AZEW C 1 A_Chase
		AZEW CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		AZEW D 1 A_Chase
		AZEW DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		AZEW D 1 A_Chase
		AZEW DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Missile:
		TNT1 A 0 A_Jump(256,"MDoubleSerie","MRightHand","MLeftHand")
	MLeftHand:
		TNT1 A 0 A_PlaySound("Goat/Attack")
		AZEA DE 4 A_FaceTarget
		AZEA F 7 A_CustomMissile("SatyrLordMissile1",32,-5,0)
		Goto see
	MRightHand:
		TNT1 A 0 A_PlaySound("Goat/Attack")
		AZEA AB 4 A_FaceTarget
		AZEA C 7 A_CustomMissile("SatyrLordMissile2",32,5,0)
		Goto see
	MDoubleSerie:
		TNT1 A 0 A_PlaySound("missile1charge",5)
		TNT1 A 0 A_FaceTarget
		AZEA D 5 A_SpawnItemEx("SatyrLordMissileDual1Fake",10,-20,60,0,0,0,32)
		AZEA E 5 A_SpawnItemEx("SatyrLordMissileDual1Fake",10,-9,46,0,0,0,32)
		AZEA F 6 A_SpawnItemEx("SatyrLordMissileDual1Fake",10,4,28,0,0,0,32)
		TNT1 A 0 A_PlaySound("missile2charge",6)
		TNT1 A 0 A_FaceTarget
		AZEA A 5 A_SpawnItemEx("SatyrLordMissileDual2Fake",10,20,60,0,0,0,32)
		AZEA B 5 A_SpawnItemEx("SatyrLordMissileDual2Fake",10,9,46,0,0,0,32)
		AZEA C 6 A_SpawnItemEx("SatyrLordMissileDual2Fake",10,-4,28,0,0,0,32)
		TNT1 A 0 A_PlaySound("Goat/Attack")
		TNT1 A 0 A_FaceTarget
		AZEA D 5 A_CustomMissile("SatyrLordMissileDual1Real",60,-20,0)
		AZEA E 5 A_CustomMissile("SatyrLordMissileDual1Real",46,-9,0)
		AZEA F 6 A_CustomMissile("SatyrLordMissileDual1Real",28,4,0)
		TNT1 A 0 A_PlaySound("Goat/Attack")
		TNT1 A 0 A_FaceTarget
		AZEA A 5 A_CustomMissile("SatyrLordMissileDual2Real",60,20,0)
		AZEA B 5 A_CustomMissile("SatyrLordMissileDual2Real",46,9,0)
		AZEA C 6 A_CustomMissile("SatyrLordMissileDual2Real",28,-4,0)
		Goto see
	Melee:
		TNT1 A 0 A_PlaySound("Goat/Attack")
		TNT1 A 0 A_Jump(128,"Melee2")
		AZEA AB 7 A_FaceTarget
		AZEA C 7 A_CustomMeleeAttack(random(1,8)*8,"baron/melee")
		Goto See
	Melee2:
		AZEA DE 7 A_FaceTarget
		AZEA F 7 A_CustomMeleeAttack(random(1,8)*8,"baron/melee")
		Goto See
	Pain:
		AZEP A 2
		AZEP A 2 A_Pain
		AZEP A 4 A_Jump(64,"Paincounteratk")
		Goto See
	Paincounteratk:
		TNT1 A 0 HealThing(random(10,120))
		AZEP A 1 A_CustomMissile("SLGroundFire",0,0,0)
		AZEP A 1 A_CustomMissile("SLGroundFire",0,0,45)
		AZEP A 1 A_CustomMissile("SLGroundFire",0,0,-45)
		AZEP A 1 A_CustomMissile("SLGroundFire",0,0,90)
		AZEP A 1 A_CustomMissile("SLGroundFire",0,0,-90)
		AZEP A 1 A_CustomMissile("SLGroundFire",0,0,135)
		AZEP A 1 A_CustomMissile("SLGroundFire",0,0,-135)
		AZEP A 5 A_CustomMissile("SLGroundFire",0,0,180)
		Goto See
	Pain.Stunner:
		AZEP A 2 A_ChangeFlag("NoPain",1)
		AZEP A 300 A_Pain
		AZEP A 4 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		AZED A 5 ACS_NamedExecuteAlways("AwardPoints",0)
		AZED B 5 A_Scream
		AZED C 5
		AZED D 4 A_Fall
		AZED E 4
		AZED F 3
		AZED G 3000
		AZED G 1 A_FadeOut(0.05)
		Wait
	}
}

actor SLGroundFire
{
	Projectile
	Speed 16
	Damage 1
	+RIPPER
	Scale 0.4
	+FLOORHUGGER
	SeeSound "groundfirestart"
	RenderStyle Add
	Alpha 0.9
	Decal Scorch
	States
	{
	Spawn:
		FX03 BCDEFGHIJKLMNOPQRSTUVWXY 2 LIGHT("SLGroundFire") A_SpawnItemEx("SLGFXtraSpawner")
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

Actor SLGFXtraSpawner
{
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Explode(16,64,0)
		TNT1 AAA 0 A_SpawnItemEx("SLGFXtra",0,0,0,random(3,-3),random(3,-3),random(3,-3))
		Stop
	}
}

Actor SLGFXtra
{
	+CLIENTSIDEONLY
	Projectile
	+NOCLIP
	Radius 1
	+DONTBLAST
	Height 1
	Scale 0.09
	RenderStyle Add
	States
	{
	Spawn:
		STAR P 3 Bright
		STAR P 0 A_Jump(128,1)
		Loop
		STAR P 3 Bright
		STAR P 1 Bright A_FadeOut
		Wait
	}
}

//*************************************************
//*************************************************
Actor SatyrLordMissile1 : FastProjectile
{
	Projectile
	Radius 8
	Height 16
	Speed 50
	Damage 19
	Scale .65
	RenderStyle Add
	SeeSound "redballbsee"
	DeathSound "redballbdeth"
	Translation "192:207=172:191"
	Decal PlasmaScorchLower
	States
	{
	Spawn:
		ANFB AAABBBCCC 1 Bright LIGHT("SatyrLordMissile11") A_SpawnItemEx("SLM1Trail",random(8,-8),0,random(5,-5))
		Loop
	Death:
		ANFB D 3 Bright LIGHT("SatyrLordMissile12")
		ANFB E 3 Bright LIGHT("SatyrLordMissile13")
		ANFB F 3 Bright LIGHT("SatyrLordMissile14")
		ANFB G 3 Bright LIGHT("SatyrLordMissile15")
		Stop
	}
}

Actor SLM1Trail
{
	+CLIENTSIDEONLY
	Projectile
	-NOGRAVITY
	Gravity 0.4
	+NOCLIP
	RenderStyle Add
	Alpha 0.8
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 ThrustThingZ(0,random(-4,8),0,1)
		SHTR ABCDDE 3 Bright
		SHTR F 1 Bright A_FadeOut(0.1)
		Wait
	}
}

//*************************************************
//*************************************************
Actor SatyrLordMissile2
{
	Speed 9
	damage 15
	scale 0.35
	radius 6
	height 12
	RENDERSTYLE ADD
	Alpha 0.75
	PROJECTILE
	+SEEKERMISSILE
	seesound "whiteballbsee"
	deathsound "whiteballbdeth"
	states
	{
	Spawn:
		PSMM A 2 Bright LIGHT("SatyrLordMissile21") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM A 2 Bright LIGHT("SatyrLordMissile21") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM B 2 Bright LIGHT("SatyrLordMissile22") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM B 2 Bright LIGHT("SatyrLordMissile22") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM C 2 Bright LIGHT("SatyrLordMissile23") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM C 2 Bright LIGHT("SatyrLordMissile23") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM D 2 Bright LIGHT("SatyrLordMissile24") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM E 2 Bright LIGHT("SatyrLordMissile24") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		PSMM F 2 Bright LIGHT("SatyrLordMissile24") A_SeekerMissile(10,25)
		TNT1 A 0 A_SpawnItemEx("SLM2Trail")
		goto Spawn+12
	Death:
		PSMM GGGG 1 Bright LIGHT("SatyrLordMissile25") A_FadeOut(0.025)
		PSMM HHHH 1 Bright LIGHT("SatyrLordMissile26") A_FadeOut(0.025)
		PSMM IIII 1 Bright LIGHT("SatyrLordMissile27") A_FadeOut(0.025)
		PSMM JJJJ 1 Bright LIGHT("SatyrLordMissile28") A_FadeOut(0.025)
		PSMM KKKK 1 Bright LIGHT("SatyrLordMissile29") A_FadeOut(0.025)
		PSMM LLLL 1 Bright LIGHT("SatyrLordMissile210") A_FadeOut(0.025)
		PSMM MMMM 1 Bright LIGHT("SatyrLordMissile211") A_FadeOut(0.025)
		stop
	}
}

Actor SLM2Trail
{
	+CLIENTSIDEONLY
	Projectile
	+NOCLIP
	Damage 0
	RenderStyle Add
	Alpha 0.45
	Scale 0.9
	States
	{
	Spawn:
		TNT1 A 2
		PUF2 BDFHJLNPRTVXZ 1
		PUF3 B 1 A_FadeOut(0.2)
		Wait
	}
}

//*************************************************
//*************************************************
Actor SatyrLordMissileDual1Fake
{
	Projectile
	Scale 0.34
	RenderStyle Add
	SeeSound "waitingballsspawn"
	States
	{
	Spawn:
		MSP2 ABCDABCDABCDABCD 2
		Stop
	}
}

Actor SatyrLordMissileDual1Real : FastProjectile
{
	Speed 42
	Scale 0.34
	RenderStyle Add
	Damage 16
	Radius 6
	height 12
	SeeSound "redballssee"
	DeathSound "redballsdeth"
	Decal DoomImpScorch
	States
	{
	Spawn:
		MSP2 A 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		MSP2 A 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		MSP2 B 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		MSP2 B 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		MSP2 C 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		MSP2 C 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		MSP2 D 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		MSP2 D 1 LIGHT("SatyrLordRedMissile1") A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		TNT1 AA 0 A_SpawnItemEx("SLMD1RTrail",random(8,-8),0,random(5,-5))
		Loop
	Death:
		MSP1 G 2 LIGHT("SatyrLordRedMissile2") A_Explode(64,128,0)
		MSP1 H 2 LIGHT("SatyrLordRedMissile3")
		MSP1 I 2 LIGHT("SatyrLordRedMissile4")
		MSP1 J 2 LIGHT("SatyrLordRedMissile5")
		MSP1 K 2 LIGHT("SatyrLordRedMissile6")
		MSP1 L 2 LIGHT("SatyrLordRedMissile7")
		MSP1 M 2 LIGHT("SatyrLordRedMissile8")
		MSP1 N 2 LIGHT("SatyrLordRedMissile9")
		MSP1 O 2 LIGHT("SatyrLordRedMissile10")
		Stop
	}
}

Actor SLMD1RTrail : SLM1Trail
{
	Scale 0.3
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 ThrustThingZ(0,random(-3,5),0,1)
		SHTR ABCDDE 3 Bright
		SHTR F 1 Bright A_FadeOut(0.1)
		Wait
	}
}

Actor SatyrLordMissileDual2Fake
{
	Projectile
	Scale 0.8
	RenderStyle Add
	Alpha 0.75
	SeeSound "waitingballsspawn"
	Translation "112:127=80:111"
	States
	{
	Spawn:
		CSSF DEDEDEDEDEDEDEDE 2 Bright
		Stop
	}
}

Actor SatyrLordMissileDual2Real : SatyrLordMissileDual2Fake
{
	Speed 7
	Radius 6
	Damage 12
	+SeekerMissile
	SeeSound "whiteballssee"
	DeathSound "whiteballsdeth"
	height 12
	Decal PlasmaScorchLower
	States
	{
	Spawn:
		CSSF D 1 Bright LIGHT("SatyrLordWhiteMissile1") A_SeekerMissile(4,10)
		TNT1 AAA 0 A_SpawnItemEx("SLMD2RTrail",random(5,-5),0,random(5,-5))
		CSSF D 1 Bright LIGHT("SatyrLordWhiteMissile1") A_SeekerMissile(4,10)
		TNT1 AAA 0 A_SpawnItemEx("SLMD2RTrail",random(5,-5),0,random(5,-5))
		CSSF E 1 Bright LIGHT("SatyrLordWhiteMissile1") A_SeekerMissile(4,10)
		TNT1 AAA 0 A_SpawnItemEx("SLMD2RTrail",random(5,-5),0,random(5,-5))
		CSSF E 1 Bright LIGHT("SatyrLordWhiteMissile1") A_SeekerMissile(4,10)
		TNT1 AAA 0 A_SpawnItemEx("SLMD2RTrail",random(5,-5),0,random(5,-5))
		Loop
	Death:
		CSSF G 4 Bright LIGHT("SatyrLordWhiteMissile2")
		CSSF H 4 Bright LIGHT("SatyrLordWhiteMissile3")
		CSSF I 4 Bright LIGHT("SatyrLordWhiteMissile4")
		CSSF I 1 Bright A_FadeOut
		Wait
	}
}

Actor SLMD2RTrail
{
	+CLIENTSIDEONLY
	Projectile
	+NOCLIP
	RenderStyle Add
	Alpha 0.5
	Scale 0.25
	States
	{
	Spawn:
		PUF2 BCDEFGHIJKL 1
		PUF2 L 1 A_FadeOut(0.2)
		Wait
	}
}