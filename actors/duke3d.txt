actor ShrunkenPlayer : StrongholdPlayer
{
  Scale 0.125
  Radius 4
  Height 7
  Speed 0.45
  Player.ViewHeight 5
  Player.MorphWeapon "DEShrinkFist"
}

actor DEShrinkFist : DEFist
{
  Weapon.SisterWeapon "" // Just in case.
  Tag "$TAG_FIST"
  +NOALERT
  +CHEATNOTWEAPON
  States
  {
  Ready:
    PUNG Z 1 A_WeaponReady
	Loop
  Select:
    TNT1 A 0 A_Raise
    PUNG Z 1 A_Raise
	Loop
  Deselect:
    TNT1 A 0 A_Lower
    PUNG Z 1 A_Lower
	Loop
  Fire:
    PUNG Z 0
    Goto Ready
  }
}

/****** actor prototypes - changing these changes all the actors that inherit from them, no need to doomednum ******/

ACTOR DukeMetalBarrelPrototype
{
	Radius 16
	Height 40
	Mass 100
	Scale 0.7
	Health 50
//	+SHOOTABLE
	+SOLID
	+NOBLOOD
	States
	{
	Death:
		DNBR A 0
		Stop
	}
}

/****** normal definitions, start numbering ******/

ACTOR DukeParkingMeter 164
{
//$Category Stronghold_DukeNukem
	Radius 4
	Height 40
	Scale 0.6
	Mass 900000
	+SOLID
	States
	{
	Spawn:
		DNST C -1
        stop
	}
}

ACTOR DukeLampPost  165
{
//$Category Stronghold_DukeNukem
	Radius 4
	Height 128
	+SOLID
	States
	{
	Spawn:
		DNST D -1
        stop
	}
}

ACTOR DukeBin 166
{
//$Category Stronghold_DukeNukem
	Health 100
	Radius 16
	Height 40
	Scale 0.6
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+NODAMAGE
	Mass 0x7fffffff
    PainChance 255
	States
	{
	Spawn:
		DNBR B -1
        stop
	Pain:
		DNBR C 10
		Goto Spawn
	}
}

ACTOR DukeBox 167
{
//$Category Stronghold_DukeNukem
	Radius 4
	Height 8
	Scale 0.6
	States
	{
	Spawn:
		DNBX A -1
        stop
	}
}

/****** inherited classes ******/

ACTOR DukeMetalBarrel1 : DukeMetalBarrelPrototype 168
{
//$Category Stronghold_DukeNukem
	States
	{
	Spawn:
		DNBR A -1
        stop
	}
}

ACTOR FlameBreathX {
	Radius 16
	Height 1
	Speed 16
	RenderStyle ADD
	Scale 0.5
    DamageType "Fire"
	Projectile
	Damage 1
	ExplosionDamage 2
	ExplosionRadius 64
	+Ripper
	+NoDamageThrust
	+StrifeDamage
	DontHurtShooter
	SeeSound "duke/flame"
	States
	{
	Spawn:
		XPLO ABCDEFGHIJKLMN 3 BRIGHT A_Explode
		XPLO OPQRSTU 3 BRIGHT
		Stop
	}
}

ACTOR Queen
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 173
	Health 6000
	Mass 800
	Radius 40
	Height 140
	Scale 0.65
	Speed 20
    PainChance 32
	MONSTER
	+FLOORCLIP
    //+DONTRIP
    +DONTMORPH
	+BOSS
	+FULLVOLACTIVE
	SeeSound "queen/sight"
	PainSound "queen/pain"
	DeathSound "queen/death"
	ActiveSound "queen/active"
	Obituary "$STR_OB_QUEEN"
    Tag "$STR_MONSTERTAG_QUEEN"
	States
	{
	Spawn:
		QUEE AB 10 A_Look
		Loop
	See:
		QUEE AABBCCDD 3 A_Chase
		Loop
	Missile:
		QUEE S 0 A_PlaySound("queen/scream")
		QUEE STU 8 A_FaceTarget
		QUEE UUUUUUUUUUUUUUUUUUUU 2 Bright A_CustomMissile("FlameBreathX", random(42,46), 0, random(-8,8))
		Goto See
	Melee:
		QUEE E 3 A_FaceTarget
		QUEE E 3 A_PlaySound("queen/shock")
		QUEE FG 3 BRIGHT A_CustomMeleeAttack(5,"","","Melee",0) // was originally 1-2, I think
		QUEE F 0 A_FaceTarget
		QUEE FG 3 BRIGHT A_CustomMeleeAttack(5,"","","Melee",0)
		QUEE F 0 A_FaceTarget
		QUEE FG 3 BRIGHT A_CustomMeleeAttack(5,"","","Melee",0)
		QUEE F 0 A_FaceTarget
		QUEE FG 3 BRIGHT A_CustomMeleeAttack(5,"","","Melee",0)
		QUEE F 0 A_FaceTarget
		Goto See
	Pain:
		QUEE S 3
		QUEE S 3 A_Pain
		Goto See
	Pain.Stunner:
		QUEE S 2 A_ChangeFlag("NoPain", 1)
		QUEE S 300 A_Pain
		QUEE S 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 40)
		QUEE H 6
		QUEE I 6 A_Scream
		QUEE J 6 A_Fall
		QUEE KLMNOP 6
		QUEE Q 3000
		QUEE Q 2 A_FadeOut(0.1)
		wait
	}
}

ACTOR Cycloid
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 32
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 172
	Health 4500
	Mass 10000
	Radius 40
	Height 140
	Scale 1.35
	Speed 20
    PainChance 32
	MONSTER
	+BOSS
    +DONTMORPH
    //+DONTRIP
	+FLOORCLIP
	SeeSound "cycloid/sight"
	PainSound "cycloid/pain"
	DeathSound "cycloid/death"
	ActiveSound "cycloid/active"
	Obituary "$STR_OB_CYCLOID"
    Tag "$STR_MONSTERTAG_CYCLOID"
	States
	{
	Spawn:
		CYCL AB 10 A_Look
		Loop
	See:
		CYCL A 0 A_PlaySound("cycloid/stomp")
		CYCL ABCD 5 A_Chase
		CYCL A 0 A_JumpIfCloser(1024,"See")
		Goto Charge
	Charge:
		CYCL A 0 A_PlaySound("cycloid/stomp")
		CYCL ABCD 3 A_Chase
		Loop
	Missile:
		CYCL E 0 A_PlaySound("cycloid/attack")
		CYCL E 0 A_JumpIfCloser(512, "Psi") //change to jump if closer+chance
        Rockets:
		CYCL E 4 A_FaceTarget
		CYCL F 4 BRIGHT	A_CustomMissile("DukeRocket", 48, 12, random(-45,45))
		CYCL E 0 A_CPosRefire
		Loop
	Psi:
		CYCL A 0 A_Jump(128,"Rockets")
		CYCL G 20 A_FaceTarget
		CYCL HHHHHHHHHH 4 A_CustomMissile("PsiBlast", 64, 0, random(-45,45))
		CYCL E 0 A_Jump(128, "See")
		Goto Missile+1
	Pain:
		CYCL I 1
		CYCL I 1 A_Pain
        goto PainFinish
	Pain.Stunner:
		CYCL I 2 A_ChangeFlag("NoPain", 1)
		CYCL I 300 A_Pain
		CYCL I 0 A_ChangeFlag("NoPain", 0)
    PainFinish:
		CYCL A 0 A_JumpIfCloser(1024,"See")
		Goto Charge
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 48)
		CYCL I 8
		CYCL J 8 A_Scream
		CYCL K 8 A_Fall
		CYCL LMNOP 8
		CYCL Q 0 A_PlaySound("cycloid/thud")
		CYCL Q 3000
		CYCL Q 2 A_FadeOut(0.1)
		wait
	}
}

ACTOR Overlord
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 171
	Health 4500
	Mass 2000
	Radius 48
	Height 100
	Scale 1.2
	Speed 25
    PainChance 32
	MONSTER
    //+DONTRIP
    +BOSS
	+FLOORCLIP
    +DONTMORPH
	SeeSound "fatty/sight"
	AttackSound "duke/rocket"
	PainSound "fatty/pain"
	DeathSound "fatty/die"
	ActiveSound "fatty/active"
	MissileType "DukeRocketSlow"
	MissileHeight 100
	Obituary "$STR_OB_OVERLORD"
    Tag "$STR_MONSTERTAG_OVERLORD"
	States
	{
	Spawn:
		FATY AB 10 A_Look
		Loop
	See:
		FATY A 0 A_PlaySound("fatty/stomp")
		FATY ABCD 6 A_Chase
		Loop
	Charge:
		FATY A 0 A_PlaySound("fatty/stomp")
		FATY ABCD 5 A_Chase
		Loop
	Missile:
		FATY E 0 A_JumpIfCloser(1024, 1)
		Goto Charge
		FATY EF 15 A_FaceTarget
		FATY G 8 A_FaceTarget
		FATY G 7 A_FaceTarget
		FATY H 7 Bright	A_MissileAttack
		FATY G 0 A_CPosRefire
		Goto Missile+4
	Pain:
		FATY I 3
		FATY I 3 A_Pain
		Goto See
	Pain.Stunner:
		FATY I 2 A_ChangeFlag("NoPain", 1)
		FATY I 300 A_Pain
		FATY I 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 33)
		FATY J 6
		FATY K 6 A_Scream
		FATY L 6 A_Fall
		FATY MNOPQ 6
		FATY R 3000
		FATY R 2 A_FadeOut(0.1)
		wait
	}
}

ACTOR AlienDrone
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
    spawnid 170
	Health 300
	GibHealth -150
	Radius 20
	Height 56
	Scale 0.75
	Speed 16
    PainChance 60
	Gravity 0.75
	MONSTER
	+FLOORCLIP
	SeeSound "alien/sight"
	PainSound "alien/pain"
	DeathSound "alien/death"
	ActiveSound "alien/active"
	MissileType "AlienDroneRay"
	MissileHeight 52
	Obituary "$STR_OB_ALIENDRONE" // HitObituary only works with the Melee damage type
    Tag "$STR_MONSTERTAG_ALIENDRONE"
	States
	{
	Spawn:
		DGRD A 10 A_Look
		Loop
	See:
		DGRD BBCCDDEE 3 A_Chase
		TNT1 A 0 A_Jump(128,1) //change to jump if closer
		loop
		DGRD BBCCDDEEBBCCDDEEBBCCDDEE 2 A_Chase //running speed
		loop
	Melee:
		DGRD F 6 A_FaceTarget
		DGRD F 0 A_PlaySound("alien/attack")
		DGRD G 6 A_CustomMeleeAttack(22,"","","Melee")
		DGRD H 6
		Goto See
	Missile:
		DGRD I 0 A_Jump(240,"Jump")
		DGRD I 0 A_PlaySound("alien/missile")
		DGRD IIIJJJJ 5 Bright A_FaceTarget
		DGRD J 0 A_MissileAttack
		Goto See
	Jump:
        TNT1 A 0 A_JumpIf(velz==0,1)
        goto See
		DGRD R 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-8)
		DGRD RRR 3 ThrustThingZ(0,35,0,0)
		DGRD SSSSSSSSS 1
	CheckforFloor:
		DGRD T 1 A_JumpIf(velz==0,"Land")
		Loop
	Land:
		TNT1 A 0 A_PlaySound("alien/splode")
		TNT1 A 0 Thing_Stop(0)
		Goto See
	Pain:
		DGRD L 3
		DGRD L 3 A_Pain
		Goto See
	Pain.Stunner:
		DGRD L 2 A_ChangeFlag("NoPain", 1)
		DGRD L 300 A_Pain
		DGRG L 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 9)
		DGRD L 6
		DGRD M 6 A_Scream
		DGRD N 6 A_Fall
		DGRD OP 6
		DGRD Q 3000
		DGRD Q 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 12)
        TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
        TNT1 A 5
		Stop
	}
}

ACTOR AlienDroneRay : MorphProjectile 
{
//$Category Stronghold_DukeNukem
	Scale 0.5
	Radius 8
	Height 8
	Speed 25
	Damage (1)
	DontHurtShooter
	Projectile
	SeeSound ""
	DeathSound "alien/splode"
	MorphProjectile.PlayerClass "ShrunkenPlayer"
	MorphProjectile.Duration 175
	MorphProjectile.MorphStyle MRF_FULLHEALTH|MRF_UNDOBYTOMEOFPOWER|MRF_FAILNOTELEFRAG|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHFORCED
	MorphProjectile.MorphFlash "ShrinkFuzz"
	MorphProjectile.UnMorphFlash "ShrinkFuzz"
	States
	{
	Spawn:
		DGS1 ABCD 4 Bright
		Loop
	Death:
		DGE1 ABCD 4 Bright
		Stop
	}
}

ACTOR KamikazeSentry
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 169
	Health 150
	Mass 80
	Radius 20
	Height 56
	Scale 0.7
	Speed 15
    PainChance 255
	MONSTER
	+NOGRAVITY
	+DROPOFF
	+FLOAT
	+NOBLOOD
	SeeSound "sentry/sight"
	PainSound "sentry/pain"
	ActiveSound "sentry/active"
	DeathSound "duke/splode"
	Damage (0)
	MeleeRange 96
    //Decal Scorch
	Obituary "$STR_OB_KAMIKAZESENTRY"
    Tag "$STR_MONSTERTAG_KAMIKAZESENTRY"
	States
	{
	Spawn:
		SENT A 10 A_Look
		Loop
	See:
		SENT A 4 A_Chase
		SENT A 0 A_PlaySound("sentry/jet")
		Loop
	Missile:
		SENT A 0 A_PlaySound("sentry/charge") //unlike the lost soul, the sentry stops charging after a while so he doesnt get stuck
		SENT A 6 A_FaceTarget
		SENT A 6 A_SkullAttack
		SENT A 6 A_FaceTarget
		SENT A 30 A_SkullAttack // 6 + SENT AAAA 6
		Goto See
	Pain:
		SENT A 3
		SENT A 3 A_Pain
		Goto See
	Pain.Stunner:
		SENT A 2 A_ChangeFlag("NoPain", 1)
		SENT A 300 A_Pain
		SENT A 0 A_ChangeFlag("NoPain", 0)
		goto See
	Melee:
		SENT A 3 A_PlaySound("sentry/arm")
		TNT1 A 0 A_GiveToTarget("AwardItem", 6)
		TNT1 A 0 A_Die("KamikazeSentry")
        goto Death.KamikazeSentry
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 8)
	Death.KamikazeSentry:
        TNT1 A 0 A_SetDamageType("Rocket")
		TNT1 A 0 A_Explode(27,128,4)
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Scrap1",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Scrap2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_SpawnItem("RedExplosion",0,12)
		TNT1 A 0 A_Fall
		Stop
	}
}

actor RedExplosion
{
    DeathSound "duke/splode"
    +CLIENTSIDEONLY
    +NOINTERACTION
    States
    {
      Spawn:
        XPLO A 0 bright
        XPLO A 0 bright A_Scream
        XPLO ABCDEFGHIJKLMNOPQRSTU 2 bright
        stop
    }
}

actor RedExplosionSilent : RedExplosion
{
    DeathSound ""
    States
    {
      Spawn:
        goto Super::Spawn+2
    }
}

ACTOR Commander
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 168
	Health 350
	GibHealth -175
	Mass 200
	Radius 20
	Height 56
	Scale 0.7
	Speed 4
    PainChance 64
	MONSTER
	+NOGRAVITY
	+DROPOFF
	+FLOAT
	SeeSound "commander/sight"
	PainSound "commander/pain"
	DeathSound "commander/death"
	ActiveSound "commander/active"
	MissileType "DukeRocketTrailer"
	MissileHeight 16
	Obituary "$STR_OB_COMMANDER"
    Tag "$STR_MONSTERTAG_COMMANDER"
	States
	{
	Spawn:
		COMM A 10 A_Look
		Loop
	See:
		COMM AAAABBBBCCCC 1 A_Chase
		Loop
	Missile:
		COMM I 0 A_PlaySound("commander/attack")
		COMM JJJJJJJJJJ 6 A_FaceTarget
		COMM J 4 BRIGHT A_MissileAttack
		COMM J 8
		COMM J 0 A_Jump(150,1)
		Goto See
		COMM I 0 A_SpidRefire
		Goto Missile+6
	Pain:
		COMM K 3
		COMM K 3 A_Pain
		Goto See
	Pain.Stunner:
		COMM K 2 A_ChangeFlag("NoPain", 1)
		COMM K 300 A_Pain
		COMM K 0 A_ChangeFlag("NoPain", 0)
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 9)
		COMM K 8 A_Scream
		COMM LM 8
		COMM N 3000
		COMM N 2 A_FadeOut(0.1)
		wait
	Crash:
		COMM O 8 A_Fall
		COMM PQ 8
		COMM R 3000
		COMM R 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 12)
        TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
        TNT1 A 5 A_ChangeFlag("DONTCORPSE",1) // don't jump to the crash state if it hits the ground
		Stop
	}
}

ACTOR DukeRocket {
	Scale 0.6
	Radius 8
	Height 8
	Speed 25
	DontHurtShooter
	Projectile
	SeeSound "duke/rocket"
	DeathSound "duke/splode"
    DamageType "Rocket"
	MissileType "DukeRocketTrail"
	MissileHeight 3
    Decal Scorch
	States
	{
	Spawn:
		DROK A 1
		Loop
	Death:
		TNT1 A 0 A_Explode(156,104,1)
		TNT1 A 1 A_SpawnItem("RedExplosionSilent",0,32)
		Stop
	}
}

actor DukeRocketTrailer : DukeRocket {
	SeeSound ""
	States
	{
	Spawn:
		DROK A 0
		DROK A 1 A_SpawnItemEx("DukeRocketTrail", 0, 0, 3, 0, 0, 1, 0, 128)
		Loop
	Death:
		TNT1 A 0 A_Explode(39,91,1)
		TNT1 A 1 A_SpawnItem("RedExplosionSilent",0,32)
		Stop
	}
}

actor DukeRocketSlow : DukeRocket {
	SeeSound ""
	Scale 0.5
	Speed 18
}

ACTOR DukeRocketTrail {
	Scale 0.5
	Radius 12
	Height 8
	Speed 1
	Damage (0)
	RenderStyle Translucent
	PROJECTILE
	States
	{
	Spawn:
		TRAI XABCD 2
		Stop
	}
}

ACTOR BattleLord
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 64
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 167
	Health 1000
	Mass 2000
	Radius 20
	Height 72
	Scale 0.75
	Speed 10
    PainChance 32
	MONSTER
	+FLOORCLIP
	+BOSS
	+FULLVOLACTIVE
    //+DONTRIP
	SeeSound "battlelord/sight"
	AttackSound "lizard/attack"
	PainSound "battlelord/pain"
	DeathSound "battlelord/death"
	ActiveSound "battlelord/active"
	MissileType "Mortar"
	MissileHeight 24
	Obituary "$STR_OB_BATTLELORD"
    Tag "$STR_MONSTERTAG_BATTLELORD"
	States
	{
	Spawn:
		LORD AB 10 A_Look
		Loop
	See:
		LORD AABBCCDD 3 A_Chase
		Loop
	Missile:
		LORD E 2 A_Jump(90, "Mortar")
		Goto Chaingun
	Chaingun:
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		Goto See
	Mortar:
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 10 Bright A_CustomMissile("Mortar",32,20,0,2,35)
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 10 Bright A_CustomMissile("Mortar",32,20,0,2,35)
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 10 Bright A_CustomMissile("Mortar",32,20,0,2,35)
		LORD G 10 A_SpidRefire
		LORD E 0 A_Jump(256,"See","Missile")
		Goto See
	Pain:
		LORD I 3
		LORD I 3 A_Pain
		Goto See
	Pain.Stunner:
		LORD I 2 A_ChangeFlag("NoPain", 1)
		LORD I 300 A_Pain
		LORD I 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 16)
		LORD I 14 A_Scream
		LORD J 14 A_Fall
		LORD KLM 14
		LORD N 3000
		LORD N 2 A_FadeOut(0.1)
		wait
	}
}

ACTOR BattleLordPal : BattleLord 
{
    Health 1500
//$Category Stronghold_DukeNukem
	spawnid 166
}

ACTOR SuperBattleLord : BattleLord 
{
//$Category Stronghold_DukeNukem
	spawnid 165
	Scale 1.4
	Speed 18
	Radius 69
	Height 130
	Health 4500
	Mass 6000
	MissileHeight 64
	MinMissileChance 48
	Obituary "$STR_OB_SUPERBATTLELORD"
    Tag "$STR_MONSTERTAG_SUPERBATTLELORD"
    +DONTMORPH
	States
	{
	See:
	Roam:
		LORD G 0 A_PlaySound("battlelord/stomp")
		LORD AABBCCDD 3 A_Chase
		Loop
	Charge:
		LORD G 0 A_PlaySound("battlelord/stomp")
		LORD AABBCCDD 2 A_Chase
		Loop
	Missile:
		LORD E 0 A_JumpIfCloser(1024, 1)
		Goto Charge
		LORD E 0 A_Jump(90, "Mortar")
		Goto Chaingun
	Chaingun:
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		LORD E 3 A_FaceTarget
		LORD F 3 Bright	A_CustomBulletAttack(22.5,5,6,6,"NewPuff",0,CBAF_NORANDOM)
		Goto See
	Mortar:
		LORD G 10 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 12 Bright A_CustomMissile("Mortar",38,40,0,2,45)
		LORD G 12 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 12 Bright A_CustomMissile("Mortar",38,40,0,2,45)
		LORD G 12 A_FaceTarget
		LORD G 0 A_PlaySound("duke/rocket")
		LORD H 12 Bright A_CustomMissile("Mortar",38,40,0,2,45)
		LORD G 12 A_SpidRefire
		LORD E 0 A_Jump(256,"See","Missile")
		Goto See
	Pain:
		LORD I 3
		LORD I 3 A_Pain
		Goto See
	Pain.Stunner:
		LORD I 2 A_ChangeFlag("NoPain", 1)
		LORD I 300 A_Pain
		LORD I 0 A_ChangeFlag("NoPain", 0)
		goto See
	}
}

Actor Mortar {
	Scale 0.6
	Radius 8
	Height 8
	Speed 16
	Damage (0)
	DontHurtShooter
	PROJECTILE
	-NOGRAVITY
	BounceType "Doom"
	BounceSound "duke/bounce"
	States
	{
	Spawn:
		LGRE ABCD 2 A_SpawnItemEx("MortarTrail",0,0,0,0,0,0,0,128)
		Loop
	Death:
		XPLO A 0 A_SpawnItemEx("MortarTrap",0,0,0,0,0,0,0,1024)
		Stop
	}
}

Actor MortarTrap {
   Scale 0.6
   Radius 16
   Height 12
   Mass 1000000
   Health 10
   Damage (0)
   DONTHURTSHOOTER
   Speed 0
   DamageType "Rocket"
   Decal Scorch
   +NOBLOOD
   +SHOOTABLE
   +MISSILE
   States
   {
   Spawn:
      LGRE A 0 A_Gravity
      LGRE ABCD 2
      Loop
   Melee:
        TNT1 A 0 A_Die
   Death:
        TNT1 A 0 A_SetDamageType("Rocket")
		TNT1 A 0 A_Explode(56,96)
		TNT1 A 1 A_SpawnItem("RedExplosion",0,0)
		Stop
	}
}

ACTOR MortarTrail {
	+NOGRAVITY
	+NOBLOCKMAP
	Scale 0.5
	States
	{
	Spawn:
		LGRE ABCD 2 A_FadeOut(0.125)
		loop
	}
}

ACTOR OctaBrain
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 164
	Health 175
	GibHealth -105
	Radius 20
	Height 56
	Scale 0.7
	Speed 10
	Mass 400
    PainChance 200
	MONSTER
	+NOGRAVITY
	+DROPOFF
	+FLOAT
	SeeSound "octabrain/sight"
	AttackSound "octabrain/attack"
	PainSound "octabrain/pain"
	DeathSound "octabrain/death"
	ActiveSound "octabrain/active"
	MissileType "PsiBlast"
	MissileHeight 32
    Obituary "$STR_OB_OCTABRAIN"
    Tag "$STR_MONSTERTAG_OCTABRAIN"
	States
	{
	Spawn:
		OCTA AB 10 A_Look
		Loop
	See:
		OCTA AABBCC 3 A_Chase
		Loop
	Missile:
		OCTA B 5
		OCTA C 5 A_FaceTarget 
		OCTA D 20 A_FaceTarget
		OCTA D 5 BRIGHT A_CustomComboAttack("PsiBlast",32,11,"octabrain/attack","Melee")
		OCTA C 5
		Goto See
	Pain:
		OCTA E 3
		OCTA E 3 A_Pain
		Goto See
	Pain.Stunner:
		OCTA E 2 A_ChangeFlag("NoPain", 1)
		OCTA E 300 A_Pain
		OCTA E 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 6)
		OCTA E 8
		OCTA F 8 A_Scream
		OCTA G 8 A_Fall
		OCTA HI 8
		OCTA J 3000
		OCTA J 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 10)
        TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
        TNT1 A 5
		Stop
	}
}

ACTOR PsiBlast {
	Scale 0.7
	Radius 12
	Height 8
	RenderStyle Translucent
	Alpha 0.85
	Speed 20
	Damage (38)
	Projectile
	SeeSound "octabrain/blast"
	States
	{
	Spawn:
		OPSI ABCDEFGHIJKLMNOPQRST 2 Bright
		Stop
	Death:
		OPSI T 0
		Stop
	}
}

ACTOR LizardCommando
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 163
	Health 100
	GibHealth -75
	Radius 20
	Height 56
	Scale 0.7
	Speed 24
    PainChance 32
	Gravity 0.6
	MONSTER
	+FLOORCLIP
	SeeSound "lizard/sight"
	AttackSound "lizard/attack"
	PainSound "lizard/pain"
	DeathSound "lizard/death"
	ActiveSound "lizard/roam"
	MissileType "LizardPhlegm"
	MissileHeight 24
	Obituary "$STR_OB_LIZARDCOMMANDO"
    Tag "$STR_MONSTERTAG_LIZARDCOMMANDO"
	States
	{
	Spawn:
		LIZD AB 10 A_Look
		Loop
	See:
		LIZD A 0 A_Chase
		LIZD AB 5 A_Chase
		LIZD C 0 A_Chase
		LIZD CD 5 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_JumpIfCloser(512,1)
		Goto Jump
		TNT1 A 0 A_Jump(256,"Chaingun","Jump","Spit")
	Chaingun:
		LIZD I 2 A_FaceTarget
		LIZD J 2 BRIGHT A_CustomBulletAttack(22.5,2.25,1,5,"NewPuff",0,CBAF_NORANDOM)
		TNT1 A 0 A_SpidRefire
		Loop
	Spit:
		LIZD EEEEFFFFGGGG 3 A_FaceTarget
		LIZD E 0 A_CustomMissile("LizardPhlegm",24,0,-20,2,3)
		LIZD E 0 A_CustomMissile("LizardPhlegm",24,0,20,2,3)
		LIZD E 8 A_CustomMissile("LizardPhlegm",24,0,0,2,3)
		Goto See
	Jump:
        TNT1 A 0 A_JumpIf(velz==0,1)
        goto See
		LIZD Q 1 A_FaceTarget
		TNT1 A 0 A_Recoil(-8)
		LIZD QQQ 3 ThrustThingZ(0,35,0,0)
		LIZD RRRRRRRRR 1
	CheckforFloor:
		LIZD S 1 A_JumpIf(velz==0,"Land")
		Loop
	Land:
		TNT1 A 0 A_PlaySound("alien/splode")
		TNT1 A 0 Thing_Stop(0)
		Goto See
	Pain:
		LIZD K 3
		LIZD K 3 A_Pain
        goto PainFinish
	Pain.Stunner:
		LIZD K 2 A_ChangeFlag("NoPain", 1)
		LIZD K 300 A_Pain
		LIZD K 0 A_ChangeFlag("NoPain", 0)
    PainFinish:
		LIZD E 0 A_Jump(25, 1) //dump chance
		Goto See
		LIZD EF 10
		LIZD G 15
		LIZD H 15 A_CustomMissile("ADump", 4,0,0)
		LIZD G 10
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 4)
		LIZD K 6
		LIZD L 6 A_Scream
		TNT1 A 0 A_Jump(64, "DropStuff") // emulates DropItem "LizardAmmoDrop" 64 properly
	DeathContinue:
		LIZD M 6 A_Fall
		LIZD NO 6
		LIZD P 3000
		LIZD P 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 6)
        TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_PlaySound("duke/splat")
    	TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    	TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
        TNT1 A 5
		Stop
	DropStuff:
		TNT1 A 0 A_Jump(256,1,2)
		TNT1 A 0 A_DropItem("Chaingun")
		goto DeathContinue
		TNT1 A 0 A_DropItem("ClipBox")
		goto DeathContinue
	}
}

ACTOR LizardAmmoDrop : RandomSpawner
{
//$Category Stronghold_DukeNukem
  DropItem Chaingun 255 1
  DropItem ClipBox 255 4
}

ACTOR LizardPhlegm {
	Scale 0.7
	Radius 13
	Height 8
	Speed 20
	VSpeed 20
	Damage (8)
	PROJECTILE
	-NOGRAVITY
	Gravity 0.6
	PoisonDamage 8
	SeeSound "lizard/spit"
	States
	{
	Spawn:
		SPIT A 3 A_SpawnItemEx("LizardPhlegmTrail", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	}
}

ACTOR LizardPhlegmTrail {
	Scale 0.35
	Radius 13
	Height 8
	+LOWGRAVITY
	States
	{
	Spawn:
		SPIT Z 2
		SPIT A 6
		Stop 
	}
}

ACTOR ADump {
	Scale 0.4
	Radius 13
	Height 8
	Speed 0
	States
	{
	Spawn:
		SHIT A 10 A_Look
		Loop
	See:
		SHIT A 15 A_Chase
		Loop
	Melee:
		SHIT A 1 A_PlaySound("lizard/shit")
	Death:
		SHIT A 0
		Stop
	}
}

ACTOR PigCop
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 162
	Health 100
	GibHealth -75
	Radius 20
	Height 56
	Scale 0.7
	Speed 8
    PainChance 32
	MONSTER
	+FLOORCLIP
	SeeSound "pig/sight"
	AttackSound "pig/attack"
	PainSound "pig/pain"
	DeathSound "pig/death"
	ActiveSound "pig/active"
	Obituary "$STR_OB_PIGCOP"
    Tag "$STR_MONSTERTAG_PIGCOP"
	States
	{
	Spawn:
		LARD AB 10 A_Look
		Loop
	See:
		LARD AABBCCDD 4 A_Chase
		Loop
	Missile:
		LARD F 0 A_Jump(64,"DuckShoot")
	Shoot:
		LARD F 12 A_FaceTarget
		LARD G 10 Bright A_CustomBulletAttack(22.5,2.25,5,5,"NewPuff",0,CBAF_NORANDOM)
		LARD E 10 A_PlaySound("pig/cock")
		LARD EFF 7
		LARD E 0 A_Jump(128,"See")
		Goto Shoot+1
	DuckShoot: //heh
		LARD H 12 A_FaceTarget
		LARD I 22
		LARD J 10 Bright A_CustomBulletAttack(22.5,2.25,3,10,"NewPuff",0,CBAF_NORANDOM)
		LARD I 10 A_PlaySound("pig/cock")
		LARD E 0 A_Jump(128,"See")
		Goto DuckShoot+1
	Pain:
		LARD K 3
		LARD K 3 A_Pain
		Goto See
	Pain.Stunner:
		LARD K 2 A_ChangeFlag("NoPain", 1)
		LARD K 300 A_Pain
		LARD K 0 A_ChangeFlag("NoPain", 0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 2)
		LARD K 6
		LARD L 6 A_Scream
		TNT1 A 0 A_Jump(128, "DropStuff") // emulates DropItem "PigCopAmmoDrop" 128 properly
	DeathContinue:
		LARD N 6 A_Fall
		LARD O 6
		LARD P 6
		LARD Q 3000
		LARD Q 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 3)
        TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_PlaySound("duke/splat")
		TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
		TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
        TNT1 A 5
		Stop
	DropStuff:
		TNT1 A 0 A_Jump(256,1,2)
		TNT1 A 0 A_DropItem("Shotgun")
		goto DeathContinue
		TNT1 A 0 A_DropItem("Shell")
		goto DeathContinue
	}
}

ACTOR PigCopAmmoDrop : RandomSpawner
{
//$Category Stronghold_DukeNukem
  DropItem Shotgun 255 1
  DropItem Shell 255 4
}

ACTOR DukeFodder
{
//$Category Stronghold_DukeNukem
    PainChance "Stunner", 128
	BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
	spawnid 160
	Health 30
	GibHealth -30
	Radius 20
	Height 56
	Scale 0.7
	Speed 12
    PainChance 200
	MONSTER
	+FLOORCLIP
	SeeSound "lizard/sight"
	PainSound "lizard/pain"
	DeathSound "lizard/death"
	ActiveSound "lizard/active"
	MissileType "FodderLaser"
	MissileHeight 26
	DropItem Clip 128
	Obituary "$STR_OB_DUKEFODDER"
    Tag "$STR_MONSTERTAG_DUKEFODDER"
	States
	{
	Spawn:
		DFOD AB 10 A_Look
		Loop
	See:
		DFOD G 0 A_ChangeFlag("NOGRAVITY",0)
		DFOD G 0 A_ChangeFlag("NOPAIN",0)
		DFOD G 0 A_ChangeFlag("FLOAT",0)
		DFOD ABCD 4 A_Chase
		Goto See+3
	Missile:
		DFOD F 0 A_JumpIfCloser(1024,2)
		DFOD F 0 A_Jump(224,1)
		Goto SeeJetpackRising
		DFOD F 0 A_Jump(128, "DuckMissile")
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_MissileAttack
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_MissileAttack
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_MissileAttack
		Goto See+3
	SeeJetpackRising:
		DFOD G 0 A_ChangeFlag("NOGRAVITY",1)
		DFOD G 0 A_ChangeFlag("NOPAIN",1)
		DFOD G 0 A_JumpIf(z-floorz>=128,"SeeJetpack")
		DFOD G 0 A_JumpIf(ceilingz-z<=56,"SeeJetpack")
		DFOD G 0 ThrustThingZ(0,3,0,0)
		DFOD G 2 A_Chase("","",0)
		Goto SeeJetpackRising+2
	SeeJetpack:
        TNT1 A 0 A_ChangeFlag("FLOAT",1)
		DFOD G 1
		DFOD G 1 A_Chase("","JetpackAttack",0)
		Goto SeeJetpack+1
	JetpackAttack:
		DFOD G 0 A_Jump(64,1)
		Goto SeeJetpack+1
		DFOD G 0 A_FaceTarget
		DFOD G 0 A_MissileAttack
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD H 0 A_Chase("","",0)
		DFOD HHHH 1 Bright A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD H 0 A_Chase("","",0)
		DFOD HHHH 1 Bright A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		DFOD G 0 ThrustThingZ(0,2,1,0)
		DFOD G 0 A_Chase("","",0)
		DFOD GG 1 A_CheckFloor("See")
		Loop 
	DuckMissile:
		DFOD Q 10 A_FaceTarget
		DFOD R 8 Bright A_FaceTarget
		DFOD Q 8 Bright A_MissileAttack
		DFOD R 8 Bright A_FaceTarget
		DFOD Q 8 Bright A_MissileAttack
		DFOD R 8 Bright A_FaceTarget
		DFOD Q 8 Bright A_MissileAttack
		Goto See+3
	Pain:
		DFOD I 3
		DFOD I 3 A_Pain
		Goto See+3
	Pain.Stunner:
		DFOD I 2 A_ChangeFlag("NoPain", 1)
		DFOD I 300 A_Pain
		DFOD I 0 A_ChangeFlag("NoPain", 0)
		goto See+3
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 3)
		DFOD I 6
		DFOD J 6 A_Scream
		DFOD K 6 A_Fall
		DFOD L 6
		DFOD M 3000
		DFOD M 2 A_FadeOut(0.1)
		wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 5)
        TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_PlaySound("duke/splat")
    	TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    	TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    	TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
        TNT1 A 5
		Stop
	}
}

ACTOR FodderLaser {
	Scale 0.4
	Radius 8
	Height 8
	Speed 25
	Damage (7)
	Projectile
	SeeSound "lizard/laser"
	DeathSound "duke/hit"
	States
	{
	Spawn:
		DLAS A 1 BRIGHT A_SpawnItem("FodderLaserTrail",0)
		Loop
	}
}

ACTOR FodderLaserTrail {
	Scale 0.4
	Radius 8
	Height 8
	+Nogravity
	States
	{
	Spawn:
		DLAS A 4 Bright
		Stop 
	}
}

ACTOR TeleFodder : DukeFodder
{
//$Category Stronghold_DukeNukem
	spawnid 161
	Translation "112:127=[255,75,25]:[68,12,4]"
	Health 60
	GibHealth -60
	Obituary "$STR_OB_TELEFODDER"
    Tag "$STR_MONSTERTAG_TELEFODDER"
	States
	{
	SeeInvisible:
		TNT1 A 0
		TNT1 A 0 A_Chase("GoVisible","",2)
		TNT1 A 1 A_Jump(10, "GoVisibleSee")
		Loop
	Missile:
		DFOD F 0 A_Jump(90, "GoInvisible")
		DFOD F 0 A_Jump(128, "DuckMissile")
		DFOD F 10 A_FaceTarget 
		DFOD F 6 Bright A_MissileAttack
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_MissileAttack
		DFOD F 10 A_FaceTarget
		DFOD F 6 Bright A_MissileAttack
		Goto See+3
	GoInvisible:
		DFOD S 20 A_UnSetShootable
		DFOD T 4
		DFOD TTTTTTTTT 1 A_FadeOut(0.1)
		TNT1 A 0 A_SpawnItem("TeleportFuzz",0,32)
		TNT1 A 0 A_PlaySound("lizard/teleport")
		TNT1 A 0 A_UnSetSolid
		Goto SeeInvisible
	GoVisible:
		TNT1 A 6 A_SpawnItem("TeleportFuzz",0,32)
		TNT1 A 0 A_PlaySound("lizard/teleport")
		TNT1 A 0 A_SetShootable
		TNT1 A 0 A_SetSolid
		TNT1 A 0 A_SetTranslucent(1.0,0)
		Goto Missile+2
	GoVisibleSee:
		TNT1 A 6 A_SpawnItem("TeleportFuzz",0,32)
		TNT1 A 0 A_PlaySound("lizard/teleport")
		TNT1 A 0 A_SetShootable
		TNT1 A 0 A_SetSolid
		TNT1 A 0 A_SetTranslucent(1.0,0)
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 4)
		goto Super::Death+1
	}
}

actor TeleportFuzz
{
  PROJECTILE
  Speed 0
  XScale 0.4
  YScale 0.5
  RenderStyle Translucent
  Alpha 0.756
  +ClientSideOnly
  States
  {
  Spawn:
  Death:
    DFOG ABCDE 1
    Stop
  }
}

actor ShrinkFuzz : TeleportFuzz
{
  Translation "80:111=112:127"
  RenderStyle Add
  SeeSound "duke/shrink"
}

// Duke babes - added by Jimmy91 :D

ACTOR BabeSpawner : RandomSpawner 20088
{
//$Category Stronghold_DukeNukem
  DropItem Babe1 255 1
  DropItem Babe2 255 1
  DropItem Babe3 255 1
  DropItem Babe4 255 1
  DropItem Babe5 255 1
  +FRIENDLY
}

ACTOR BabeBase
{
  +SOLID
  +SHOOTABLE
  +FRIENDLY
  +ISMONSTER
  +DONTMORPH
  +NOTAUTOAIMED
  +THRUSPECIES
  Species "Player"
  DamageFactor "Massacre", 0
  DamageFactor "MarineAlly",0
  DamageFactor "MarineAllyRocket",0
  DamageFactor "MarineAllyPlasma",0
  DamageFactor "MarineAllyRailgun",0
  DamageFactor "MarineAllyRepeater",0
  DamageFactor "MarineAllyFire",0
  DamageFactor "MarineAllyBFG",0
  DamageFactor "MarineAllyBFGSplash",0
  DamageFactor "MarineAllyIce",0
  DamageFactor "SmartBomb",0
  DamageFactor "CoreFriendly",0
  DamageFactor "CoreFriendlyFire",0
  DamageFactor "BossFactor",0
  DamageFactor "PlayerAlly",0
  DamageFactor "PlayerAllyRocket",0
  DamageFactor "PlayerAllyPlasma",0
  DamageFactor "PlayerAllyRailgun",0
  DamageFactor "PlayerAllyRepeater",0
  DamageFactor "PlayerAllyFire",0
  DamageFactor "PlayerAllyBFG",0
  DamageFactor "PlayerAllyBFGSplash",0
  DamageFactor "PlayerAllyIce",0
  DamageFactor "Stunner",0
  DamageFactor "Cyber2Factor", 0.5
  DamageFactor "Cyber2FactorExplosive", 0.5
  DamageFactor "Cyber2FactorFire", 0.5
  DamageFactor "STBoss", 0.02
  DamageFactor "STBossFactor", 0.25
  DamageFactor "SpiritBoss", 0.02
  DamageFactor "SpiritBossFactor", 0.25
  DamageFactor "Animate", 0.1
  DamageFactor "Bullet", 0.8
  DamageFactor "Melee", 0.8
  RadiusDamageFactor 0.5
  BloodType "DN3DBlood", "DN3DBlood", "AxeBlood"
  Health 500 // 300
  Height 64
  Radius 24
  Mass 9999999
  Scale 0.5
  DeathSound "duke/babedie"
  Tag "$STR_MONSTERTAG_BABE"
}

ACTOR Babe1 : BabeBase
{
//$Category Stronghold_DukeNukem
  //Health 600
  States
  {
  Spawn:
    BABE A 1
	Loop
  Death:
    TNT1 A 0 A_UnsetSolid
    TNT1 A 0 A_Scream
	TNT1 A 0 A_PlaySound("duke/splat")
    TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
    BABE E -1
    stop
  }
}

ACTOR Babe2 : BabeBase
{
//$Category Stronghold_DukeNukem
  Height 48
  States
  {
  Spawn:
    BABE B 1
	Loop
  Death:
    TNT1 A 0 A_UnsetSolid
    TNT1 A 0 A_Scream
	TNT1 A 0 A_PlaySound("duke/splat")
    TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
    BABE F -1
    stop
  }
}

ACTOR Babe3 : BabeBase
{
//$Category Stronghold_DukeNukem
  States
  {
  Spawn:
    BABE D 1
	Loop
  Death:
    TNT1 A 0 A_UnsetSolid
    TNT1 A 0 A_Scream
	TNT1 A 0 A_PlaySound("duke/splat")
    TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
    BABE H -1
    stop
  }
}

ACTOR Babe4 : BabeBase
{
//$Category Stronghold_DukeNukem
  //Health 200
  States
  {
  Spawn:
    BABE I 1
	Loop
  Death:
    TNT1 A 0 A_UnsetSolid
    TNT1 A 0 A_Scream
	TNT1 A 0 A_PlaySound("duke/splat")
    TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
    BABE J -1
    stop
  }
}

ACTOR Babe5 : BabeBase
{
//$Category Stronghold_DukeNukem
  States
  {
  Spawn:
    BABE C 1
	Loop
  Death:
    TNT1 A 0 A_UnsetSolid
    TNT1 A 0 A_Scream
	TNT1 A 0 A_PlaySound("duke/splat")
    TNT1 A 0 A_CustomMissile("Gibbage1",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage2",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage3",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage4",24,0,random(0,359),2,random(30,50))
    TNT1 A 0 A_CustomMissile("Gibbage5",24,0,random(0,359),2,random(30,50))
    TNT1 AAAAAAAAAA 0 A_CustomMissile("Gibbage6",24,0,random(0,359),2,random(30,50))
    BABE G -1
    stop
  }
}

// Duke gibs added by Jimmy91

actor DN3DBlood : Blood
{
  Scale 0.45
  +CORPSE
  States
  {
  Spawn:
    DNBD AAA 0
    DNBD A 0 A_Jump(128,2)
    DNBD A 0 A_SpawnItem("DN3DBloodSplat",0,0,0,1)
    DNBD A 1
    wait

  Crash:
    DNBD BCDEFGH 3
    Stop
  }
}

actor DN3DBloodSplat
{
  Radius 1
  Height 1
  +NOCLIP
  +NOBLOCKMAP
  +NOGRAVITY
  States
  {
  Spawn:
    DNBS ABCD 2
    Stop
  }
}

actor Gibbage1
{
  PROJECTILE
  Radius 2
  Height 2
  Speed 9
  VSpeed 24
  Mass 5
  Scale 0.65
  Gravity 0.5
  Decal BloodSplat
  -NOGRAVITY
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  +RIPPER
  +NOBLOCKMAP
  +CORPSE
  +ClientSideOnly
  +BLOODLESSIMPACT
  States
  {
  Spawn:
    GIB1 ABCD 6
    Loop
  Death:
    DNBD BCDEFGH 3
    Stop
  }
}

actor Gibbage2 : Gibbage1
{
  States
  {
  Spawn:
    GIB2 ABCD 6
    Loop
  }
}

actor Gibbage3 : Gibbage1
{
  States
  {
  Spawn:
    GIB3 ABCD 6
    Loop
  }
}

actor Gibbage4 : Gibbage1
{
  States
  {
  Spawn:
    GIB4 ABCD 6
    Loop
  }
}

actor Gibbage5 : Gibbage1
{
  States
  {
  Spawn:
    GIB5 ABCD 6
    Loop
  }
}

actor Gibbage6 : Gibbage1
{
  States
  {
  Spawn:
    DNBD A 1
    Loop
  }
}

actor Scrap1 : Gibbage1
{
  Decal ""
  Scale 0.5
  States
  {
  Spawn:
    SCRP ABCD 6
    Loop
  Death:
    SCRP A 0
    Stop
  }
}

actor Scrap2 : Scrap1
{
  States
  {
  Spawn:
    SCRP EFGHIJKLM 6
    Loop
  }
}
