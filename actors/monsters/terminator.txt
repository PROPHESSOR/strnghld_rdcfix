///////////////////////////////////////////////////////////////////
//Terminator
//created by: Vader
///////////////////////////////////////////////////////////////////

ACTOR Terminator 30666
{
//$Category Stronghold_Monsters
PainChance "Stunner", 128
SpawnID 231
Health 7000
Radius 24
Height 64
Mass 1500
Speed 12
PainChance 8
scale 1.2
MONSTER
Obituary "$STR_OB_TERMINATOR"
Tag "$STR_MONSTERTAG_TERMINATOR"
+FLOORCLIP
+BOSS
+MISSILEMORE
+MISSILEEVENMORE
+DONTHURTSPECIES
+NORADIUSDMG
+DONTMORPH
//+DONTRIP
Missiletype Grenadeblast
Damagefactor "RedBlast", 0
MeleeRange 192
SeeSound "monster/termsit"
PainSound "monster/termpin"
DeathSound "monster/termdth"
ActiveSound "Terminator/termact"
States
   {
   Spawn:
       TERM AB 10 A_Look
       Loop
      See:
       TERM A 0 A_PlaySound("Terminator/terstepA")
       TERM AABB 4 A_Chase
       TERM C 0 A_PlaySound("Terminator/terstepA")
       TERM CCDD 4 A_Chase
       Loop
   Pain:
       TERM L 3
       TERM L 3 A_Pain
       Goto See
   Pain.Stunner:
       TERM L 2 A_ChangeFlag("NoPain", 1)
       TERM L 300 A_Pain
       TERM L 0 A_ChangeFlag("NoPain", 0)
       goto See
   Melee:
       Goto Missile2+1
   Missile:
       TERM E 0 A_JumpIfCloser(512, "Missile2")
       TERM E 0 A_Jump(170, "Missile2")
       TERM E 5 A_FaceTarget
       TERM F 5 Bright A_CustomMissile("TerminatorBlaster",38,15,Random(Random(20,Random(30,40)),Random(-20,(Random(-30,-40))))/10.0)
       TERM F 0 A_Spidrefire
       Goto Missile+2
    Missile2:
       TERM G 0 A_Jump(128, "Missile3")
       TERM G 2 A_Facetarget
       TNT1 A 0 A_CustomBulletAttack (24, 12, 6, 3, "Terminatorpuff")
       TERM H 2 Bright A_PlayWeaponSound("Terminator/tershotB")
       TERM G 2 A_Facetarget
       TNT1 A 0 A_CustomBulletAttack (24, 12, 6, 3, "Terminatorpuff")
       TERM I 2 Bright A_PlayWeaponSound("Terminator/tershotB")
       TERM G 1 A_Spidrefire
       Goto Missile2+1
    Missile3:
       TERM J 0 A_Jump(192,"Missile5")
       TERM K 0 A_JumpIfCloser(512,"Missile4")
       TERM J 8 A_Facetarget
       TERM K 8 Bright A_CustomMissile("Redgrenade", 38, -15, 0, 0)
       Goto See
    Missile4:
       TERM J 6 A_Facetarget
       TERM K 6 Bright A_CustomMissile("Redstar", 38, -15, 0, 0) // Random(2,-8)
       TERM J 6 A_Facetarget
       TERM K 6 Bright A_CustomMissile("Redstar", 38, -15, 0, 0) // Random(8,-2)
    Missile5:
       TERM J 6 A_Facetarget
       TERM K 6 Bright A_CustomMissile("Redstar", 38, -15, 0, 0)
       Goto See
   Death:
       TNT1 A 0 A_GiveToTarget("AwardItem", 60)
       TERM M 6 A_Scream
       TERM N 4
       TERM OPQ 4 Bright
       TERM R 0 Bright A_SpawnItemEx("TerminatorHead", 15, 0, 90, 10, 0, 0, -170, 128)
       TERM R 0 Bright A_SpawnItemEx("TerminatorShoulder", 60, 0, 75, 8, 0, 0, -70, 128)
       TERM R 4 Bright A_SpawnItemEx("TerminatorArm", 60, 0, 0, 3, 0, 0, -90, 128)
       TERM STUV 4 Bright
       TERM W 4
       TERM X 6 A_Fall
       TERM Y 6
       TERM Z 6 A_PlaySound("Terminator/terstepA")
       TERM [ 3000
       TERM [ 2 A_FadeOut(0.1)
       wait
   }
}

ACTOR TerminatorBlaster
{
   Radius 8
   Height 12
   Speed 35 //Too Slow?
   FastSpeed 45 //More like NOT SLOW ENOUGH.
   Damage 8
   DamageType "Rocket"
   SpawnID 251
   PROJECTILE
   RENDERSTYLE ADD
   alpha 0.85
   SeeSound "monster/brufir"
   DeathSound "weapons/hellex"
   Decal RevenantScorch
   +FORCERADIUSDMG
   States
   {
   Spawn:
      BLST A 1 Bright
      loop
   Death:
      BLST B 6 Bright A_Explode(16,128,1)
      BLST CDEFGHIJKL 3 Bright
      stop
   }
}

actor Redstar
{
    Radius 10
    Height 20
    Speed 30 //Ohhhhhhhhhhhhhhhh...
    FastSpeed 45 //I've been having way too much fun making this a hard enemy, aren't I...
    Damage 4
    Scale 0.6
    +SEEKERMISSILE
    +FORCERADIUSDMG
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.8
    SeeSound "Terminator/tershotA"
    DeathSound "imp/shotx"
    Decal CacoScorch
    States
    {
    Spawn:
        STAX A 0 Bright A_SeekerMissile (10,30) //Welp.
	STAX AAAA 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        TNT1 A 0 A_SeekerMissile (10,30) //Good luck dodging.
	STAX BBBB 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        TNT1 A 0 A_SeekerMissile (10,30) //I've tested. You're gonna need it.
	STAX CCCC 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        TNT1 A 0 A_SeekerMissile (10,30) //Grenade Trails make it harder to judge too.
	STAX DDDD 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
        Loop
    Death:
        STAX E 3 Bright A_Explode(20,192,1)
        STAX FGHIJKLMNO 3 Bright
        Stop
    }
}

actor Redgrenade //More like Red Missile now. Heh heh.
{
    Radius 6
    Height 12
    Speed 30
    FastSpeed 40
    Damage 4
    DamageType "RedBlast"
    OBITUARY "$STR_OB_TERMINATORGRENADE"
    PROJECTILE
    //Heh heh. Players won't know what hit them.
    //+DoomBounce
    //-NoGravity
    +EXTREMEDEATH
    RENDERSTYLE ADD
    ALPHA 0.8
    SeeSound "Terminator/tershotA"
    DeathSound "imp/shotx"
    Decal CacoScorch
    States
    {
    Spawn:
        TGND A 0 Bright
    	TGND AAAABBBBCCCC 1 Bright A_SpawnItemEx("grenadetrail",0,0,0,0,0,0,0,0)
    	loop
    Death:
    	STAX E 3 Bright
    	TNT1 A 0 A_custommissile("grenadeblast",0,0,0,6,0)
    	TNT1 A 0 A_custommissile("grenadeblast",0,0,20,6,0)
    	TNT1 A 0 A_custommissile("grenadeblast",0,0,40,6,0)
    	TNT1 A 0 A_custommissile("grenadeblast",0,0,60,6,0)
    	TNT1 A 0 A_custommissile("grenadeblast",0,0,80,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,100,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,120,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,140,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,160,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,180,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,200,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,220,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,240,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,260,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,280,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,300,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,320,6,0)
        TNT1 A 0 A_custommissile("grenadeblast",0,0,340,6,0)
        STAX FGHIJKLMNO 3 Bright
        Stop
    }
}

ACTOR Grenadetrail
{       
   	PROJECTILE
   	+NOCLIP
    	Renderstyle ADD
	Alpha 0.6
	States
	{   
	Spawn:
	    TGTR ABCDEFGHI 4 Bright
	    Stop
	}
}  

actor Grenadeblast
{
    Radius 6
    Height 12
    Speed 6
    Damage (Random(1,6)*3)
    OBITUARY "$STR_OB_TERMINATORGRENADE"
    DamageType "RedBlast"
    PROJECTILE
    RENDERSTYLE ADD
    +Ripper
    +ExtremeDeath
    ALPHA 0.8
    States
    {
    Spawn:
    	TGBL ABCDEFGHIJK 2 Bright
        stop
    }
}

ACTOR TerminatorPuff : BulletPuff
{
    Renderstyle ADD
	Alpha 0.75
	scale 0.75
    DamageType "Bullet"
    Decal BulletChip
	States
	{   
	Spawn:
	Melee:
	    TPUF A 2 Bright
	    TPUF A 0 A_Jump(128,8)
	    TPUF BCDEFGH 2 Bright
	    Stop
	    TPUF IJKLMNO 2 Bright 
	    Stop
	}
}

ACTOR TerminatorArm
{
	Radius 12
	Height 15
	Speed 3
	Mass 1000
	scale 1.2
	+DOOMBOUNCE
      States
	{
	Spawn:
         TARM A 5
         TARM B 5 A_Fall 
         TARM CDE 5
         TARM F 3000
         TARM F 2 A_FadeOut(0.1)
         wait
	}
}

actor TerminatorHead
{
    Radius 6
    Height 8
    Speed 10
    Mass 1000
    +DOOMBOUNCE
    +DROPOFF
    +Missile
    scale 1.2
    
    States
    {
    Spawn:
    	THAD DEFGHABC 2
        loop
    Death:
    	THAD I 3000
        THAD I 2 A_FadeOut(0.1)
        wait
    }
}

actor TerminatorShoulder
{
    Radius 8
    Height 8
    Speed 8
    Mass 1000
    +DOOMBOUNCE
    +DROPOFF
    +Missile
    scale 1.2
    
    States
    {
    Spawn:
    	TSHO ABCDEFGH 2
        loop
    Death:
    	TSHO I 3000
        TSHO I 2 A_FadeOut(0.1)
        wait
    }
}
