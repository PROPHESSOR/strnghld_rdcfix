ACTOR BruiserDemon 3333
{
//$Category Stronghold_Monsters
    PainChance "Stunner", 120
	SpawnID 234
    Health 1500
	Radius 24
	Height 64
	Mass 2000
	Speed 8
	PainChance 15
	Monster
	+BOSS
	+FLOORCLIP
	+LOOKALLAROUND
	+MISSILEEVENMORE
	+NORADIUSDMG
    //+DONTRIP
	MissileType BruiserBall
	SeeSound "superbaron/scream"
	PainSound "superbaron/pain"
	DeathSound "superbaron/death"
	ActiveSound "superbaron/act"
	MeleeSound "baron/melee"
    DamageFactor "BruiserAttack",0
	Obituary "$STR_OB_BRUISERDEMON"
	HitObituary "$STR_OB_BRUISERDEMONHIT"
    Tag "$STR_MONSTERTAG_BRUISERDEMON"
	MeleeDamage 20
	Scale 1.25
	States
	{
	Spawn:
		BRUS AB 10 Bright A_Look 
		Loop
	See:
		BRUS AABBCCDD 3 Bright A_Chase
		Loop
	Melee:
		BRUS EF 6 Bright A_FaceTarget
		BRUS G 6 Bright A_ComboAttack
		Goto See
	Missile:
		BRUS E 0 Bright A_Jump(144, 8)
		BRUS EF 6 Bright A_FaceTarget
		BRUS G 6 Bright A_MissileAttack
		BRUS G 0 Bright A_Jump(96, 1)
		Goto See
		BRUS HI 6 Bright A_FaceTarget
		BRUS J 6 Bright A_MissileAttack
		Goto See
		BRUS E 0 Bright A_Jump(96, 20)
		BRUS EF 6 Bright A_FaceTarget
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,-40,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,-30,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,-20,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,-10,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,0,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,10,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,20,0)
		BRUS G 0 Bright A_Jump(96, 1)
		Goto See
		BRUS HI 6 Bright A_FaceTarget
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,40,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,30,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,20,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,10,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,0,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,-10,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,-20,0)
		Goto See
		BRUS KL 6 Bright A_FaceTarget
		TNT1 A 0 A_Custommissile("BruiserFireSpawner",16,0,-15,0)
		TNT1 A 0 A_Custommissile("BruiserFireSpawner",16,0,0,0)
		BRUS M 6 Bright A_Custommissile("BruiserFireSpawner",16,0,15,0)
		Goto See
	Pain:
		BRUS N 5 bright light("BruiserDemon") A_Pain
 		Goto See
        Pain.Stunner:
                BRUS N 2 bright light("BruiserDemon") A_ChangeFlag("NoPain", 1)
                BRUS N 300 bright light("BruiserDemon") A_Pain
                BRUS N 0 A_ChangeFlag("NoPain", 0)
                goto See
	Death:
                TNT1 A 0 A_GiveToTarget("AwardItem", 27)
		BRUD A 6 Bright A_Scream
		BRUD BCDEFG 4 Bright
		TNT1 A 0 A_Fall
		BRUD HIJKLMNOP 4 Bright
		BRUD QRSTUV 4
		BRUD W 3000
        BRUD W 2 A_FadeOut(0.1)
		wait
	}
}

ACTOR BruiserBall
{
	Radius 16
	Height 16
	Speed 18
	Damage 16
	Projectile
	+RANDOMIZE
	DamageType "BruiserAttack"
	RenderStyle "ADD"
	Alpha 0.9
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
    Decal Scorch
	States
	{
	Spawn: 
		BRBA AABB 2 BRIGHT A_SpawnItemEx("BruiserBallTrail",0,0,0,0,0,0,0,128)
		Loop
	Death:
		BRBA KLMNOPQRSTUVWX 3 Bright
		Stop
	}
}

ACTOR BruiserBall2
{
	Radius 8
	Height 8
	Speed 10
	Damage 8
	Projectile
	+RANDOMIZE
	DamageType "BruiserAttack"
	RenderStyle "ADD"
	Alpha 0.9
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
    Decal BaronScorch
	States
	{
	Spawn: 
		BRB2 AB 6 BRIGHT 
		Loop
	Death:
		BRB2 CDEFGHI 3 Bright
		Stop
	}
}

ACTOR BruiserFireSpawner
{
	Radius 8
	Height 8
	Speed 12
	Damage (0)
	+RIPPER
	+FLOORHUGGER
	+BLOODLESSIMPACT
	DamageType "BruiserAttack"
	Projectile
	//MissileType BruiserFire
	Missileheight 0
	Obituary "$STR_OB_BRUISERDEMONFIRE"
	States
	{
	Spawn: 
		TNT1 A 3 
  		TNT1 A 6 A_CustomMissile("BruiserFire", 32, 0, 0, 4)
		Loop
	Death:	
		TNT1 A 6
		Stop
	}
}

actor BruiserBallTrail
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.70
    +NOCLIP
    States
    {
    Spawn:
     	BRBA CDEFGHIJ 4 Bright
     	Stop
    }
}

ACTOR BruiserFire
{
	Radius 1
    	Height 1
    	Speed 0
    	Damage 20
    	PROJECTILE
    	RENDERSTYLE ADD
    	ALPHA 0.9
    	+NOCLIP
	+FLOORHUGGER
        DamageType "BruiserAttack"
	SeeSound "weapons/rocklx"
	Obituary "$STR_OB_BRUISERDEMONFIRE"
    	States
    	{
    	Spawn:
  		XXBF AB 3 Bright
    		XXBF C 3 Bright A_Explode(48,128,1)
		XXBF DEFGHIJKLMNOPQRST 3 Bright
     		Stop       
   	}
}
