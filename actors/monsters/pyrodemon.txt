ACTOR PyroDemon : StrongholdBaronOfHell 30105
{
	Species "PyroDemon"
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	Health 2500
	PainChance 20
	OBITUARY "%o was burnt to a crisp by a pyro demon."
	HITOBITUARY "%o was melted by a pyro demon."
	Bloodcolor "75 25 00"
	+NoTarget
	+FloorClip
	+MissileMore
	+NoRadiusDMG
	+Boss
	+DONTHARMCLASS
	SEESOUND "pyro/see"
	PAINSOUND "pyro/pain"
	DEATHSOUND "pyro/death"
	ACTIVESOUND "pyro/idle"
	RenderStyle Translucent
	states
	{
	Spawn:
		PYRO AB 10 A_Look
		Loop
	See:
		PYRO A 1 A_Chase
		PYRO AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO A 1 A_Chase
		PYRO AA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO A 0 A_SpawnItemEx("WalkFire")
		PYRO B 1 A_Chase
		PYRO BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO B 1 A_Chase
		PYRO BB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO A 0 A_SpawnItemEx("WalkFire")
		PYRO C 1 A_Chase
		PYRO CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO C 1 A_Chase
		PYRO CC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO A 0 A_SpawnItemEx("WalkFire")
		PYRO D 1 A_Chase
		PYRO DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO D 1 A_Chase
		PYRO DD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PYRO A 0 A_SpawnItemEx("WalkFire")
		Loop
	Melee:
		Stop
	Missile:
		PYRO B 0 A_Jump(32,29)
		PYRO B 0 A_Jump(32,20)
		PYRO EF 8 BRIGHT A_FaceTarget
		PYRO G 0 A_CustomMissile("FireShot",28,0,0,0)
		PYRO G 0 A_CustomMissile("FireShot",28,0,-4,0)
		PYRO G 3 BRIGHT A_CustomMissile("FireShot",28,0,4,0)
		PYRO H 3 BRIGHT
		PYRO H 0 A_Jump(32,8)
		PYRO IJ 8 BRIGHT A_FaceTarget
		PYRO K 0 A_CustomMissile("FireShot",28,0,0,0)
		PYRO K 0 A_CustomMissile("FireShot",28,0,-4,0)
		PYRO K 3 BRIGHT A_CustomMissile("FireShot",28,0,4,0)
		PYRO L 3 BRIGHT
		PYRO L 0 A_Jump(32,1)
		Goto See
		PYRO MN 8 BRIGHT A_FaceTarget
		PYRO O 4 BRIGHT A_CustomMissile("PyroBlast",28,0,0,0)
		PYRO P 4 BRIGHT
		Goto See
		PYRO MN 8 BRIGHT A_FaceTarget
		PYRO O 4 BRIGHT A_CustomMissile("RainCometNull",28,0,0,0)
		PYRO O 4 BRIGHT A_CustomMissile("RainCometNull",28,0,90,0)
		PYRO O 4 BRIGHT A_CustomMissile("RainCometNull",28,0,180,0)
		PYRO O 4 BRIGHT A_CustomMissile("RainCometNull",28,0,270,0)
		PYRO P 4 BRIGHT
		Goto See
		PYRO O 1 A_FaceTarget
		PYRO O 0 A_CustomMissile("EscapeExplosion",28,10,0,0)
		PYRO O 0 A_Mushroom("Pyroball",16)
		PYRO O 3
		PYRO P 3 BRIGHT
		PYRO A 0 A_UnSetShootable
		PYRO A 1 A_PlaySound("pyro/tele")
		PYRO AAAAAAAAA 1 A_FadeOut(.1,0)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander
		TNT1 A 0 A_Jump(128,75)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Chase("","")
		PYRO A 1 A_PlaySound("pyro/tele")
		PYRO AAAAAAAAAA 1 A_FadeIn
		PYRO A 0 A_CustomMissile("EscapeExplosion",28,10,0,0)
		PYRO O 0 A_Mushroom("Pyroball",16)
		PYRO A 0 A_SetShootable
		Goto See
	Pain:
		PYRO Q 2
		PYRO Q 2 A_Pain
		Goto See
	Pain.Stunner:
		PYRO Q 2 A_ChangeFlag("NoPain",1)
		PYRO Q 300 A_Pain
		PYRO Q 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("AwardPoints",0)
		PYRO R 20 BRIGHT A_Scream
		PYRO ST 20 BRIGHT
		PYRO U 20 BRIGHT A_NoBlocking
		PYRO VW 6 BRIGHT
		PYRO X 6 BRIGHT
		PYRO Y 3 BRIGHT
		PYRO Z 3000
		PYRO Z 1 A_FadeOut(0.05)
		Wait
	Raise:
		Stop
	}
}

ACTOR FireShot
{
	Radius 3
	Height 3
	Speed 15
	Damage 2
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.67
	DamageType Fire
	SeeSound "pyro/flame"
	DeathSound "pyro/flamepoof"
	States
	{
	Spawn:
		FSHT A 1 Bright LIGHT("FireShot1") A_SpawnItemEx("FireTail")
		Loop
	Death:
		FSHT B 3 Bright LIGHT("FireShot1") A_Explode
		FSHT C 3 Bright LIGHT("FireShot2")
		FSHT D 3 Bright LIGHT("FireShot3")
		FSHT E 3 Bright LIGHT("FireShot4")
		Stop
	}
}

actor FireTail
{
	+CLIENTSIDEONLY
	PROJECTILE
	RENDERSTYLE ADD
	+NOCLIP
	ALPHA 0.4
	States
	{
	Spawn:
		FRTB ABCDEFGHI 1 Bright
		Stop
	}
}

ACTOR EscapeExplosion
{
	SeeSound "pyro/explosion"
	Radius 8
	Height 56
	States
	{
	Spawn:
		PYXP A 3 BRIGHT LIGHT("EscapeExplosion1")
		PYXP B 3 BRIGHT LIGHT("EscapeExplosion2")
		PYXP C 3 BRIGHT LIGHT("EscapeExplosion3") A_Explode(50,192)
		PYXP D 3 BRIGHT LIGHT("EscapeExplosion4")
		PYXP E 3 BRIGHT LIGHT("EscapeExplosion5")
		PYXP F 3 BRIGHT LIGHT("EscapeExplosion6")
		PYXP G 3 BRIGHT LIGHT("EscapeExplosion7")
		PYXP H 3 BRIGHT LIGHT("EscapeExplosion8")
		PYXP I 3 BRIGHT LIGHT("EscapeExplosion9")
		PYXP J 3 BRIGHT LIGHT("EscapeExplosion10")
		PYXP K 3 BRIGHT LIGHT("EscapeExplosion11")
		PYXP L 3 BRIGHT LIGHT("EscapeExplosion12")
		PYXP M 3 BRIGHT LIGHT("EscapeExplosion13")
		PYXP N 3 BRIGHT LIGHT("EscapeExplosion14")
		PYXP O 3 BRIGHT LIGHT("EscapeExplosion15")
		PYXP P 3 BRIGHT LIGHT("EscapeExplosion16")
		PYXP Q 3 BRIGHT LIGHT("EscapeExplosion17")
		PYXP R 3 BRIGHT LIGHT("EscapeExplosion18")
		PYXP S 3 BRIGHT LIGHT("EscapeExplosion19")
		PYXP T 3 BRIGHT LIGHT("EscapeExplosion20")
		PYXP U 3 BRIGHT LIGHT("EscapeExplosion21")
		Stop
	}
}

ACTOR PyroBall
{
	Radius 10
	Height 8
	Speed 15
	SEESOUND "pyro/bomb"
	DEATHSOUND "pyro/bombhit"
	PROJECTILE
	RENDERSTYLE Add
	Alpha 0.7
	-NoGravity
	BounceType Doom
	Gravity .125
	DamageType Fire
	+SeekerMissile
	states
	{
	Spawn:
		RBAL A 0
		RBAL A 0 A_AlertMonsters
		RBAL AB 4 BRIGHT LIGHT("PyroBall")
		Goto Spawn+2
	Death:
		RBAL D 4 BRIGHT LIGHT("PyroBall2")
		RBAL E 4 BRIGHT LIGHT("PyroBall3")
		RBAL FGHI 4 BRIGHT LIGHT("PyroBall4")
		Stop
	}
}

ACTOR FirebombExplosion1
{
	Radius 11
	Height 8
	Speed 18
	Damage 6
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	Obituary "%o was killed by a Firebomb explosion."
	States
	{
	Spawn:
		FRME A -1 Bright LIGHT("FirebombExplosion11")
		Loop
	Death:
		FRME A 0 A_PlaySound("pyro/blastexplosion")
		FRME A 3 Bright LIGHT("FirebombExplosion11") A_Explode
		FRME B 1 Bright LIGHT("FirebombExplosion12") A_Mushroom("Pyroball",8)
		FRME B 2 Bright LIGHT("FirebombExplosion12") A_CustomMissile("FirebombExplosion2",25,0,180)
		FRME C 3 Bright LIGHT("FirebombExplosion13")
		FRME D 3 Bright LIGHT("FirebombExplosion14")
		FRME E 3 Bright LIGHT("FirebombExplosion15")
		FRME F 3 Bright LIGHT("FirebombExplosion16")
		FRME G 3 Bright LIGHT("FirebombExplosion17")
		FRME H 3 Bright LIGHT("FirebombExplosion18")
		FRME I 3 Bright LIGHT("FirebombExplosion19")
		FRME J 3 Bright LIGHT("FirebombExplosion110")
		FRME K 3 Bright LIGHT("FirebombExplosion111")
		FRME L 3 Bright LIGHT("FirebombExplosion112")
		FRME M 3 Bright LIGHT("FirebombExplosion113")
		FRME N 3 Bright LIGHT("FirebombExplosion114")
		FRME O 3 Bright LIGHT("FirebombExplosion115")
		FRME P 3 Bright LIGHT("FirebombExplosion116")
		FRME Q 3 Bright LIGHT("FirebombExplosion117")
		FRME R 3 Bright LIGHT("FirebombExplosion118")
		FRME S 3 Bright LIGHT("FirebombExplosion119")
		Stop
	}
}

ACTOR FirebombExplosion2 : FirebombExplosion1
{
	States
	{
	Death:
		FRME A 0 A_PlaySound("pyro/blastexplosion")
		FRME A 3 Bright A_Explode(128,112)
		FRME B 1 Bright
		FRME B 2 Bright A_CustomMissile("FirebombExplosion3",20,0,180)
		Goto Super::Death+4
	}
}

ACTOR FirebombExplosion3 : FirebombExplosion1
{
	Speed 14
	Damage 5
	States
	{
	Death:
		FRME A 0
		FRME A 0 A_PlaySound("pyro/blastexplosion")
		FRME A 3 Bright A_Explode(128,96)
		FRME B 3 BRIGHT
		Goto FirebombExplosion1::Death+4
	}
}

ACTOR PyroBlast
{
	PROJECTILE
	+SeekerMissile
	+NoGravity
	RENDERSTYLE Add
	Alpha 0.4
	SEESOUND "pyro/blast"
	DEATHSOUND "pyro/flamepoof"
	Speed 17
	Height 10
	Radius 10
	states
	{
	Spawn:
		SBLL ABCDEFGHIJ 1 BRIGHT LIGHT("PyroBlast1") A_SpawnItemEx("FireTail")
		SBLL A 0 A_SeekerMissile(0,2)
		Goto Spawn+1
	Death:
		SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,45)
		SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,135)
		SBLL K 0 A_CustomMissile("FirebombExplosion1",1,0,225)
		SBLL K 1 BRIGHT LIGHT("PyroBlast2") A_CustomMissile("FirebombExplosion1",1,0,315)
		SBLL L 1 BRIGHT LIGHT("PyroBlast3")
		SBLL M 1 BRIGHT LIGHT("PyroBlast4")
		SBLL N 1 BRIGHT LIGHT("PyroBlast5")
		SBLL O 1 BRIGHT LIGHT("PyroBlast6")
		SBLL P 1 BRIGHT LIGHT("PyroBlast7")
		SBLL Q 1 BRIGHT LIGHT("PyroBlast8")
		SBLL R 1 BRIGHT LIGHT("PyroBlast9")
		SBLL S 1 BRIGHT LIGHT("PyroBlast10")
		SBLL TUVW 1 BRIGHT LIGHT("PyroBlast11")
		Stop
	}
}

ACTOR WalkFire
{
	+CLIENTSIDEONLY
	+NOCLIP
	+MISSILE
	+DROPOFF
	+RANDOMIZE
	Scale 1.5
	Speed 1
	RENDERSTYLE ADD
	ALPHA 0.67
	States
	{
	Spawn:
		TNT1 A 8
		FRTF A 3 Bright LIGHT("WalkFire")
		FRTF B 3 Bright LIGHT("WalkFire2")
		FRTF C 3 Bright LIGHT("WalkFire3")
		FRTF C 0 A_Jump(191,4)
		FRTF C 0 A_CustomMissile("WalkFire",0,24)
		FRTF C 0 A_CustomMissile("WalkFire",0)
		FRTF C 0 A_CustomMissile("WalkFire",0,-24)
		FRTF D 3 BRIGHT LIGHT("WalkFire4")
		FRTF E 3 BRIGHT LIGHT("WalkFire5")
		FRTF F 3 BRIGHT LIGHT("WalkFire6")
		FRTF G 3 BRIGHT LIGHT("WalkFire7")
		FRTF H 3 BRIGHT LIGHT("WalkFire8")
		FRTF I 3 BRIGHT LIGHT("WalkFire9")
		FRTF J 3 BRIGHT LIGHT("WalkFire10")
		FRTF K 3 BRIGHT LIGHT("WalkFire11")
		FRTF L 3 BRIGHT LIGHT("WalkFire12")
		FRTF M 3 BRIGHT LIGHT("WalkFire13")
		FRTF N 3 BRIGHT LIGHT("WalkFire14")
		FRTF O 3 Bright
		Stop
	}
}

ACTOR RainCometNull
{
	Radius 0
	Height 0
	Speed 8
	PROJECTILE
	+FloorClip
	+Ripper
	BounceType Doom
	+CeilingHugger
	states
	{
	Spawn:
		ZAP5 A 0 NODELAY A_Jump(251,1)
		Stop
		ZAP5 AAAAAA 1 LIGHT("RainCometNull") A_Wander
		ZAP5 A 8 LIGHT("RainCometNull") A_CustomMissile("FallingComet",-24,random(0,128),random(0,360),2,random(1,89))
		Loop
	}
}

ACTOR FallingComet
{
	Radius 6
	Height 8
	Speed 5
	Damage 5
	Scale .55
	PROJECTILE
	SEESOUND "pyro/comet"
	DEATHSOUND "pyro/cometdeath"
	-NoGravity
	+FloorClip
	+SpawnCeiling
	states
	{
	Spawn:
		COMT AAAABBBBCCCC 1 BRIGHT LIGHT("Comet1") A_SpawnItemEx("FireTail")
		Loop
	Death:
		COMT D 3 BRIGHT LIGHT("Comet2") A_SpawnItemEx("CometDeathGlow")
		COMT E 3 BRIGHT LIGHT("Comet3") A_Explode(20,128,0)
		COMT F 3 BRIGHT LIGHT("Comet4") A_SetTranslucent(.7,1)
		COMT G 3 Bright LIGHT("Comet5")
		COMT H 3 Bright LIGHT("Comet6")
		COMT I 3 Bright LIGHT("Comet7")
		Stop
	}
}

actor CometDeathGlow : FireTail
{
	Scale 2.0
	Alpha 0.6
	States
	{
	Spawn:
		FRTB ABCDEFGHI 3 Bright
		Stop
	}
}