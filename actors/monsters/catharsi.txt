ACTOR Catharsi : StrongholdDoomImp 29123
{
	//$Category Stronghold_Monsters
	Species "Catharsi"
	OBITUARY "%o was blasted by a catharsi."
	Health 120
	PainChance 140
	SEESOUND "catharsi/sit"
	PAINSOUND "catharsi/pain"
	DEATHSOUND "catharsi/death"
	var int user_theta;
	states
	{
	Spawn:
		SDDM AB 10 A_Look
		Loop
	Melee:
		Stop
	Missile:
		SDDM E 4
		SDDM E 10 A_FaceTarget
		SDDM E 0 A_Jump(128,"Missile2")
		SSDM E 0 A_SetUserVar("user_theta",-4)
		SDDM F 2 BRIGHT LIGHT("Catharsi") A_CustomMissile("CatharsiBall",30,8,user_theta)
		SDDM E 2 A_SetUserVar("user_theta",user_theta+4)
		SDDM G 0 A_JumpIf(user_theta>16,"See")
		Goto Missile+4
	Missile2:
		SSDM E 0 A_SetUserVar("user_theta",4)
		SDDM F 2 BRIGHT LIGHT("Catharsi") A_CustomMissile("CatharsiBall",30,8,user_theta)
		SDDM E 2 A_SetUserVar("user_theta",user_theta-4)
		SDDM G 0 A_JumpIf(user_theta<-16,"See")
		Goto Missile2+1
	Pain:
		SDDM G 3
		SDDM G 3 A_Pain
		Goto See
	Pain.Stunner:
		SDDM G 3 A_ChangeFlag("NoPain",1)
		SDDM G 300 A_Pain
		SDDM G 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		SDDM H 5 ACS_NamedExecuteAlways("AwardPoints",0)
		SDDM I 5 A_Scream
		SDDM J 5 A_NoBlocking
		SDDM J 0 A_SpawnItem("CatharsiDetonator",0,30)
		SDDM KLM 5
		SDDM N 3000
		SDDM N 1 A_FadeOut(0.05)
		Wait
	XDeath:
		SDDM O 5 ACS_NamedExecuteAlways("AwardPoints",0,1)
		SDDM P 5 A_XScream
		SDDM Q 5 A_NoBlocking
		SDDM RSTUV 5
		SDDM V 3000
		SDDM V 1 A_FadeOut(0.05)
		Wait
	Raise:
		SDDM NMLKJIH 5
		Goto See
	}
}

ACTOR CatharsiBall
{
	Radius 6
	Height 8
	Speed 15
	Damage 3
	RENDERSTYLE Add
	SEESOUND "catharsi/shotsit"
	DEATHSOUND "catharsi/shotdth"
	PROJECTILE
	+Randomize
	states
	{
	Spawn:
		SDFB A -1 BRIGHT LIGHT("CatharsiShot1")
		Loop
	Death:
		SDFB B 6 BRIGHT LIGHT("CatharsiShot2")
		SDFB C 6 BRIGHT LIGHT("CatharsiShot3")
		SDFB D 6 BRIGHT LIGHT("CatharsiShot4")
		SDFB E 6 BRIGHT LIGHT("CatharsiShot5")
		stop
	}
}

ACTOR CatharsiDetonator
{
	Radius 6
	Height 8
	SEESOUND "catharsi/shotsit"
	DEATHSOUND "catharsi/shotdth"
	+SHOOTABLE
	+SOLID
	+ACTIVATEMCROSS
	+CANPASS
	+FloatBob
	+Randomize
	+NoGravity
	+NoBlood
	var int user_theta;
	states
	{
	Spawn:
		THRM A 4 A_PlaySound("catharsi/thermtick")
		THRM B 16 A_SetUserVar("user_theta",user_theta+1)
		THRM A 0 A_JumpIf(user_theta==5,1)
		Loop
		THRM A 0 A_SetUserVar("user_theta",0)
		THRM A 0 A_Die
	Death:
		SDFB B 0 A_CustomMissile("CatharsiBall",4,0,user_theta,CMF_AIMDIRECTION)
		SDFB B 0 A_SetUserVar("user_theta",user_theta+20)
		SDFB B 0 A_JumpIf(user_theta==360,1)
		Loop
		SDFB BCDE 6 BRIGHT
		Stop
	}
}